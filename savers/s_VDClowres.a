
	!src "godotlib.lib"	; Label-Library einbinden
	*= $c000		; generelle Startadresse für alle Module
	!ct pet		; Texte werden in PETSCII assembliert
;	!sl "labels.txt"

; -----------------------------------------
;
; svr.VDClowres
;
; 1.00, 14.01.24, first release
; 1.01, 24.01.24, added requester to choose filetype
;
;       creates 8000 bytes for a 160x100 pixel
;       cga-style vdc image (like Basic8 mode 1)
;       or 320x100 pixel double width scroll image
;
; -----------------------------------------

; ----------------------------------------- Label-Definitionen

	pport	= $01		; Systemlabels

	cnt0	= $30
	cnt1	= $31
	cnt2	= $32
	cnt3	= $33
	adcnt	= $34		; Zähler für activity
	ymerk	= $35
	byte	= $36
	getdata	= $37		; /38
	height	= $39		; Anzahl Rasterlinien (50/100)
	width	= $3a		; Breite in Kacheln (20/40)
	doit	= $3b		; Auftragsübermittlung
	cntset	= $3c

	offx	= ls_adrmisc	; gauge counters
	offy	= offx+1

; ----------------------------------------- Header

 	jmp start			; Jedes Modul beginnt hiermit

	!by $40 			; $80=Lader, $40=Saver, $20=Modifier, $10=Device
	!by 0			; Own-Requester-Flag (nur!) für Lader (4 = eigener Req)
	!by 0			; Dirty Flag (1 = dirty, 0 = clean)
	!wo modend		; Endadresse+1 des Moduls
	!wo 0			; reserviert
	!tx "VDC Lowres Saver"	; Beschreibung (16 Zeichen)
	!tx "1.01"		; Versionsnummer (4 Zeichen)
	!tx "24.01.24"		; Datum (8 Zeichen tt.mm.jj)
	!tx "A.Dettke        "	; Autor (16 Zeichen)

; ----------------------------------------- 

; ----------------------------------------- 
; ----------------------------------------- These next lines _mandatory_ for savers!
; ----------------------------------------- 

start	ldx gr_nr		; Funktionsnummer testen:
	dex
	bne saver		; Null, dann eigentlichen Saver aufrufen

	jmp initinp	; eins, dann "Input" ausführen (in INPUT.INC)

; -----------------------------------------
; -----------------------------------------
; ----------------------------------------- 

; ----------------------------------------- Main

saver

st11	lda gd_modswitch	; Memory Management
	pha

	jsr makechr	; Listbox-Kringel erzeugen
	stx doit		; .x=$ff
	ldx #<vdclst	; build requester
	ldy #>vdclst
	jsr gd_screen
	jsr gd_eloop	; wait for clicks

	pla		; dann Saver abwickeln: Memory Management
	sta gd_modswitch

st12	jsr gd_spron	; Mauszeiger wieder an
	sec		; Leave-Flag setzen
	rts		; fertig, Saver verlassen


; ----------------------------------------- 
; ----------------------------------------- Input Routines (This next line _mandatory_ for savers!)
; ----------------------------------------- 

	!src "input.inc"

; ----------------------------------------- 
; ----------------------------------------- Write File
; ----------------------------------------- 

write	lda #100		; set height in rasterlines
	ldx #20		; set width in tiles
wrdouble	sta height
	stx width

pass2	lda #$80		; start address: $3e80 (in VDC)
	jsr bsout
	lda #$3e
	jsr bsout
	lda #0
	sta cnt0		; init double pixel counter (to 4)
	sta cnt1		; init tile counter (to 20/40)
	sta cnt3		; init line counter (to 50/100)
	sta byte		; init data byte
	lda cntset
	sta cnt2		; init tile line counter (to 8 passes)

	inc doit		; pass 1: 0
	lda doit
	bne +
	jsr setclip	; 1st pass: set 4bit start address

+
---	lda getdata+1	; save current vector to start of clip
	pha
	lda getdata
	pha
--	ldy #0
-	lda (getdata),y	; read one line:
	pha
	lsr
	lsr
	lsr
	lsr
	tax		; get 2 pixels (1 tile)
	lda convert,x	; convert to vdc colors
	and #$f0
	sta byte
	pla
	and #15
	tax
	lda convert,x
	and #15
	ora byte
	jsr bsout
	sty ymerk
	jsr action	; activity
	ldy ymerk
	iny
	cpy #4		; one tile finished?
	bcc -

	clc		; yes, next tile
	lda getdata
	adc #32
	sta getdata
	bcc +
	inc getdata+1
+	inc cnt1		; cnt tiles to 20/40
	lda cnt1
	cmp width
	bcc --

	ldy #0
	sty cnt1		; re-init cnt
	pla		; then re-get start of current line
	sta getdata
	tax
	pla
	sta getdata+1

	txa		; address next line of a tile
	clc
	adc #4
	sta getdata
	bcc +
	inc getdata+1

+	inc cnt3		; count to 50/100
	lda cnt3		; already 50/100 lines?
	cmp height
	bcs ++		; yes, image finished

	inc cnt2		; already 8 lines (of a tile)?
	lda cnt2
	cmp #8
	bcc ---		; yes, address next row

	sty cnt2		; .y=0, re-init cnt
+ 	lda getdata	; one row finished
	clc
	adc #$e0		; add 1248 for next row
	sta getdata
	lda getdata+1
	adc #4
	sta getdata+1
	bne ---		; and turn over

; ----------------------------------------- 
; ----------------------------------------- Event: Cancel
; ----------------------------------------- 

evcancel
++	sec
	rts		; finished

; ----------------------------------------- Event: Mode

evmode	ldy lowmode	; 0: 320x200
	cpy #1		; 1: 640x200
	bne +
	ldy #$ff
+	iny
	sty lowmode

	ldx loffs,y	; change texts
	ldy #1
-	lda lowtext,x
	sta lowtx,y
	dex
	dey
	bpl -

	lda #$31
	sta sigtx+4	; reset file count to "1"
	sta sigtxs+4
	ldy lowmode
	ldx lsig,y	; change suffixes
	stx ag5+1
	iny
	iny
	ldx lsig,y
	stx lsg0+1
	stx lsg1+1
	inc lsg0+1	; and...
	
; ----------------------------------------- Output Text

textout	ldy #0		; .y: offset index in tabigad
out2	jsr settxt
	ldy #6		; offset to gadget text area
	jmp gd_xtxout1	; and show text

; ----------------------------------------- Event: Save

evsave	jsr gd_sproff	; .a=0
	lda ls_flen	; Filename wurde vom Filerequester übergeben?
	beq st2		; nein (dann Cancel)

	sta wflen		; sonst: Länge sichern
	jsr addsig	; Namenskürzel anhängen (".vdc")
	jsr fileopen	; Datei zum Schreiben öffnen
	ldx #0		; Anzeigetext #1 (Offset .x=0) eintragen
	stx cntset
	jsr tcopy
	iny		; .y=1
	sty adcnt		; Activity (Fortschrittsbalken) starten (.y=1)

	lda lowmode
	bne +
	jsr write		; Bild 320x200 speichern
	bcs st2
+	lda #50		; set height in rasterlines
	ldx #40		; set width in tiles
	jsr wrdouble	; Bild 640x200 speichern

st2	jsr endfile	; Datei schließen
	bne ++		; bei Fehler: Ende

	lda lowmode	; Modus checken, 640x200?
	beq ++

	ldx #2
	stx cntset
	inc sigtx+4	; ja, increment file count (to "2", "3"...)
	inc sigtxs+4
	sec
	lda ls_flen
	sbc #5
	bmi +
	tay
	jsr ag5
+	jsr fileopen
	jsr pass2
	jsr endfile
	bne ++

	jsr setname	; nur wenn fehlerfrei: Image Information aufbereiten

++	jmp st12

; ---------------------------------------- Set Start address of Image

setclip	lda #>sy_4bit	; 4Bit starting from $4000
	sta getdata+1
	ldy #<sy_4bit
	sty getdata
	rts

; ----------------------------------------- Open Files for Writing

fileopen	jsr wopen		; Datei zum Schreiben öffnen
	beq +
	pla		; wenn Diskettenfehler: Ende
	pla
	jmp st2

+	jsr getname	; Filenamen für Image Information sichern
	ldx #12		; OPEN #12, out
	jsr ckout
	jmp gd_clrms	; Fortschrittsbalken und Statusausgaben vorbereiten

endfile	jsr clrch		; nach Laden Bus freigeben
	lda #12		; Datei #12 schließen
	jsr close
	jmp err9		; Floppystatus zeigen (nur sehr kurz)

; ----------------------------------------- 
; ----------------------------------------- Disk Access Routines
; ----------------------------------------- 

pw	!pet "w,p,"
sigtx	!pet "cdv.1"	; Signatur ".vdc"
sigtxs	!scr "cdv.1"
drinit	!pet "i0"
wflen	!by 0

; ----------------------------------------- Filehandling

wopen	jsr inull		; Initialize
	bne err8
	ldy wflen		; ",p,w" anhängen
	ldx #3
wo1	lda pw,x
	sta ls_nambuf,y
	iny
	dex
	bpl wo1
	sty wflen
	lda #12		; OPEN 12,drive,12,"name,p,w"
	tay
	ldx ls_drive
	jsr filpar
	lda wflen
	ldx #<ls_nambuf
	ldy #>ls_nambuf
	jsr filnam
	jsr copen
	jmp err9
;
inull	ldx #<drinit	; Initialize Drive
	ldy #>drinit
	lda #2
	jsr gd_sendcom

err9	jsr gd_xmess
	lda ls_err2	; liefert Fehlernummer der Floppy
	and #15		; und zwar als Bytewert (wenn <>0, dann Fehler)
err8 	sec
	rts

; --------------------------- Kringel definieren

makechr	ldx #7
-	lda chschr,x
	sta sc_undochar,x
	dex
	bpl -
	rts

; ----------------------------------------- 
; ----------------------------------------- Text Output Handler
; ----------------------------------------- 

tabigad	!wo lowgad	; .y: 0	(Offset)

settxt	lda #<tabigad
	sta sc_texttab
	lda #>tabigad
	sta sc_texttab+1

gettab	lda (sc_texttab),y
	sta sc_screentab
	iny
	lda (sc_texttab),y
	sta sc_screentab+1

	lda #0
	tax
	tay
gt0	jsr gd_setpos	; prepare gui addresses
	cpx #4
	bne gt0
	jsr gd_trim
	jmp gd_initmove	; returns sc_vekt20 and .x=0

; ----------------------------------------- 
; ----------------------------------------- Activity Handler
; ----------------------------------------- 

messout	ldx #<message	; Textausgabe
	ldy #>message
	jmp gd_xtxout2

tcopy	ldy #0		; Informationstextauswahl (hinter Label "txt"), Offset auf Text in .x
tc0 	lda txt,x
	beq clrmess
	sta message,y
	inx
	iny
	bne tc0

clrmess	ldx #25		; Puffer für Balken löschen
	lda #32
cl0	sta mess,x
	dex
	bpl cl0
	ldy #0		; Balkenlänge 0
	ldx #7		; Dicke am Ende 0
	sty offy
	stx offx
	rts

action 	dec adcnt		; Activity, aktiviert sich nur bei 0 (Zähler abgelaufen)
	bne ld4
	lda cntwert	; dann: Reload des Zählers
	sta adcnt
	ldy offy		; Offset auf Balkenende
	cpy clrmess+1	; Ende der Ausgabezeile erreicht?
	bne act0
	jsr clrmess	; ja, re-Init des Status-Bereichs
act0	ldx offx		; offx: Index auf aktuelles Zeichen für Dicke des Balkenendstücks
	lda filltab,x
	sta mess,y
	jsr messout	; ganzen Balken ausgeben
	dec offx		; Endstück nächstes Mal dicker
	bpl ld4		; bis Endstück gefüllt
	inc offy		; dann den Balken selbst verlängern
	lda #7		; nächstes Endstück wieder dünn
	sta offx
ld4 	rts

filltab	!by 160,93,103,127,126,124,105,109	; Zeichen der Endstücke des Balkens (von hinten nach vorne)
cntwert	!by 40		; 40*8*25=8000: gauge bar

txt	!scr " VDC  ",0	; der Text mit Offset 0

message	!fill 6,32	; 6 Zeichen für Informationstext (Anzahl kann geändert werden, dann "mess" und CLRMESS anpassen)
mess	!fill 26,32	; 26 Zeichen für Fortschrittsbalken, Länge von message und mess zusammen immer 32!
	!by 0		; hier Wert=26 (bei CLRMESS Wert minus 1)!

; ----------------------------------------- 
; ----------------------------------------- Image Information Handler
; ----------------------------------------- 

getname	ldx #0		; aktuellen Filenamen sichern
si0	lda ls_lastname,x
	beq si1
	sta nbuf,x
	inx
	cpx #16
	bcc si0
si1	rts

setname	lda #0		; Namen in Image am Ende in Info eintragen
	ldx #<ls_picname
	ldy #>ls_picname
si3	stx sc_texttab
	sty sc_texttab+1
	tax
	ldy #0
si4	lda nbuf,x
	beq si5
	sta (sc_texttab),y
	inx
	iny
	bne si4
si5	rts
;
nbuf	!fill 16,32	; Namenspuffer
	!by 0

; ----------------------------------------- 
; ----------------------------------------- Namen verlängern
; ----------------------------------------- 

addsig	pha		; Länge merken
	tay
	dey
	ldx #0
ag4	lda ls_nambuf,y	; endet auf ".vdc"?
	cmp sigtx,x
	bne ag2		; nein
	dey
	inx
	cpx #4
	bcc ag4
	pla		; ja, fertig
	rts

ag2	pla
	tay		; Name länger als 12/11 Zeichen?
lsg0	cpy #13
	bcs ag0		; ja, behandeln

ag5	ldx #3		; nein, "1.vdc" anhängen
ag1	lda sigtx,x
ag3	sta ls_nambuf,y
	lda sigtxs,x	; auch für Image Information
	sta ls_lastname,y
	iny
	dex
	bpl ag1
	sty wflen		; neue Länge merken, fertig
	sty ls_flen
	rts

ag0	dey		; auf 12/11 verkürzen
lsg1	cpy #12
	bne ag0
	beq ag5		; unbed. Sprung

; ----------------------------------------- 
; ----------------------------------------- Data
; ----------------------------------------- 

convert	!by $00,$22,$cc,$11,$88,$aa,$dd,$11
	!by $33,$99,$44,$ee,$77,$dd,$55,$ff

chschr	!by $00,$3a,$46,$4e,$40,$40,$3c,$00

lowtext	!scr "3264"
loffs	!by 1,3
lowmode	!by 0
lsig	!by 3,4,12,11

; ----------------------------------------- 
; ----------------------------------------- Requester
; ----------------------------------------- 

vdclst	!by 0
	!by 7,14,12,11,$81,0,0
	!scr "VDC Lowres",0
lowgad	!by 11,14,12,3,$ca
	!wo evmode
	!by 223,32
lowtx	!scr "320x200 ",0
	!by 13,14,12,3,$cb
	!wo evsave
	!scr "   Save   ",0
	!by 15,14,12,3,$cb
	!wo evcancel
	!scr "  Cancel  ",0
	!by $c0,9,15,7
	!scr "Save as",0
	!by $80

; ----------------------------------------- 

modend 	!eof
