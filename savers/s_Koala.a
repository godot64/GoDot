
	!src "godotlib.lib"
	*= $c000
	!ct pet
;	!sl "labels.txt"

; -------------------------------------------------- 
;
; svr.Koala
; Saver for Multicolor Mode 320x200 Images
; The images have first to be rendered,
; svr.Koala uses the *rendered* image data to save
; *not* the 4Bit data
;
; 1.01: 07.02.98, added compress routines
; 1.02: 14.06.01, bug: "Cancel Save" didn't work, fixed
; 1.03: 08.02.21, rewrote code for better convenience
;                 now .koa suffix can be chosen
;
; -------------------------------------------------- 

; ----------------------------------------- Equates

	gbyte		= $30
	cnt		= $31
	adcnt		= $3e 

	offx		= ls_adrmisc
	offy		= offx+1

	colors		= $c82f
	vid1		= colors
	vid2		= $ca23
	colram		= $cc17
	back		= $cfff

; ----------------------------------------- 

header	jmp start
	!by $40
	!by 0
	!by 1
	!wo modend
	!wo 0
	!pet "Koala Multicolor"
	!pet "1.03"
	!pet "08.02.21"
	!pet "A.Dettke        "

; ----------------------------------------- Main

start	ldx gr_nr
	dex
	bne st0
	jmp initinp

; ----------------------------------------- Saver Koala

st0	jsr makechr	; build list box arrow

st11	lda gd_modswitch
	pha

	lda ls_flen	; no filename?
	sta wflen
	beq st01		; yes, leave

	ldx #<koalalst
	ldy #>koalalst
	jsr gd_xmloop

st01	pla		; finished
	sta gd_modswitch

; ----------------------------------------- Event: Leave saver

evcancel	sec
	rts

; --------------------------- 
; --------------------------- Input Routines

!src "input.inc"

; --------------------------- 
; --------------------------- 

pw	!pet "w,p,"
drinit	!pet "i0"
wflen	!by 0
suftx	!pet "aok."
suftxs	!scr "aok."

koala	!by $81,80,73,67,32	; "?PIC "
	!fill 16,32 	; name


; ----------------------------------------- Open Files for SAVE

wopen	ldx suffmode	; add PC suffix to name?
	beq asf0		; no, build Koala name

	ldx #0		; yes, add ".koa" 
	stx mode		; (don't ever compress PC files)
	lda wflen
	jsr addsuffix

asf0	lda mode		; file should be compressed?
	beq wl3		; no, skip prefix

asf1	ldy wflen		; yes, move name 2 chars right
	dey
cm00	lda ls_nambuf,y
	sta ls_nambuf+2,y
	dey
	bpl cm00
	lda wflen
	clc
	adc #2		; take care of len 
	cmp #17
	bcc st3
	lda #16		; (max 16 chars)
st3	sta wflen
	lda #$47		; and add prefix "gg"
	sta ls_nambuf
	sta ls_nambuf+1
	bne wo60		; uncond. branch

wl3	lda suffmode	; has PC name? 
	bmi wo6		; yes, skip koala stuff

	ldy #5		; uncompressed Koala:
	ldx #0
wo2	lda ls_nambuf-5,y	; combine own filename with Koala name intro
	sta koala,y
	iny
	inx
	cpx wflen
	bne wo2
	cpy #16
	bcs wo3
	lda #32		; fill with spaces (to 15 chars)
wo4	sta koala,y
	iny
	cpy #16
	bne wo4
wo3	ldy #14
	sty wflen
	inc wflen
wo5	lda koala,y	; move from edit buffer to out buffer
	sta ls_nambuf,y
	dey
	bpl wo5

wo60	ldy wflen		; now convert to screen code for GoDot
	sty sc_br
-	dey
	bmi +
	lda ls_nambuf,y	; copy to convert buffer
	sta sc_movetab,y
	bne -
+	jsr gd_cnvbc	; convert, returns .a=0
	tay
-	lda sc_movetab,y	; copy to GoDot screenlist for proper display
	sta ls_lastname,y
	iny
	cpy wflen
	bcc -

; ----------------------------------------- 

wo6	jsr inull		; now open file, first: init disk
	bne err8
	ldy wflen
	ldx #3
wo1	lda pw,x
	sta ls_nambuf,y	; add ",p,w" to filename
	iny
	dex
	bpl wo1
	sty wflen
	lda #12		; open 12,dr,12,"name,p,w"
	tay
	ldx ls_drive
	jsr filpar
	lda wflen
	ldx #<ls_nambuf
	ldy #>ls_nambuf
	jsr filnam
	jsr copen
	jmp err9

; ----------------------------------------- Init Disk

inull	ldx #<drinit
	ldy #>drinit
	lda #2
	jsr gd_sendcom
err9	jsr gd_xmess
	lda ls_err2
	and #15
err8	sec
	rts

; ----------------------------------------- Message Handling

messout	ldx #<message
	ldy #>message
	jmp gd_xtxout2

tcopy	ldy #0
tc0	lda txt,x
	beq clrmess
	sta message,y
	inx
	iny
	bne tc0

action	dec adcnt
	bne ld4
	lda cntwert
	sta adcnt
	ldy offy
	cpy clrmess+1	; #1.03: catch Gauge Overflow
	bne act0
	jsr clrmess
act0	ldx offx
	lda filltab,x
	sta mess,y
	jsr messout
	dec offx
	bpl ld4
	inc offy
	lda #7
	sta offx
ld4	rts

clrmess	ldx #23
	lda #32
cl0	sta mess,x
	dex
	bpl cl0
	ldy #0
	ldx #7
	sty offy
	stx offx
	rts

filltab	!by 160,93,103,127,126,124,105,109

txt	!scr " Bitmap@"
	!scr " Colors@"

message	!fill 8,32
mess	!fill 24,32
	!by 0

; ----------------------------------------- Image Information Handler

getname	ldx #0
si0	lda ls_lastname,x
	beq si1
	sta nbuf,x
	inx
	cpx #16
	bcc si0
si1	rts

setname	lda #0
	ldx #<ls_picname
	ldy #>ls_picname
si3	stx sc_texttab
	sty sc_texttab+1
	tax
	ldy #0
si4	lda nbuf,x
	beq si5
	sta (sc_texttab),y
	inx
	iny
	bne si4
si5	rts

nbuf	!fill 16,32
	!by 0

; ----------------------------------------- Event: Set Compression Mode

evsetcomp	ldx compr
	beq +
	ldx #$ff
+	inx

dth1	stx compr		; hold
	stx mode		; preset mode flag
	ldy offs,x	; toggle text
	ldx #13
-	lda comptxt,y
	sta comptx,x
	dey
	dex
	bpl -

	lda #$80		; now set appropriate mode flag
	ldx compr		; (uncompr.  : 0)
	beq dth2
	sta mode		; (compressed: $80)
dth2	ldy #0		; and output text
	!by $2c
dth3	ldy #2
	jsr settab
	ldy #6
	jmp gd_xtxout1	; returns .c=0

; ----------------------------------------- Event: Set Suffix Mode

evsetsuff	ldx suffx
	beq +
	ldx #$ff
+	inx

dth4	stx suffx		; hold
	stx suffmode	; preset suffmode flag
	ldy offs,x	; toggle text
	ldx #13
-	lda sufftxt,y
	sta sufftx,x
	dey
	dex
	bpl -

	lda #$80		; now set appropriate suffmode flag
	ldx suffx		; (koala prefix: 0)
	beq dth3
	sta suffmode	; (pc suffix   : $80)
	bne dth3		; uncond. branch

; ----------------------------------------- Event: Save Image

evsave	jsr gd_sproff	; no, mouse pointer off
	sta cnt		; .a=0, init compression counter

	lda #1		; init save pic
	sta adcnt
	lda #50
	sta cntwert
	jsr getpic	; first gather data

	jsr wopen		; now open file
	bne st2
	jsr getname
	ldx #12
	jsr ckout
	jsr gd_clrms
	ldx #0
	jsr tcopy

	jsr write		; save pic

st2	jsr clrch		; close file
	lda #12
	jsr close
	jsr gd_spron
	jsr err9
	bne st1

	jsr setname	; set image information if successful

st1	rts		; .c is set, so leave requester

; ----------------------------------------- Screenlist

koalalst	!by 0
	!by 6,13,17,14,$81,0,0
	!scr " Save Koala",0
compgad	!by 11,13,17,3,$ca
	!wo evsetcomp
	!by 223
comptx	!scr " Uncompressed ",0
suffgad	!by 14,13,17,3,$ca
	!wo evsetsuff
	!by 223
sufftx	!scr " w/ PC-Suffix ",0
	!by 17,13,9,3,$c7
	!wo evsave
	!scr "Save",0
	!by 17,22,8,3,$cd
	!wo evcancel
	!scr "Cancel",0
	!by $c0,8,15,12
	!scr "Select  Mode@"
	!by $80

; ----------------------------------------- 

compr	!by 0
mode	!by 0
suffx	!by 1
suffmode	!by $80
cntwert	!by 50
offs	!by 13,27
comptxt	!scr " Uncompressed "
	!scr "  Compressed  "
sufftxt	!scr " w/KoalaPrefix"
	!scr " w/ PC-Suffix "
chschr	!by $00,$3a,$46,$4e,$40,$40,$3c,$00

; --------------------------- Kringel definieren

makechr	ldx #7
-	lda chschr,x
	sta sc_undochar,x
	dex
	bpl -
	rts

; ------------------------------------ Output gadget text routines

tabigad	!wo compgad	; 0
	!wo suffgad	; 2

settab	lda #<tabigad	; address gadget descriptor
	sta ls_vekta8
	lda #>tabigad
	sta ls_vekta8+1

gettab	lda (ls_vekta8),y	; set working vector
	sta sc_screentab
	iny
	lda (ls_vekta8),y
	sta sc_screentab+1

	lda #0		; transfer gadget data (to compute them)
	tax
	tay
gt0	jsr gd_setpos
	cpx #3
	bne gt0
	stx sc_ho
	jsr gd_trim	; trim to inner area of gadget
	jmp gd_initmove	; compute screen address for display

; ----------------------------------------- Gather Image Colors

getpic	lda #$0b		; gather 2001 bytes at $c82f:
	sta sc_texttab
	lda #<(vid1)
	ldx #>(vid1)
	ldy #$ce		; Videoram pt 1 from $ce0b
	jsr move
	lda #$0b
	sta sc_texttab
	lda #<(vid2)
	ldx #>(vid2)
	ldy #$ee		; pt 2 from $ee0b
	jsr move
	lda #$0b
	sta sc_texttab
	lda #<(colram)
	ldx #>(colram)
	ldy #$de		; colorram from $de0b
	jsr movec
	lda gr_bkcol	; and background color
	and #15
	sta back
	rts

; ----------------------------------------- Move Video RAM data

move	sty sc_texttab+1
	sta sc_vekt20
	stx sc_vekt20+1
	lda #<(500)
	ldx #>(500)
	sta ls_vekta8
	stx ls_vekta8+1
	sei
	lda #$30
	sta 1
	ldy #0
mv0	lda (sc_texttab),y
	sta (sc_vekt20),y
	jsr count
	bne mv0
	lda #$36
	sta 1
	cli
	rts

; ----------------------------------------- Move Color RAM data

movec	sty sc_texttab+1
	sta sc_vekt20
	stx sc_vekt20+1
	lda #<(500)		; color ram is compacted to 500 bytes
	ldx #>(500)
	sta ls_vekta8
	stx ls_vekta8+1
	sei
	lda #$30
	sta 1
	ldy #0
mv1	sty sc_merk
	lda (sc_texttab),y		; make 1000 bytes
	lsr
	rol sc_merk
	lsr
	rol sc_merk
	lsr
	rol sc_merk
	lsr
	rol sc_merk
	sta (sc_vekt20),y
	inc sc_vekt20
	bne mv2
	inc sc_vekt20+1
mv2	lda sc_merk
	sta (sc_vekt20),y
	jsr count
	bne mv1
	lda #$36
	sta 1
	cli
	rts

; ----------------------------------------- Counters

count	inc sc_vekt20
	bne cou5
	inc sc_vekt20+1
cou5	inc sc_texttab
	bne cou6
	inc sc_texttab+1
cou6	lda ls_vekta8
	bne cou7
	dec ls_vekta8+1
cou7	dec ls_vekta8
	lda ls_vekta8
	ora ls_vekta8+1
	rts

; ----------------------------------------- Write Image

write	lda #0		; Start address $6000
	jsr bsout
	lda #$60
	jsr bsout		; 8000 bytes bitmap
	lda #<8000
	ldx #>8000
	sta ls_vekta8
	stx ls_vekta8+1
	lda #<$2000
	ldx #>$2000
	sta sc_texttab
	stx sc_texttab+1
wr0	ldy #0
	lda (sc_texttab),y
	jsr pack		; packed, if wanted
	jsr action	; activity
	jsr cou5		; count
	bne wr0

	ldx #8
	jsr tcopy
	lda #12
	sta cntwert
	lda #<2001	; 2001 bytes colors
	ldx #>2001
	sta ls_vekta8
	stx ls_vekta8+1
	lda #<colors
	ldx #>colors
	sta sc_texttab
	stx sc_texttab+1
wr1	ldy #0
	lda (sc_texttab),y
	jsr pack		; packed, if wanted
	jsr action	; activity
	jsr cou5		; count
	bne wr1

	lda #<vid1	; recontruction of video ram 1 area
	sta sc_texttab
	lda #$0b
	ldx #$ce
	ldy #>vid1
	jsr move

	lda mode		; compressed?
	beq fin		; no, finished

	lda gbyte		; yes, flush data
	eor #$ff

; ----------------------------------------- Compress

pack	bit mode
	bmi wl4		; compress if flag $80
	jmp bsout

wl4	cmp gbyte
	beq incr
p255	pha
	lda cnt
	beq wl1
	cmp #$ff
	beq wl0
	cmp #4
	bcs wl0
	lda gbyte
	cmp #$fe
	bne wl2
wl0	lda #$fe		; $fe, byte, counter
	jsr bsout
	lda gbyte
	jsr bsout
	lda cnt
	jsr bsout
	lda #0
	sta cnt
	beq wl1
wl2	lda gbyte
	jsr bsout
	dec cnt
	bne wl2
wl1	pla
	sta gbyte
incr	inc cnt
	lda cnt
	cmp #$ff
	bne fin
	lda gbyte
	jsr p255
	dec cnt
fin	rts

; ----------------------------------------- Namen verlängern

addsuffix	pha		; Länge merken
	tay
	dey
	ldx #0
ag4	lda ls_nambuf,y	; endet auf ".koa"?
	cmp suftx,x
	bne ag2		; nein
	dey
	inx
	cpx #4
	bcc ag4
	pla		; ja, fertig
	rts
	
ag2	pla
	tay		; Name länger als 12 Zeichen?
	cpy #13
	bcs ag0		; ja, behandeln

ag5	ldx #3		; nein, ".koa" anhängen
ag1	lda suftx,x
ag3	sta ls_nambuf,y
	lda suftxs,x	; auch für Image Info
	sta ls_lastname,y
	iny
	dex
	bpl ag1
	sty wflen		; neue Länge merken, fertig
	sty ls_flen
	rts

ag0	dey		; auf 12 verkürzen
	cpy #12
	bne ag0
	beq ag5		; unbed. Sprung

; ----------------------------------------- 

modend	!eof
