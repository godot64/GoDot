	!src "..\godotlib.lib"	; library nicht erforderlich
	*=$c000

; ------------------------------------------------ 
;
; mod.EDgray
; Modul zum Reduzieren der Farben
; nach den Error-Distribution-Verfahren
;
; 1.00, 06.04.93
; 1.01, 24.09.93
; 1.02, 09.07.17, added palette reset
;
; re-assembled on 28.05.2017
;
; ------------------------------------------------ 

; ------------------------------------------------ Declarations

	adcnt	= $30
	dst	= $35
	src	= $37
	linenr	= $39
	bcnt	= $3a
	m2err	= $3b
	m3err	= $3c
	perr	= $3d
	m1err	= $3e
	derr	= $3f
	count	= $41

	makeqtab	= $17c9
	numcols	= $1ee9
	buffer	= $c400
	cpalette	= $c600
	y2	= $d005

; ------------------------------------------------ Header

	jmp start
	!by $20
	!by $00
	!by $00
	!wo modend 
	!wo 0
	!pet "reduce ed gray  "
	!pet "1.02"
	!pet "09.07.17"
	!pet "W.Kling         "

; ------------------------------------------------ Main

start	ldx sc_screenvek
	lda sc_screenvek+1
	stx screenpos
	sta screenpos+1

	jsr initad	; prepare activity

	lda #0		; clear buffer
	tay 
-	sta buffer,y
	sta buffer+160,y
	iny 
	cpy #160
	bne -
	sta linenr	; 200 rasterlines (from 0 to 199)

	ldy #15		; prepare palette
-	ldx gr_qtab,y
	lda gr_palette,x
	tax 
	lda godcols,x
	sta cpalette,y
	dey 
	bpl -

--	jsr getline	; buffer one rasterline
	lda #<sy_bigbuffer
	ldx #>sy_bigbuffer
	sta dst
	stx dst+1
	lda #<buffer
	ldx #>buffer
	sta src
	stx src+1
	lda #$40		; count 320 bytes
	ldx #$01
	sta count
	stx count+1

	ldy #0		; apply error distribution
	sty perr
-	lda (src),y
	clc 
	adc perr
	sta derr
	lda (dst),y
	asl
	asl
	asl
	sta m1err
	bit derr
	bmi +
	adc derr
	sec 
	ror derr+1
	jmp ++

+	lda derr
	adc m1err
	ror derr+1

++	sta m2err		; new color
	bit derr+1
	bmi +
	lda #0
+	cmp #$79
	bcc +
	lda #$78
+	lsr		; /8: index into palette
	lsr
	lsr
	tax 
	lda cpalette,x	; get new color
	sta (dst),y	; and set it
	asl
	asl
	asl
	sta m3err
	lda m2err
	sec 
	sbc m3err
	clc 
	bpl +
	sec 
+	ror
	sta (src),y
	sta perr
	iny 
	bne +
	inc dst+1
	inc src+1

+	lda count		; count 320 bytes
	bne +
	dec count+1
+	dec count
	lda count
	ora count+1
	bne -
	jsr putline	; then transfer it to 4bit 

	inc linenr	; next rasterline

adinc	dec adcnt		; activity
	bne +
	inc y2
	inc y2+2
	lda #5
	sta adcnt

+	lda linenr	; count rasterlines
	cmp #200
	beq resetpal
	jmp --

resetpal	lda #16
	sta gr_howmany	; volle Farbanzahl erzeugen
	jsr gd_xcnvdez	; in Ziffern wandeln
	ldy #2
	sta numcols,y	; Einer eintragen
	txa		; Zehner eintragen, wenn nicht 0
	and #15
	bne +
	ldx #32
+	txa
	dey
	sta numcols,y
	jsr makeqtab	; reset quantisation table
	dex		; reset colors (.x=16)
-	lda gr_basepal,x
	sta gr_palette,x
	dex
	bpl -

	jsr clearad	; then clear activity bar
	ldx screenpos
	ldy screenpos+1
	jsr gd_screen
	clc
	rts

; ------------------------------------------------ 

godcols	!by $00,$0f,$04,$0c,$05,$0a,$01,$0d
	!by $06,$02,$09,$03,$07,$0e,$08,$0b

; ------------------------------------------------ 

getline	lda linenr	; compute 4bit address
	pha 
	lsr
	lsr
	lsr
	tax 
	pla 
	and #7
	asl
	asl
	sta src
	lda fbhigh,x
	sta src+1
	lda #<sy_bigbuffer	; destination
	ldx #>sy_bigbuffer
	sta dst
	stx dst+1

	ldx #0
	lda #40		; 40 Kacheln
	sta bcnt
--	ldy #0
-	lda (src),y	; isolate one rasterline
	pha 
	lsr
	lsr
	lsr
	lsr
	sta (dst,x)
	inc dst
	bne +
	inc dst+1
+	pla 
	and #15
	sta (dst,x)
	inc dst
	bne +
	inc dst+1
+	iny 
	cpy #4
	bne -

	lda src		; nächste Kachel
	clc 
	adc #32
	sta src
	bcc +
	inc src+1
+	dec bcnt		; bis alle durch
	bne --
	rts 

; ------------------------------------------------ 

putline	lda linenr	; compute 4bit address
	pha 
	lsr
	lsr
	lsr
	tax 
	pla 
	and #7
	asl
	asl
	sta src
	lda fbhigh,x
	sta src+1
	lda #<sy_bigbuffer	; destination
	ldx #>sy_bigbuffer
	sta dst
	stx dst+1

	ldx #0
	lda #40		; 40 Kacheln
	sta bcnt
--	ldy #0
-	lda (dst,x)	; isolate one rasterline
	asl
	asl
	asl
	asl
	inc dst
	bne +
	inc dst+1
+	ora (dst,x)
	inc dst
	bne +
	inc dst+1
+	sta (src),y
	iny 
	cpy #4
	bne -

	lda src		; nächste Kachel
	clc 
	adc #32
	sta src
	bcc +
	inc src+1
+	dec bcnt		; bis alle durch
	bne --
	rts 

; ------------------------------------------------ 

fbhigh	!by $40,$45,$4a,$4f,$54
	!by $59,$5e,$63,$68,$6d
	!by $72,$77,$7c,$81,$86
	!by $8b,$90,$95,$9a,$9f
	!by $a4,$a9,$ae,$b3,$b8

; ------------------------------------------------ 

screenpos	!wo 0

; ------------------------------------------------ 

clearad	lda #$ff
	sta gr_redisp
	lda $d015
	and #$f3
	sta $d015
	lda spritehi
	and #$f3
	sta spritehi
	clc 
	rts 

; ------------------------------------------------ initad

initad 	ldy #$3c
	lda #$00
-	sta $3fc3,y
	dey 
	bpl -
	sty $3fc0
	sty $3fc1
	sty $3fc2
	lda #15
	sta $d029
	sta $d02a
	lda $d01d
	ora #$0c
	sta $d01d
	lda spritehi
	ora #$0c
	sta spritehi
	lda #8
	sta $d004
	lda #32
	sta $d006
	lda #$92
	sta y2
	sta y2+2
	sty $07fa
	sty $07fb
	lda $d015
	ora #$0c
	sta $d015
	lda #5
	sta adcnt
	rts 

; ------------------------------------------------ 

modend	!eof

