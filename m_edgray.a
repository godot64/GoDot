	!src "..\godotlib.lib"	; library nicht erforderlich
	*=$c000

; ------------------------------------------------ 
;
; mod.EDgray
; Modul zum Reduzieren der Farben
; nach dem Error-Distribution-Verfahren
;
; 1.00, 06.04.93
; 1.01, 24.09.93
; 1.02, 09.07.17, added palette reset,
;                 combined Hires and MC,
;                 added requester
; 1.03, 10.07.17, combined with EDmono
;
; re-assembled on 28.05.2017
;
; ------------------------------------------------ 

; ------------------------------------------------ Declarations

	adcnt	= $30
	mono1	= $31
	mono2	= $32
	ymrk	= $33
	dst	= $35
	src	= $37
	linenr	= $39
	bcnt	= $3a
	m2err	= $3b
	m3err	= $3c
	perr	= $3d
	m1err	= $3e
	derr	= $3f
	count	= $41
	ccol	= $41

	makeqtab	= $17c9
	numcols	= $1ee9
	buffer	= $c600
	cpalette	= $c740
	y2	= $d005

; ------------------------------------------------ Header

	jmp start
	!by $20
	!by $00
	!by $00
	!wo modend 
	!wo 0
	!pet "reduce ed gray  "
	!pet "1.03"
	!pet "11.07.17"
	!pet "W.Kling         "

; -------------------------------------- Main

start	jsr makechar
	ldx #<(edlst)
	ldy #>(edlst)
	jmp gd_xmloop

; ------------------------------------------------ Event: Set Gamma Values

evgamma	lda gamma
	cmp #2
	bne evg0
	lda #$ff
	sta gamma
evg0	inc gamma
	ldy gamma
	ldx goffs,y
	ldy #3
-	lda gammavals,x
	sta gammatx,y
	dex
	dey
	bpl -
	ldx #<sgamma
	ldy #>sgamma+1
	jmp settext

; ------------------------------------------------ Event: Reduce to Monochrome

evmono	ldy #1
	bne +

; ------------------------------------------------ Event: Reduce Hires

evhires	ldy #0
+	sty mode
	jsr init

	ldy #15		; prepare palette
-	ldx gr_qtab,y
	lda gr_palette,x
	tax 
	lda godcols,x
	sta cpalette,y
	dey 
	bpl -
	sta mono1
	lda cpalette+15
	sta mono2

--	jsr getlineh	; buffer one rasterline
	lda #<sy_bigbuffer
	ldx #>sy_bigbuffer
	sta dst
	stx dst+1
	lda #<buffer
	ldx #>buffer
	sta src
	stx src+1
	lda #$40		; count 320 bytes
	ldx #$01
	sta count
	stx count+1

	ldy #0		; apply error distribution
	sty perr
-	lda (src),y
	clc 
	adc perr
	sta derr
	lda (dst),y

	sty ymrk
	tax
	lda mode
	beq +
	ldy gamma		; Gamma:
	beq +		; 1.00
	dey
	beq ++		; 0.35
	lda g180,x	; 1.80
	jmp hr0
+	txa
	asl
	asl
	asl
	jmp hr0
++	lda g035,x
hr0	ldy ymrk
	sta m1err
	
	bit derr
	bmi +
	adc derr
	sec 
	ror derr+1
	jmp ++

+	lda derr
	adc m1err
	ror derr+1

++	ldx mode
	beq +

	sta m2err		; monochrome
	bit derr+1
	bpl ++
	cmp #$3c		; Mittelwert
	bcc ++
	lda mono2		; weiﬂ
	!by $2c
++	lda mono1		; schwarz
	jmp +++

+	sta m2err		; new color
	bit derr+1
	bmi +
	lda #0
+	cmp #$79
	bcc +
	lda #$78
+	lsr		; /8: index into palette
	lsr
	lsr
	tax 
	lda cpalette,x	; get new color

+++	sta (dst),y	; and set it
	asl
	asl
	asl
	sta m3err
	lda m2err
	sec 
	sbc m3err
	clc 
	bpl +
	sec 
+	ror
	sta (src),y
	sta perr
	iny 
	bne +
	inc dst+1
	inc src+1

+	lda count		; count 320 bytes
	bne +
	dec count+1
+	dec count
	lda count
	ora count+1
	bne +

	jsr putlineh	; then transfer it to 4bit 
	jsr lloop
	jmp --

+	jmp -

; ------------------------------------------------ Event: Reduce Multicolor

evmcolor	jsr init
	sty mode		; .y=$ff
	jsr makepal

--	jsr getlinem
	ldy #0		; apply error distribution
	sty perr
-	lda buffer,y
	clc 
	adc perr
	sta derr
	lda sy_bigbuffer,y
	asl
	asl
	sta m1err
	bit derr
	bmi +
	adc derr
	sec 
	ror derr+1
	jmp ++

+	lda derr
	adc m1err
	ror derr+1

++	sta m2err		; new color
	bit derr+1
	bmi +
	lda #0
+	cmp #$79
	bcc +
	lda #$78
+	lsr		; /8: index into palette
	lsr
	lsr
	tax 
	lda cpalette,x	; get new color
	sta sy_bigbuffer,y
	asl
	asl
	asl
	sta m3err
	lda m2err
	sec 
	sbc m3err
	clc 
	bpl +
	sec 
+	ror
	sta buffer,y
	sta perr
	iny 
	cpy #160
	bne -
	jsr putlinem	; then transfer it to 4bit 

	jsr lloop
	jmp --

; ------------------------------------------------ 

godcols	!by $00,$0f,$04,$0c,$05,$0a,$01,$0d
	!by $06,$02,$09,$03,$07,$0e,$08,$0b

; ------------------------------------------------ 

getlineh	lda linenr	; compute 4bit address
	pha 
	lsr
	lsr
	lsr
	tax 
	pla 
	and #7
	asl
	asl
	sta src
	lda fbhigh,x
	sta src+1
	lda #<sy_bigbuffer	; destination
	ldx #>sy_bigbuffer
	sta dst
	stx dst+1

	ldx #0
	lda #40		; 40 Kacheln
	sta bcnt
--	ldy #0
-	lda (src),y	; isolate one rasterline
	pha 
	lsr		; linkes Pixel
	lsr
	lsr
	lsr
	sta (dst,x)	; ablegen
	inc dst
	bne +
	inc dst+1
+	pla 		; rechtes Pixel
	and #15
	sta (dst,x)	; ablegen
	inc dst
	bne +
	inc dst+1
+	iny 
	cpy #4
	bne -

	lda src		; n‰chste Kachel
	clc 
	adc #32
	sta src
	bcc +
	inc src+1
+	dec bcnt		; bis alle durch (320 Pixel)
	bne --
	rts 

; ------------------------------------------------ 

putlineh	lda linenr	; compute 4bit address
	pha 
	lsr
	lsr
	lsr
	tax 
	pla 
	and #7
	asl
	asl
	sta src
	lda fbhigh,x
	sta src+1
	lda #<sy_bigbuffer	; destination
	ldx #>sy_bigbuffer
	sta dst
	stx dst+1

	ldx #0
	lda #40		; 40 Kacheln
	sta bcnt
--	ldy #0
-	lda (dst,x)	; isolate one rasterline
	asl
	asl
	asl
	asl
	inc dst
	bne +
	inc dst+1
+	ora (dst,x)
	inc dst
	bne +
	inc dst+1
+	sta (src),y
	iny 
	cpy #4
	bne -

	lda src		; n‰chste Kachel
	clc 
	adc #32
	sta src
	bcc +
	inc src+1
+	dec bcnt		; bis alle durch
	bne --
	rts 

; ------------------------------------------------ 

getlinem	lda linenr	; compute 4bit address
	pha 
	lsr
	lsr
	lsr
	tax 
	pla 
	and #7
	asl
	asl
	sta dst
	ldy fbhigh,x
	sty dst+1
	sta src
	sty src+1

	ldx #0
	lda #40		; 40 Kacheln
	sta bcnt
--	ldy #0
-	lda (dst),y	; isolate one rasterline
	lsr
	lsr
	lsr
	lsr
	sta sy_bigbuffer,x
	lda (dst),y
	and #15
	clc 
	adc sy_bigbuffer,x
	sta sy_bigbuffer,x
	inx 
	iny 
	cpy #4
	bne -

	lda dst
	clc 
	adc #32
	sta dst
	bcc +
	inc dst+1
+	dec bcnt		; bis alle durch
	bne --
	rts 

; ------------------------------------------------ 

putlinem	lda src
	ldy src+1
	sta dst
	sty dst+1

	ldx #0
	lda #40		; 40 Kacheln
	sta bcnt
--	ldy #0
-	lda sy_bigbuffer,x
	asl
	asl
	asl
	asl
	ora sy_bigbuffer,x
	inx 
	sta (dst),y
	iny 
	cpy #4
	bne -
	lda dst
	clc 
	adc #32
	sta dst
	bcc +
	inc dst+1
+	dec bcnt		; bis alle durch
	bne --
	rts 

; ------------------------------------------------ 

makepal	ldy #0
-	ldx gr_palette,y
	lda godcols,x
	sta npalette,y
	iny 
	cpy gr_howmany
	bne -
	ldy #15
--	ldx #0
	lda #16
	sta ccol
-	tya 
	sec 
	sbc npalette,x
	beq ++
	bcs +
	eor #$ff
	adc #1
+	cmp ccol
	bcs +
++	sta ccol
	lda npalette,x
	sta cpalette,y
+	inx 
	cpx gr_howmany
	bne -
	dey 
	bpl --
	rts 

; ------------------------------------------------ Text output

settext	stx sc_screentab
	sty sc_screentab+1
	lda #0
	tax
	tay
stt	jsr gd_setpos
	cpx #3
	bne stt
	jsr gd_trim
	jsr gd_initmove
	ldy #6
	jmp gd_xtxout1

; ------------------------------------------------ 

makechar	ldx #7
-	lda gammach,x
	sta sc_undochar,x
	dex
	bpl -
	rts

; ------------------------------------------------ 

npalette	!fill 16,5

; ------------------------------------------------ 

fbhigh	!by $40,$45,$4a,$4f,$54
	!by $59,$5e,$63,$68,$6d
	!by $72,$77,$7c,$81,$86
	!by $8b,$90,$95,$9a,$9f
	!by $a4,$a9,$ae,$b3,$b8

; ------------------------------------------------ 

screenpos	!wo 0
mode	!by 0
gammach	!by $00,$0c,$6e,$6d,$38,$7c,$c6,$7c
gamma	!by 0
gammavals	!scr "1.000.351.80"
goffs	!by 3,7,11
gray	!scr "Gray "
g035	!by $00,$2f,$3b,$44,$46,$52,$57,$5c
	!by $60,$64,$68,$6c,$6f,$72,$75,$78
g180	!by $00,$01,$03,$07,$0b,$11,$17,$1f
	!by $27,$30,$3a,$45,$51,$5d,$6a,$78 

; -------------------------------------- Requester

edlst	!by 0
	!by 8,12,17,11,$81,0,0
	!scr "Reduce Grays@"
sgamma	!by 12,22,7,3,$cf,<evgamma,>evgamma
	!by 223
gammatx	!scr "1.00@"
	!by 12,12,10,3,$c8,<evmono,>evmono
	!scr "Mono@"
shires	!by 14,17,12,3,$cf,<evhires,>evhires
hirestx	!scr "Hires@"
smcol	!by 16,17,12,3,$ce,<evmcolor,>evmcolor
mcoltx	!scr "Multicolor@"
	!by 18,12,17,3,$cb,<evcan,>evcan
	!scr "Cancel@"
	!by $c0,14,13,2
	!scr "or@"
	!by $c0,16,13,2
	!scr "or@"
	!by $c0,10,13,12
	!scr "Choose Mode:@"
	!by $80

; ------------------------------------------------ adinc

lloop	dec adcnt		; activity
	bne +
	inc y2
	inc y2+2
	lda #5
	sta adcnt
+	inc linenr	; next rasterline
	lda linenr	; count rasterlines
	cmp #200
	beq resetpal
	rts

; ------------------------------------------------ Abschluss

resetpal	pla
	pla
	lda #16
	sta gr_howmany	; volle Farbanzahl erzeugen
	jsr gd_xcnvdez	; in Ziffern wandeln
	ldy #2
	sta numcols,y	; Einer eintragen
	txa		; Zehner eintragen, wenn nicht 0
	and #15
	bne +
	ldx #32
+	txa
	dey
	sta numcols,y
	jsr makeqtab	; reset quantisation table
	dex		; reset colors (.x=16)
-	lda gr_basepal,x
	sta gr_palette,x
	dex
	bpl -

	lda mode
	beq +
	bpl +
	lda #$31
	ldx #$36
	bne ++
+	lda #$33
	ldx #$32
++	sta ls_imode
	stx ls_imode+1
	ldx #4
-	lda gray,x
	sta ls_idrive,x
	dex
	bpl -

; ------------------------------------------------ 

clearad	lda #$ff
	sta gr_redisp
	lda $d015
	and #$f3
	sta $d015
	lda spritehi
	and #$f3
	sta spritehi

; ------------------------------------------------ Event: Cancel

evcan	sec 
	rts

; ------------------------------------------------ Init

init	lda #0		; clear buffer
	tay 
-	sta buffer,y
	sta buffer+160,y
	iny 
	cpy #160
	bne -
	sta linenr	; 200 rasterlines (from 0 to 199)

initad 	ldy #$3c
	lda #$00
-	sta $3fc3,y
	dey 
	bpl -
	sty $3fc0
	sty $3fc1
	sty $3fc2
	lda #15
	sta $d029
	sta $d02a
	lda $d01d
	ora #$0c
	sta $d01d
	lda spritehi
	ora #$0c
	sta spritehi
	lda #8
	sta $d004
	lda #32
	sta $d006
	lda #$92
	sta y2
	sta y2+2
	sty $07fa
	sty $07fb
	lda $d015
	ora #$0c
	sta $d015
	lda #5
	sta adcnt
	rts 

; ------------------------------------------------ 

modend	!eof

