
*= $c000
!src "..\godotlib.lib"

; ------------------------------------------------------------ 
;
; mod..NewPointer
; Modul zum Installieren eines alternativen Mauszeigers
;
; 1.00, 13.06.92, first release
; 1.01, 25.07.17, Modul sicherer gemacht
;
; ------------------------------------------------------------ 

; ----------------------------------------- Equates

	ok		= $30
	status		= $90
	buffer		= $c600

; ----------------------------------------- Header

header	jmp start
	!by $80
	!by 4
	!by 0
	!wo modend
	!wo 0
	!pet "Mouse Pointer   "
	!pet "1.01"
	!pet "25.07.17"
	!pet "A. Dettke       "

; ----------------------------------------- Main

start	jsr chgdrv	; Sysdrive auf Bootdrive umlenken
	ldx #<(buffer)	; Zeiger auf Directory-Filter setzen ("ptr.*")
	ldy #>(buffer)
	stx ls_adrmisc	; (wird bei Klick auf LOAD nach sc_vekt20 geschrieben)
	sty ls_adrmisc+1
	sty ok		; Success-Flag
	lda gd_modswitch	; Modul-Management
	pha 
	lda #0		; und mit 0 vorbesetzen (für Rückmeldung)
	sta ls_flen
	sta xoff		; Hotspot vorbesetzen
	sta yoff

	jsr lddir		; gefiltertes Directory anzeigen (Filerequester)
	jsr gd_eloop	; warten auf Mausklick (Load oder Cancel)

	lda ls_flen	; kein File ausgewählt?
	beq lp0		; dann Abschluss

	jsr readptr	; sonst: Mauszeiger einlesen
	lda ok		; geklappt?
	bne lp0		; nein, Abbruch

	ldy #62		; erfolgreich, neuen Mauszeiger installieren
-	lda buffer,y
	sta sc_pointer,y
	dey
	bpl -

	lda xoff		; Hotspot x größer als 15?
	cmp #16
	bcs pt0
	sec 
	lda #24		; dann von 24 abziehen (linker Rand)
	sbc xoff
	sta sy_soffx
pt0	lda yoff		; Hotspot y größer als 15?
	cmp #16
	bcs lp0
	sec 
	lda #50		; dann von 50 abziehen (oberer Rand)
	sbc yoff		; (damit bleibt der Hotspot im sichtbaren Screen)
	sta sy_soffy

lp0	jsr gd_spron	; Mauszeiger an
	pla 		; aufräumen
	sta gd_modswitch
	jsr chgdrv	; Sysdrive zurück
	ldx sc_scvek2	; Main-Screenlist ansteuern
	stx sc_screenvek
	ldx sc_scvek2+1
	stx sc_screenvek+1
	sec 		; Modul verlassen
	rts 

; ----------------------------------------- Load mit Dateifilter

lddir	lda #<(ptr)	; Zeiger auf Filter
	pha 
	lda #>(ptr)
	pha 
	lda #8		; Flag für "nur Directory zeigen"
	jmp gd_xloadm	; und ausführen

; ----------------------------------------- Mauszeiger einlesen

readptr	jsr gd_xopen	; Datei öffnen
	jsr onebyte	; Startadresse überlesen ($4733="g3")
	bne lderr
	jsr onebyte
	bne lderr

	ldy #0
ld6	jsr basin		; 63 Bytes einlesen
	ldx status	; Lesefehler?
	bne lderr		; dann Abbruch
	sta (sc_vekt20),y	; und in den Puffer übertragen
	iny 
	cpy #63
	bne ld6

	jsr onebyte	; Hotspot x lesen
	bne lderr
	sta xoff
	jsr onebyte	; Hotspot y lesen
	tay
	txa
	and #$bf		; EOF ausschließen
	tax
	bne lderr
	sty yoff
	stx ok		; successful

lderr	jmp gd_xclose	; Datei schließen

; ----------------------------------------- Byteweise lesen

onebyte	jsr basin
	ldx status
	rts 

; ----------------------------------------- Change Sysdrive

chgdrv	lda ls_sysdrive	; prevents hanging system
	ldx ls_bootdrive
	sta ls_bootdrive
	stx ls_sysdrive
	rts

; ----------------------------------------- Filter

ptr	!pet "ptr.*"
	!by 0
xoff	!by 0
yoff	!by 0

; ----------------------------------------- 

modend	!eof

; ----------------------------------------- 


