	!src "godotlib.lib"
	*=$c000
	!ct pet
;	!sl "labels.txt"

; ------------------------------------------------ 
;
; mod..FirstDrive
;  module to set the first access drive
;
; 1.00, 04.12.93, first release
;
; ------------------------------------------------ 

; ------------------------------------------------ 

	jmp start
	!by $20
	!wo 0
	!wo modend
	!wo 0
	!tx "Set first Drive "
	!tx "1.00"
	!tx "04.12.93"
	!tx "A.Dettke        "

; ------------------------------------------------ 

start	jsr makech	; Listbox-Kringel
	ldx #<firstlst	; Requester
	ldy #>firstlst
	jsr gd_screen
	lda ls_bootdrive	; jetzigen Bootdrive sichern
	sta accdrv
	jsr gd1		; und anzeigen
	jsr gd_eloop	; auf Klicks warten

; ------------------------------------------------ Event: Cancel

evcan	sec 		; leave module
	rts 

; ------------------------------------------------ Event: Get new Bootdrive

evgetdrv	ldx accdrv	; Drivenummer erhöhen
	inx 
	cpx #12		; bis 12 (ausschließlich)
	bne gd0
	ldx #8		; bei 8 wieder anfangen
gd0	txa 
	sta accdrv

gd1	jsr gd_xcnvdez	; Nummer anzeigen
	cpx #$30		; führende Null zu Space
	bne gd2
	ldx #32
gd2	ldy #2
	sta drvtx,y
	txa 
	dey 
	sta drvtx,y

zeig	lda #<accgad	; Kurzform des Text Output Handlers
	sta sc_screentab
	lda #>accgad
	sta sc_screentab+1
	lda #0
	tax 
	tay 
gt0	jsr gd_setpos
	cpx #3
	bne gt0
	jsr gd_trim
	jsr gd_initmove

	ldy #6
	jmp gd_xtxout1

; ------------------------------------------------ Event: Accept

evacc	lda accdrv	; gewählter Drive
	sta ls_bootdrive	; wird Bootdrive
	sec 
	rts 

; ------------------------------------------------ 

makech	ldx #7
mc0	lda char,x
	sta sc_undochar,x
	dex 
	bpl mc0
	rts 

; ------------------------------------------------ Data

char	!by $00,$3a,$46,$4e,$40,$40,$3c,$00
accdrv	!by 8

; ------------------------------------------------ Screenlist

firstlst	!by 0
	!by 16,13,18,8,$81
	!wo 0
	!scr " Set FirstDrive @"
accgad	!by 18,25,5,3,$c0
	!wo evgetdrv
drvtx	!scr 223,"  @"
	!by 21,14,8,3,$cc
	!wo evacc
	!scr "Accept@"
	!by 21,22,8,3,$cc
	!wo evcan
	!scr "Cancel@"
	!by $c0,18,13,11
	!scr "1st Access:@"
	!by $80

; ------------------------------------------------ 

modend	!eof
