
	!src "godotlib.lib"
	*= $c000
	!ct pet
;	!sl "labels.txt"

; ----------------------------------------- 
;
;  mod.QuickMask
;   Module to generate masks for ldr.4Bit&Mask
;   contributed by Martin Bohnet
;
;  1.00,         , first release (not system friendly)
;  1.01,         , rewrote module for system convenience
;  1.02,         , bug fixes
;  1.03, 14.01.01, final release
;  1.04, 08.11.18, added fill feature for better masking
;  1.05, 26.11.18, better usability
;  1.06, 20.06.19, added init keyboard for SuperCPU
;  1.07, 08.11.20, added option to "re-render" (hitting Shift on "Make")
;  1.08, 23.11.20, bugfix: shift must be scanned directly from keybord
;
; ----------------------------------------- 

; ----------------------------------------- Equates

	bitmap		= $2000

	adcnt		= $30
	counter		= $31
	bits		= $32

	orgx0		= $33	; /$34: position of click
	orgy0		= $35
	dst		= $36	; /$37: work vector
	dirctn		= $38	; $ff: up, $00: dwn
	help		= $39	; 
	pixeladd		= $3a	; /$3b: address of click
	fmode		= $3c
	nextline		= $3d
	byte		= $3e
	null		= $3f
	dst0		= $40	; /$41
	cmpt		= $42	; /$43

	zeile		= sc_merk
	spalte		= zeile+1
	shift		= $028d

	sysbag		= $ca00

	colors		= $0400
	sprptr		= $07f8

	spr255		= $3fc0
	spr2x		= $d004
	spr2y		= $d005
	msbx		= $d010
	spren		= $d015
	sprxe		= $d01d
	border		= $d020
	sprcol2		= $d029

; ----------------------------------------- 

header	jmp start
	!by $20
	!by 0
	!by 0
	!wo modend
	!wo 0
	!tx "Generate Mask   "
	!tx "1.08"
	!tx "23.11.20"
	!tx "ADettke/M.Bohnet"

; ----------------------------------------- Main

start	jsr gd_savescvek	; save vector to previous screenlist
	ldx sc_scvek2
	ldy sc_scvek2+1
	stx list
	sty list+1
	lda #200
	sta cntwert

	lda #0
	sta sc_keyprs
	sta fillflg
	jsr show		; requester
	jsr clrmasks	; init mask flags
	jsr gd_eloop	; ...and wait

	php
	ldx list		; then: rebuild previous screenlist
	ldy list+1
	stx sc_scvek2
	sty sc_scvek2+1
	plp
	rts

; ----------------------------------------- Event: Generate Mask

evmask	sei
	lda #$fd		; col %11111101
	sta $dc00
	lda $dc01		; row %10000000
	and #$80		; if .z: shift pressed
	cli
	beq +
	lda #0
	!by $2c
+	lda #$ff		; not pressed
	sta gr_redisp	; if yes, re-render on "Display"

	lda #$40		; vekt20=$4000 (source)
	sta sc_vekt20+1
	sta ls_vekta8	; vekta8= 8000 (counter)
	ldx #$20
	stx sc_texttab+1	; texttab=$2000 (dest)
	dex
	stx ls_vekta8+1
	ldy #0
	sty sc_vekt20
	sty sc_texttab
	lda cntwert	; init activity
	jsr initad

loop1	ldy #0
	sty bits
lp0	lda (sc_vekt20),y	; get 4bit pixel
	pha
	lsr
	lsr
	lsr
	lsr
	tax
	lda masks,x	; take maskflags as mask bits
	lsr
	rol bits
	pla
	and #15
	tax
	lda masks,x
	lsr
	rol bits		; generate 1 mask byte
	iny
	cpy #4
	bne lp0

	lda bits
	ldy #0
	sta (sc_texttab),y	; and write to destination
	clc
	lda sc_vekt20	; proceed
	adc #3
	sta sc_vekt20
	bcc lp1
	inc sc_vekt20+1
lp1	jsr adinc		; activity
	jsr count
	bne loop1		; count 8000 bytes

	jsr clearad	; then: activity off
	clc
	rts

; ----------------------------------------- Event: View Mask

evview	lda #$1b		; hires graphics on
	sta $d018
	lda #$3b
	sta $d011
	lda #$f0		; light grey on black
	ldy #250
vw0	sta colors-1,y
	sta colors+249,y
	sta colors+499,y
	sta colors+749,y
	dey
	bne vw0
	sty sc_stop
	sty sc_keyprs

	jsr wait		; wait for click
	bit fillflg	; check whether used from evfill
	bpl voff
	rts

voff	ldx #$13		; no, graphics off
	lda #$1b
	stx $d018
	sta $d011
vw2	ldx list		; restore requester
	ldy list+1
	jsr gd_screen
	jsr show		; show current setting
	jsr cos
	clc
	rts

; ----------------------------------------- Wait for Clicks

wait	lda joy		; wait for click or STOP
	eor #$ff
	and #$10
	ora sc_keyprs
	ora sc_stop
	beq wait
	rts

; ----------------------------------------- Event: Leave

evleave	sec
	rts

; ----------------------------------------- Show Requester

show	ldx #<plist
	ldy #>plist
	jsr gd_screen
	jsr setgray
	jsr setcol
	jsr setcolline
	jmp gd_invert

; ----------------------------------------- Set Parameters

setmskline
	lda #<mskline	; Masks
	sta sc_screentab
	lda #>mskline
	bne gt1
setcolline
	lda #<colline	; respective Colors
	sta sc_screentab
	lda #>colline
gt1	sta sc_screentab+1
	ldx #0
	ldy #0
gt0	jsr gd_setpos
	cpx #4
	bne gt0
	jsr gd_trim
	lda #0
	sta sc_loop
	jmp gd_initmove

; ----------------------------------------- Event: Invert Choice

evinvert	ldx #15		; toggle each color mask flag
sin1	lda masks,x
	eor #1
	sta masks,x
	dex
	bpl sin1
	jsr cos
	lda cntwert	; init activity
	jsr initad

	ldy #0
	lda #<$2000	; revert graphics buffer at $2000
	ldx #>$2000
	sta sc_vekt20
	stx sc_vekt20+1
	lda #<8000	; count 8000 bytes
	ldx #>8000
	sta ls_vekta8
	stx ls_vekta8+1
sin0	lda (sc_vekt20),y	; revert
	eor #$ff
	sta (sc_vekt20),y
	jsr adinc		; activity
	jsr count		; count
	bne sin0

	jsr clearad	; then: activity off
	clc
	rts

; ----------------------------------------- Event: Fill

evfill	jsr setfflg
	jsr evview	; graphics on
	cmp #3		; STOP?
	beq +		; yes, end

	jsr savesys	; save system data from corruption
-	jsr getxy		; compute position x|y
	stx sc_stop	; returned .x=0 (re-init stop)
	jsr caddr		; compute start address in mask, set mode
	jsr fill
	jsr wait		; wait for click to fill more parts
	lda sc_stop	; until STOP
	beq -
	jsr savesys	; restore system data

+	jsr setfflg
	jsr voff		; graphics off
	clc
	rts

; ----------------------------------------- 

setfflg	lda fillflg
	eor #$ff
	sta fillflg
	rts

; ----------------------------------------- Get Screen Co-Ordinates of Click

getxy	ldx #0
	lda spritey-2	; mouse y
	sec
	sbc sy_soffy
	bcs +
	txa
+	sta orgy0		; store screen pos

	lda spritehi	; position >255?
	and #1
	sta orgx0+1
	lda spritex	; mouse x
	sec
	sbc sy_soffx
	sta orgx0
	bcs +
	dec orgx0+1
	bpl +
	stx orgx0+1
	stx orgx0
+	rts

; ----------------------------------------- Compute Screen Address of Click

caddr	jsr gd_position
	lda #0
	sta pixeladd
	sta pixeladd+1
	ldy zeile		; y:
	beq ++
-	lda pixeladd
	clc		; get tile row
	adc #<320
	sta pixeladd
	lda pixeladd+1
	adc #>320
	sta pixeladd+1
+	dey
	bne -
++	lda orgy0
	and #7		; add tile scanline
	clc
	adc pixeladd
	sta pixeladd
	bcc +
	inc pixeladd

+	ldy spalte	; x:
	beq ++
-	lda pixeladd
	clc		; get tile
	adc #8
	sta pixeladd	; result lo
	sta dst
	sta dst0
	sta cmpt
	bcc +
	inc pixeladd+1
+	dey
	bne -

++	clc
	lda pixeladd+1
	adc #>bitmap
	sta pixeladd+1	; result hi
	sta dst+1
	sta dst0+1
	sta cmpt+1

	lda orgx0		; get pixel
	and #7
	tax
	ldy #0
	lda (pixeladd),y
	and bitset,x
	beq +
	lda #$ff
+	sta fmode		; set fillmode
	rts

; ----------------------------------------- Fill Screen

fill	ldy #2		; blink border
	jsr graphon	; returns .y=0
	dey
	sty dirctn	; flag for up ($ff) or down ($00)
	sty null

	lda orgy0		; up
	sta nextline
-	jsr scan		; scan line for pixels
	lda null		; none?
	beq +
	lda #1		; yes, end scan
	sta nextline
+	lda #$ff
	sta null
	dec nextline	; next scanline up, until 0
	lda nextline
	cmp #$ff
	bcc -

	lda #0		; down
	sta dirctn
	lda dst0		; set paramters
	sta cmpt
	sta pixeladd
	lda dst0+1
	sta cmpt+1
	sta pixeladd+1
	lda orgy0
	sta nextline
-	jsr scan		; scan line for pixels
	lda null		; none?
	beq +
	lda #199		; yes, end scan
	sta nextline
+	lda #$ff
	sta null
	inc nextline	; next scanline down, until 200
	lda nextline
	cmp #200
	bcc -

	clc
	rts

; ----------------------------------------- Compute Byte Address

newbyte	ldy dirctn
	beq +++

	and #7		; .a=nextline
	cmp #7
	beq +
	ldx #0
	lda #1
	bne ++
+	lda #<313
	ldx #>313

++	sta help
	sec
	lda cmpt
	sbc help
	sta cmpt
	stx help
	lda cmpt+1
	sbc help
	sta cmpt+1
	lda cmpt
	ldx cmpt+1
	rts

+++	and #7
	beq +
	ldx #0
	lda #1
	bne ++
+	lda #<313
	ldx #>313

++	sta help
	clc
	lda cmpt
	adc help
	sta cmpt
	stx help
	lda cmpt+1
	adc help
	sta cmpt+1
	lda cmpt
	ldx cmpt+1
+	rts

; ----------------------------------------- Init Line Scan

setscan	lda nextline
	cmp orgy0
	beq +
	jsr newbyte
	bne ssc0
+	lda pixeladd
	ldx pixeladd+1
ssc0	sta dst
	stx dst+1
	ldx spalte	; get counter for left or right border
	ldy #0
	rts

; ----------------------------------------- Set or Unset Pixels to left

checklft	jsr decdst	; get next to the left
	sty null
	lda (dst),y
	cmp fmode		; fmode?
	bne +
	jmp fulll		; yes, fill startbyte

+	tay
	eor #$ff		; anti-fmode?
	cmp fmode
	bne +

	jsr fillupl	; yes, fill byte
	sta (dst),y
	ldx #0		; end scanline
	rts

+	lda byte		; startbyte: anti-fmode?
	eor #$ff
	cmp fmode
	beq +
	sty byte		; no, new byte
	stx help
	jsr fillupl2	; fillup this one
	sta (dst),y
	jsr fulll		; fill last one
	ldx help		; proceed in scanline
+	rts

; ----------------------------------------- 

fulll	jsr incdst
	lda fmode
	sta (dst),y
	rts

fillupl	jsr incdst
fillupl2	ldx #$ff
	lda #0
	bit fmode
	bvc +
	jsr fillasl	; set
	lda onlrest,x
	ldy #0
	rts

+	jsr filllsr	; unset
	lda onrrest,x
	ldy #0
	rts

; ----------------------------------------- Scan one Scanline

scan	jsr setscan	; init scan: to left
	beq scl0
-	lda (dst),y	; get current byte
	sta byte
	eor fmode		; equals to fmode (either $00 or $ff)?
	beq scl0		; yes, no change
	jsr checklft	; no, check
scl0	jsr decdst	; proceed
	dex
	bpl -		; until border

; ----------------------------------------- 

	lda cmpt		; init scan: to right
	ldx cmpt+1
	jsr ssc0

-	lda (dst),y	; get current byte
	sta byte
	eor fmode		; equals to fmode (either $00 or $ff)?
	beq scr0		; yes, no change
	jsr checkrgt	; no, check
scr0	jsr incdst	; proceed
	inx
	cpx #40
	bcc -		; until border
	rts

; ----------------------------------------- Set or Unset Pixels to right

checkrgt	jsr incdst
	sty null
	lda (dst),y
	cmp fmode		; fmode?
	bne +
	jmp fullr

+	tay
	eor #$ff		; anti-fmode?
	cmp fmode
	bne +

	jsr fillupr	; fill up last scanned byte 
	sta (dst),y
	ldx #40		; end scanline
	rts

+	lda byte		; startbyte: anti-fmode?
	eor #$ff
	cmp fmode
	beq +
	sty byte
	stx help
	jsr fillupr2
	sta (dst),y
	jsr fullr
	ldx help
+	rts

; ----------------------------------------- 

fullr	jsr decdst
	lda fmode
	sta (dst),y
	rts

fillupr	jsr decdst
fillupr2	ldx #$ff
	lda #0
	bit fmode		; mode?
	bvc +
	jsr filllsr	; set
	lda onrrest,x	; fill up
	ldy #0		; reset .y
	rts

+	jsr fillasl	; unset
	lda onlrest,x
	ldy #0
	rts

; ----------------------------------------- Next Byte

decdst	sec		; predecessor
	lda dst
	sbc #8
	sta dst
	bcs +
	dec dst+1
+	rts

incdst	clc		; decessor
	lda dst
	adc #8
	sta dst
	bcc +
	inc dst+1
+	rts

; ----------------------------------------- Find first Bit set

fillasl	inx
	cpx #8
	beq +
	asl byte
	rol
	tay
	beq fillasl
+	rts

filllsr	inx
	cpx #8
	beq +
	lsr byte
	ror
	tay
	beq filllsr
+	rts

; ----------------------------------------------------- Blink the Border

graphon	lda border	; save border color
	pha
	sty border	; set new color (in .y, e.g. "2" for red)
	ldx #$40
	jsr gd_dl2	; delay
	pla
	sta border	; reset old border color
	stx sy_tbuffer	; clear key strokes
stay	clc
	rts

; ----------------------------------------- Save and Restore Systen Data

savesys	ldx #0
-	lda bitmap-256,x
	tay
	lda sysbag,x
	sta bitmap-256,x
	tya
	sta sysbag,x
	inx
	bne -
	rts

; ----------------------------------------- Counters

count	inc sc_vekt20
	bne cou5
	inc sc_vekt20+1
cou5	inc sc_texttab
	bne cou6
	inc sc_texttab+1
cou6	lda ls_vekta8
	bne cou7
	dec ls_vekta8+1
cou7	dec ls_vekta8
	lda ls_vekta8
	ora ls_vekta8+1
	rts

; ----------------------------------------- Events: Mark (a single) Color

col0	ldx #0
	!by $2c
col1	ldx #1
	!by $2c
col2	ldx #2
	!by $2c
col3	ldx #3
	!by $2c
col4	ldx #4
	!by $2c
col5	ldx #5
	!by $2c
col6	ldx #6
	!by $2c
col7	ldx #7
	!by $2c
col8	ldx #8
	!by $2c
col9	ldx #9
	!by $2c
cola	ldx #10
	!by $2c
colb	ldx #11
	!by $2c
colc	ldx #12
	!by $2c
cold	ldx #13
	!by $2c
cole	ldx #14
	!by $2c
colf	ldx #15
	lda masks,x
	eor #1
	sta masks,x

cos	jsr setmskline	; address mask line, first entry
	ldx #0
	stx counter
cos2	ldx counter
	lda masks,x
	bne cos0
	lda #32		; fill /w space...
	bne cos1
cos0	lda #160		; ...or shift space
cos1	jsr gd_fi0
	sec 
	lda sc_vekt20	; minus 38 (one line above, two tiles ahead)
	sbc #38
	sta sc_vekt20
	bcs cos3
	dec sc_vekt20+1
cos3	inc counter	; 16 times (all colors)
	lda counter
	cmp #16
	bcc cos2
	clc
	rts

; ----------------------------------------- Show Colors

setcol	ldx colline+1
	ldy colline
	lda #2		; Breite und Höhe: 2 Kacheln
	iny
stc1	sty sc_zl
	inx
	stx sc_sp
	ldx #2
	stx sc_br
	sta sc_ho
	ldx #0
stc0	stx cmerk
	lda gr_basepal,x
	sta sc_chgcol
	ldx #4
	jsr gd_fcol
	inc sc_sp
	inc sc_sp
	ldx cmerk
	inx
	cpx #16
	bne stc0
	rts
; 
; ----------------------------------------- Mark Color

setgray	jsr setcolline
	ldx #11
	lda gr_basepal,x	; set light grey
	sta sc_chgcol
	ldx #4
	jmp gd_fcol

; ----------------------------------------- Clear all Mask Flags

clrmasks	ldx #15
	lda #1
cm0	sta masks,x
	dex
	bpl cm0
	rts

; -------------------------------------------------- Activity Display
;   sprite activity display
; ------------------------------------------------ Abschluss Activity

clearad	lda spren		; Wanderbalken aus
	and #$f3
	sta spren
	lda msbx
	and #$f3
	sta msbx
	rts 

; ------------------------------------------------ Activity

adinc	dec adcnt
	bne +
	inc spr2y
	inc spr2y+2
	lda cntwert
	sta adcnt
+	rts 

; ------------------------------------------------ Init Activity

initad	sta adcnt
	ldy #60
	lda #0
-	sta spr255+3,y	; Spriteblock 255 vorbereiten
	dey 
	bpl -
	sty spr255	; dünner Strich
	sty spr255+1
	sty spr255+2
	lda #$0f		; Farbe hellgrau
	sta sprcol2
	sta sprcol2+1
	lda sprxe		; doppelt breit
	ora #$0c
	sta sprxe

	lda msbx		; x über 255
	ora #$0c
	sta msbx
	lda #8		; x-Ordinate: 8 (plus 256)
	sta spr2x
	lda #$20
	sta spr2x+2
	lda #$92		; y-Ordinate: 146
	sta spr2y		; (im Image Information Fenster)
	sta spr2y+2

	sty sprptr+2	; Sprite 255
	sty sprptr+3
	lda spren
	ora #$0c		; Activity-Balken aktivieren
	sta spren
	rts

; ----------------------------------------- Data

list	!by 0,0
masks	!by 1,1,1,1,1,1,1,1
	!by 1,1,1,1,1,1,1,1
cntwert	!by 200
cmerk	!by 0
mskline	!by 5,3,4,3
fillflg	!by 0
bitset	!by $80,$40,$20,$10,$08,$04,$02,$01
bitreset	!by $7f,$bf,$df,$ef,$f7,$fb,$fd,$fe
onlrest	!by $ff,$7f,$3f,$1f,$0f,$07,$03,$01,$00
onrrest	!by $ff,$fe,$fc,$f8,$f0,$e0,$c0,$80,$00

; ----------------------------------------- Screenlist

plist	!by 0
	!by 1,3,34,11,$91
	!wo 0
	!scr "QuickMask@"
co0	!by 3,3,4,5,$40
	!wo col0
co1	!by 3,5,4,5,$40
	!wo col1
co2	!by 3,7,4,5,$40
	!wo col2
co3	!by 3,9,4,5,$40
	!wo col3
co4	!by 3,11,4,5,$40
	!wo col4
co5	!by 3,13,4,5,$40
	!wo col5
co6	!by 3,15,4,5,$40
	!wo col6
co7	!by 3,17,4,5,$40
	!wo col7
co8	!by 3,19,4,5,$40
	!wo col8
co9	!by 3,21,4,5,$40
	!wo col9
coa	!by 3,23,4,5,$40
	!wo cola
cob	!by 3,25,4,5,$40
	!wo colb
coc	!by 3,27,4,5,$40
	!wo colc
cod	!by 3,29,4,5,$40
	!wo cold
coe	!by 3,31,4,5,$40
	!wo cole
cof	!by 3,33,4,5,$40
	!wo colf

colline	!by 3,3,34,5,$0a
	!wo 0
	!by 9,4,6,3,$cc
	!wo evmask
	!scr "Make@"
	!by 9,10,6,3,$cc
	!wo evview
	!scr "View@"
	!by 9,16,8,3,$cc
	!wo evinvert
	!scr "Invert@"
	!by 9,24,6,3,$cc
	!wo evfill
	!scr "Fill@"
	!by 9,30,6,3,$cc
	!wo evleave
	!scr "Exit@"

	!by $c0,7,6,26
	!scr "Choose transparent colors.@"
	!by $80

; ----------------------------------------- 

modend	!eof
