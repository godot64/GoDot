
	!src "godotlib.lib"
	*= $c000
	!ct pet

; ------------------------------------------------------------ 
;
; mod..NewPointer
; Modul zum Installieren eines alternativen Mauszeigers
;
; Die Adressen sy_soffx und sy_soffy enthalten den Basisoffset
; des Mauszeiger-Sprites (x=24, y=50). Ein Zeiger-Hotspot muss
; dazu gerechnet werden. Die Offsets des Hotspots stehen in den
; beiden letzten Bytes der Zeigerdatei.
;
; 1.00, 13.06.92, first release
; 1.01, 25.07.17, Modul sicherer gemacht
; 1.02, 07.11.20, Zeiger-Hotspot begrenzt auf Spritegröße
; 1.03, 07.11.20, added: set default values if no hotspot data in file
;
; ------------------------------------------------------------ 

; ----------------------------------------- Equates

	ok		= $30

	buffer		= $c600

; ----------------------------------------- Header

header	jmp start
	!by $20
	!by 0
	!by 0
	!wo modend
	!wo 0
	!pet "Mouse Pointer   "
	!pet "1.03"
	!pet "07.11.20"
	!pet "A. Dettke       "

; ----------------------------------------- Main

start	jsr chgdrv	; Sysdrive auf Bootdrive umlenken
	ldx #<buffer	; Zeiger auf Directory-Filter setzen ("ptr.*")
	ldy #>buffer
	stx ls_adrmisc	; (wird bei Klick auf LOAD nach sc_vekt20 geschrieben)
	sty ls_adrmisc+1
	sty ok		; Success-Flag
	lda gd_modswitch	; Modul-Management
	pha 
	lda #0		; und mit 0 vorbesetzen (für Rückmeldung)
	sta ls_flen
	sta xoff		; Hotspot vorbesetzen
	sta yoff

	jsr lddir		; gefiltertes Directory anzeigen (Filerequester)
	jsr gd_eloop	; warten auf Mausklick (Load oder Cancel)

	lda ls_flen	; kein File ausgewählt?
	beq lp1		; dann Abschluss

	jsr readptr	; sonst: Mauszeiger einlesen
	lda ok		; geklappt?
	bne lp1		; nein, Abbruch, alter Zeiger bleibt

	ldy #62		; erfolgreich, neuen Mauszeiger installieren
-	lda buffer,y	; 63 Bytes übertragen
	sta sc_pointer,y
	dey
	bpl -

	lda xoff		; Hotspot x aus Datei größer als 24?
	cmp #25
	bcc pt0
	lda #24		; ja, auf 24 begrenzen ### 1.02
pt0	sec 
	lda #24		; dann von Basisoffset abziehen (linker Rand)
	sbc xoff
	sta sy_soffx
	lda yoff		; Hotspot y größer als 21?
	cmp #22
	bcc lp0
	lda #21		; ja, auf 21 begrenzen ### 1.02
lp0	sec 
	lda #50		; dann von Basisoffset abziehen (oberer Rand)
	sbc yoff		; (damit bleibt der Hotspot im sichtbaren Screen)
	sta sy_soffy

lp1	jsr gd_spron	; Mauszeiger an (ist aus durch gd_xopen)
	pla 		; aufräumen
	sta gd_modswitch
	jsr chgdrv	; Sysdrive zurück

	ldx sc_scvek2	; Main-Screenlist ansteuern
	stx sc_screenvek
	ldx sc_scvek2+1
	stx sc_screenvek+1
	sec 		; Modul verlassen
	rts 

; ----------------------------------------- Load mit Dateifilter

lddir	lda #<ptr		; Zeiger auf Filter
	pha 
	lda #>ptr
	pha 
	lda #8		; Flag für "nur Directory zeigen"
	jmp gd_xloadm	; und ausführen

; ----------------------------------------- Mauszeiger einlesen

readptr	jsr gd_xopen	; Datei öffnen
	jsr onebyte	; Startadresse überlesen ($4733="g3")
	bne lderr
	jsr onebyte
	bne lderr

	ldy #0
ld6	jsr onebyte	; 63 Bytes einlesen
	bne lderr		; bei Lesefehler Abbruch
	sta (sc_vekt20),y	; und in den Puffer übertragen
	iny 
	cpy #63
	bne ld6

	jsr onebyte	; Hotspot x lesen (Byte 64)
	beq +
	jsr default
+	and #31		; auf Zeiger begrenzen ### 1.02
	sta xoff		; und sichern
	jsr onebyte	; Hotspot y lesen (Byte 65)
	and #31		; auf Zeiger begrenzen ### 1.02
	tay
	txa
	and #$bf		; EOF ausschließen
	tax
	beq +
	jsr default
+	sty yoff		; sichern
	stx ok		; successful

lderr	jmp gd_xclose	; Datei schließen

default	lda #0		; set 0 as default value for hotspot
	tay
	rts

; ----------------------------------------- Byteweise lesen

onebyte	jsr basin
	ldx status
	rts 

; ----------------------------------------- Change Sysdrive

chgdrv	lda ls_sysdrive	; prevents hanging system
	ldx ls_bootdrive
	sta ls_bootdrive
	stx ls_sysdrive
	rts

; ----------------------------------------- Filter

ptr	!pet "ptr.*",0
xoff	!by 0
yoff	!by 0

; ----------------------------------------- 

modend	!eof

; ----------------------------------------- 


