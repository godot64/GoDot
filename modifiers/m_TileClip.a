
	!src "godotlib.lib"
	*= $c000
	!ct pet
;	!sl "labels.txt"

; ------------------------------------------------ 
;
; mod.TileClip
;  
;
; 1.00, 14.07.95, first release
; 1.01, 07.04.98, added mode: Border
;
; ------------------------------------------------ 

; ------------------------------------------------ Declarations

	adcnt		= $30
	src		= $35
	dst		= $37
	colnr		= $39
	linenr		= $39
	blcnt		= $3b
	which		= $42
	bbuf		= $3d
	tsrc		= $3e
	tcnt		= $40
	null		= $41

	ybuf0		= $cb00
	ybuf1		= $cc00

	sprptr		= $07f8
	spr255		= $3fc0

	spr2x		= $d004
	spr2y		= $d005
	msbx		= $d010
	spren		= $d015
	sprxe		= $d01d
	border		= $d020
	sprcol2		= $d029

	xbuf0		= $c800
	xbuf1		= $ca00
	xbuf1e		= xbuf1+320

; ------------------------------------------------ Header

	jmp start
	!by $20
	!wo 0
	!wo modend
	!wo 0
	!tx "Tile The Clip   "
	!tx "1.01"
	!tx "07.04.98"
	!tx "W.Kling/A.Dettke"

; ------------------------------------------------ Main

start	ldx #<tclst
	ldy #>tclst
	jmp gd_xmloop

; ------------------------------------------------ Event: Accept

accept	lda sc_lastclpbr	; höchste Clipbreite: 32!
	cmp #33
	bcs toowide
	jsr htile
	lda sc_lastclpho
	jsr vtile

; ------------------------------------------------ Event: Cancel

toowide	jsr clearad
	lda #$ff
	sta gr_redisp
	sec
	rts

; ------------------------------------------------ Process Vertically

vtile	asl		; Cliphöhe mal 8
	asl
	asl
	sta tcnt
	lda #8		; set activity
	jsr initad
	lda sc_lastclpzl	; Startzeile mal 8
	asl
	asl
	asl
	sta tsrc

	lda #>ybuf0
	sta tsrc+1
	ldx sc_lastclpho
	lda yax,x
	ldy centered	; welcher Modus?
	beq yms1		; Edge?
	cpy #2		; ### 1.01
	bne yms10
	ldx #0		; Center?
yms10	lda ycen,x	; ###
yms1	sta yoff		; Border?

	lda #0
	sta colnr
	sta colnr+1
vloop00	lda colnr+1
	lsr
	sta src+1
	lda colnr
	ror
	bcs odd0
	ldx #$0f
	!by $2c
odd0	ldx #$f0
	stx which
	tax
	and #$03
	sta src
	txa
	and #$fc
	asl
	rol src+1
	asl
	rol src+1
	asl
	rol src+1
	ora src
	sta src
	lda src+1
	ora #>sy_4bit
	sta src+1

	ldx #0
	stx null
vloop0	ldy #0
vloop1	lda (src),y
	bit which
	bvs odd1
	lsr
	lsr
	lsr
	lsr

odd1	and #$0f
	sta ybuf0,x
	beq nuls0
	sta null
nuls0	inx
	iny
	iny
	iny
	iny
	cpy #32
	bcc vloop1
	lda src+1
	clc
	adc #>1280
	sta src+1
	cmp #>sy_bigbuffer
	bcc vloop0

	lda null
	bne ydoit
	jmp nextcol

ydoit	ldy yoff
	ldx #0
	beq vtl1
vtl0	ldy #0
vtl1	lda (tsrc),y
	sta ybuf1,x
	inx
	cpx #200
	beq vok0
	iny
	cpy tcnt
	bne vtl1
	lda ymir		; Mirror?
	beq noymir
	ldy tcnt		; yes, read from rear
	dey
vtl2	lda (tsrc),y
	sta ybuf1,x
	inx
	cpx #200
	beq vok0
	dey
	cpy #$ff
	bne vtl2
noymir	jmp vtl0

vok0	lda colnr+1
	lsr
	sta dst+1
	lda colnr
	ror
	bcs odd2
	ldx #$0f
	!by $2c
odd2	ldx #$f0
	stx which
	tax
	and #$03
	sta dst
	txa
	and #$fc
	asl
	rol dst+1
	asl
	rol dst+1
	asl
	rol dst+1
	ora dst
	sta dst
	lda dst+1
	ora #>sy_4bit
	sta dst+1

	ldx #0
vloop2	ldy #0
vloop3	lda ybuf1,x
	bit which
	bvs odd3
	asl
	asl
	asl
	asl
odd3	sta bbuf
	lda (dst),y
	and which
	ora bbuf
	sta (dst),y
	inx
	iny
	iny
	iny
	iny
	cpy #32
	bcc vloop3
	lda dst+1
	clc
	adc #>1280
	sta dst+1
	cmp #>sy_bigbuffer
	bcc vloop2

nextcol	inc colnr
	bne vskip0
	inc colnr+1
vskip0	lda colnr
	cmp #<320
	bne vskip1
	lda colnr+1
	bne vok1
vskip1	dec adcnt
	bne vadskp
	inc spr2y
	inc spr2y+2
	lda #8
	sta adcnt
vadskp	jmp vloop00
vok1	rts

; ------------------------------------------------ Process Horizontally

htile	asl		; Clipbreite mal 8
	asl
	asl
	sta tcnt
	lda #5
	jsr initad
	lda #>xbuf0
	sta tsrc+1
	lda sc_lastclpsp	; Startspalte mal 8
	asl
	asl
	asl
	bcc tss
	inc tsrc+1
tss	sta tsrc

	ldx sc_lastclpbr
	lda xax,x
	ldy centered
	beq xms1
	cpy #2		; ### 1.01
	bne xms10
	ldx #0
xms10	lda xcen,x	; ###
xms1	sta xoff

	lda #0
	sta linenr
hloop00	lda linenr
	pha
	lsr
	lsr
	lsr
	tax
	pla
	and #$07
	asl
	asl
	sta dst
	lda line8,x
	sta dst+1

	lda #<xbuf0
	ldx #>xbuf0
	sta src
	stx src+1
	ldx #0
	stx null
	lda #40
	sta blcnt
hloop0	ldy #0
hloop1	lda (dst),y
	beq nuls1
	sta null
nuls1	pha
	lsr
	lsr
	lsr
	lsr
	sta (src,x)
	inc src
	bne hskip0
	inc src+1
hskip0	pla
	and #$0f
	sta (src,x)
	inc src
	bne hskip1
	inc src+1
hskip1	iny
	cpy #4
	bne hloop1
	lda dst
	clc
	adc #32
	sta dst
	bcc hskip2
	inc dst+1
hskip2	dec blcnt
	bne hloop0

	lda null
	bne xdoit
	jmp nextline

xdoit	lda #<xbuf1
	ldx #>xbuf1
	sta dst
	stx dst+1
	ldy xoff
	ldx #0
	beq htl1
htl0	ldy #0
htl1	lda (tsrc),y
	sta (dst,x)
	inc dst
	bne xskip1
	inc dst+1
xskip1	lda dst+1
	cmp #>xbuf1e
	bne xskip2
	lda dst
	cmp #<xbuf1e
	beq hok0
xskip2	iny
	cpy tcnt
	bne htl1
	lda xmir
	beq noxmir
	ldy tcnt
	dey
htl2	lda (tsrc),y
	sta (dst,x)
	inc dst
	bne xskip3
	inc dst+1
xskip3	lda dst+1
	cmp #>xbuf1e
	bne xskip4
	lda dst
	cmp #<xbuf1e
	beq hok0
xskip4	dey
	cpy #$ff
	bne htl2
noxmir	jmp htl0

hok0	lda linenr
	pha
	lsr
	lsr
	lsr
	tax
	pla
	and #$07
	asl
	asl
	sta dst
	lda line8,x
	sta dst+1

	lda #<xbuf1
	ldx #>xbuf1
	sta src
	stx src+1
	ldx #0
	lda #40
	sta blcnt
hloop2	ldy #0
hloop3	lda (src,x)
	asl
	asl
	asl
	asl
	inc src
	bne hskip3
	inc src+1
hskip3	ora (src,x)
	inc src
	bne hskip4
	inc src+1
hskip4	sta (dst),y
	iny
	cpy #4
	bne hloop3
	lda dst
	clc
	adc #32
	sta dst
	bcc hskip5
	inc dst+1
hskip5	dec blcnt
	bne hloop2
nextline	inc linenr
	lda linenr
	cmp #200
	beq hok
	dec adcnt
	bne adskp
	inc spr2y
	inc spr2y+2
	lda #5
	sta adcnt
adskp	jmp hloop00
hok	rts

; ------------------------------------------------ Start of 4Bit blockrows

line8	!by $40,$45,$4a,$4f,$54
	!by $59,$5e,$63,$68,$6d
	!by $72,$77,$7c,$81,$86
	!by $8b,$90,$95,$9a,$9f
	!by $a4,$a9,$ae,$b3,$b8

; ------------------------------------------------ sprite activity display

clearad	lda spren
	and #243
	sta spren
	lda msbx
	and #243
	sta msbx
	rts

initad	sta adcnt
	ldy #60
	lda #0
adl0	sta spr255+3,y
	dey
	bpl adl0
	sty spr255
	sty spr255+1
	sty spr255+2
	lda #15
	sta sprcol2
	sta sprcol2+1
	lda sprxe
	ora #12
	sta sprxe
	lda msbx
	ora #12
	sta msbx
	lda #8
	sta spr2x
	lda #32
	sta spr2x+2

	lda #146
	sta spr2y
	sta spr2y+2
	sty sprptr+2
	sty sprptr+3
	lda spren
	ora #12
	sta spren
	rts

; ------------------------------------------------ Data

xmir	!by 0
ymir	!by 0
centered	!by 0
xoff	!by 0
yoff	!by 0

xcen	!by 0,4,8,20,16		; x-centered (0..32)
	!by 20,8,36,64
	!by 20,40,60,80
	!by 100,8,20,32
	!by 44,56,68,80
	!by 92,104,116,128
	!by 140,152,164,176
	!by 188,200,212,224

xax	!by 0,0,0,8,0
	!by 0,32,8,32
	!by 56,0,16,32
	!by 48,64,80,96
	!by 112,128,144,0
	!by 8,16,24,32
	!by 40,48,56,64
	!by 72,80,88,96

ycen	!by 0,0,4,8,12,0,20		; y-centered (0..25)
	!by 40,60,8,20,32
	!by 44,56,68,80,92
	!by 104,110,128,140
	!by 152,164,176,188,0

yax	!by 0,4,12,20,28,20
	!by 44,12,28,44,60
	!by 76,92,4,12,20
	!by 28,36,44,52,60
	!by 68,76,84,92,100

; ------------------------------------------------ Event: Choose Mode

sevsnap	lda centered
	cmp #2		; ### 1.01
	bne cnt0		; ###
	lda #$ff
	sta centered
cnt0	inc centered
	ldx centered
	lda snapoff,x
	tax
	ldy #5
cnt1	lda snaptext,x
	sta snaptx,y
	dex
	dey
	bpl cnt1
	ldy #0
settx	jsr settext
	ldy #6
	jmp gd_xtxout1

; ------------------------------------------------ Event: Mirror X

sevmirrx	lda xmir
	beq cnt2
	lda #$ff
	sta xmir
cnt2	inc xmir
	ldx xmir
	lda mirroff,x
	tax
	ldy #2
cnt3	lda mirrtext,x
	sta mirrxtx,y
	dex
	dey
	bpl cnt3
	ldy #2
	jmp settx

; ------------------------------------------------ Event: Mirror Y

sevmirry	lda ymir
	beq cnt4
	lda #$ff
	sta ymir
cnt4	inc ymir
	ldx ymir
	lda mirroff,x
	tax
	ldy #2
cnt5	lda mirrtext,x
	sta mirrytx,y
	dex
	dey
	bpl cnt5
	ldy #4
	jmp settx

; ------------------------------------------------ Text Output Handler

tabigad	!wo snapgad
	!wo mirrxgad
	!wo mirrygad

settext	lda #<tabigad
	sta sc_texttab
	lda #>tabigad
	sta sc_texttab+1

gettext	lda (sc_texttab),y
	sta sc_screentab
	iny
	lda (sc_texttab),y
	sta sc_screentab+1
	lda #0
	tax
	tay
gt0	jsr gd_setpos
	cpx #3
	bne gt0
	stx sc_ho
	jsr gd_trim
	jmp gd_initmove

; ------------------------------------------------ Data

snaptext	!scr " Edge CenterBorder"
snapoff	!by 5,11,17
mirrtext	!scr " noyes"
mirroff	!by 2,5

; ------------------------------------------------ Screenlist

tclst	!by 0
	!by 9,13,16,11,$91
	!wo 0
	!scr "TileClip@"
snapgad	!by 11,21,8,3,$cf
	!wo sevsnap
snaptx	!scr " Edge @"
mirrxgad	!by 13,24,5,3,$ce
	!wo sevmirrx
mirrxtx	!scr " no@"
mirrygad	!by 15,24,5,3,$ce
	!wo sevmirry
mirrytx	!scr " no@"
	!by 17,13,8,3,$c7
	!wo accept
	!scr "Accept@"
	!by 17,21,8,3,$ce
	!wo toowide
	!scr "Cancel@"
	!by $c0,11,13,7
	!scr "Snap to@"
	!by $c0,13,13,8
	!scr "Mirror x@"
	!by $c0,15,13,8
	!scr "Mirror y@"
	!by $80

; ------------------------------------------------ 

modend	!eof
