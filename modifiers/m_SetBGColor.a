
	!src "godotlib.lib"
	*= $c000
	!ct pet
;	!sl "labels.txt"

; -----------------------------------------
;
;     mod.SetBGColor
;
;  Modifier to manipulate the background color of GoDot's image data
;
;  1.00, 08.11.05, first release
;
; ----------------------------------------- 

; ----------------------------------------- Equates

	adcnt		= $30
	fry0b		= $31
	fry1b		= $32
	hires		= $33
	double		= $34
	dst		= $37	; /$38
	linenr		= $39
	blcnt		= $3a
	frx0		= $3b
	frx1		= $3c
	fry0		= $3d
	fry1		= $3e

; ----------------------------------------- Header

	jmp start
	!by $20
	!wo 0
	!wo modend
	!wo 0
	!tx "Set new BG Col  "
	!tx "1.00"
	!tx "08.11.05"
	!tx "Arndt Dettke    "

; ----------------------------------------- Main

start	jsr makech
	ldx #<setbglst
	ldy #>setbglst
	jsr gd_screen
	sta sc_loop

	ldx palgad
	inx
	stx palzl
	stx sc_zl
	ldx palgad+1
	inx
	stx palsp
	stx sc_sp
	ldx #4
	stx sc_br
	stx sc_ho
	jsr gd_esm3
	lda gr_picked
	and #15
	tax
	jsr initpal
	jsr ea8

	jmp gd_eloop

; -----------------------------------------

makepal	lda gr_picked
	and #15
	sta plast
	lda palsp
	sta sc_sp
	pha
	ldx #1
	stx sc_br
	stx sc_ho
	dex
	stx cnty
	stx ptemp
mp1	stx cntx
	ldy ptemp
	lda gr_basepal,y
	sta sc_chgcol

	ldx #4
	jsr gd_fcol
	sec
	lda sc_vekt20
	sbc #40
	sta sc_vekt20
	lda sc_vekt20+1
	sbc #$d4
	sta sc_vekt20+1

	ldy sc_chgcol
	cpy plast
	beq mp2
	lda #160
	bne mp3
mp2	lda #171
mp3	ldy #0
	sta (sc_vekt20),y
	inc sc_sp
	inc ptemp
	ldx cntx
	inx
	cpx #4
	bne mp1
	pla
	pha
	sta sc_sp
	ldx #0
	inc sc_zl
	inc cnty
	lda cnty
	cmp #4
	bne mp1
	pla
	rts
;
; --------------------------------------- Event: Cancel

evcan	sec
	rts

; --------------------------------------- Event: Get Color from Chooser

evgetcol	sec
	lda sc_merk
	sbc palzl
	asl
	asl
	sta ptemp
	sec
	lda sc_merk+1
	sbc palsp
	clc
	adc ptemp
	tax
	lda gr_basepal,x
	tax
	ora #$80
	sta gr_picked

; -----------------------------------------

initpal	lda c64pal,x
	jsr makepal
	clc
	rts

; ----------------------------------------- Show color

showcol	sta sc_chgcol
	ldy #2
	jsr settab
	ldx #4
	jmp gd_fcol

; --------------------------------------- Event: Select Mode

evselmode	ldx mode		; 0: 4Bit/1: File
	beq ea0
	ldx #$ff
ea0	inx
	stx mode
	bne ea1
	ldy #4		; 4Bit
	bne ea4
ea1	ldy #9		; File 
ea4	ldx #4
ea2	lda modes,y
	sta modetx,x
	dey
	dex
	bpl ea2

ea8	lda mode
	bne ea6
	lda gr_bkcol
	jmp ea7
ea6	lda gr_fbkcol
ea7	jsr showcol
	ldy #0		; modegad
ea5	jsr settab
	ldy #6
	jmp gd_xtxout1

; --------------------------------------- Event: set new bg color (4Bit!)

evsetbg	lda gr_picked
	and #15
	sta gr_bkcol
	jsr ea8
	lda #1
	sta gr_redisp
	clc
	rts

; --------------------------------------- Textout

tabigad	!wo modegad		; 0
	!wo showgad		; 2
;
settab	lda #<tabigad
	sta sc_texttab
	lda #>tabigad
	sta sc_texttab+1

gettab	lda (sc_texttab),y
	sta sc_screentab
	iny
	lda (sc_texttab),y
	sta sc_screentab+1
	lda #0
	tax
	tay
gt0	jsr gd_setpos
	cpx #4
	bne gt0
	jsr gd_trim
	jmp gd_initmove

; ----------------------------------------- Create Toggle Indicator

makech	ldx #7
mc0	lda char,x
	sta sc_undochar,x
	dex
	bpl mc0
	rts

; ----------------------------------------- Values

char	!by $00,$5c,$62,$72,$02,$02,$3c,$00

modes	!scr 223,"4Bit"
	!scr 223,"File"

mode	!by 0
palzl	!by 0
palsp	!by 0
ptemp	!by 0
vert	!by 0
plast	!by 0
cntx	!by 0
cnty	!by 0
cntmk	!wo 0
data0	!wo 0

c64pal	!by $00,$ff,$44,$cc,$55,$aa,$11,$dd
	!by $66,$22,$99,$33,$77,$ee,$88,$bb
;
; ----------------------------------------- Screenlist

setbglst	!by 0
	!by 7,12,16,11,$91,0,0
	!scr "SetBGColor@"
palgad	!by 9,13,6,6,$40
	!wo evgetcol
showgad	!by 9,20,7,3,$a0,0,0
	!by 160,160,160,160,160,0
modegad	!by 12,20,7,3,$c0
	!wo evselmode
modetx	!scr 223,"4Bit@"
	!by 15,13,7,3,$cc
	!wo evsetbg
	!scr " Set @"
	!by 15,20,7,3,$cc
	!wo evcan
	!scr "Leave@"
	!by $80

; -----------------------------------------

modend	!eof
