
	!src "godotlib.lib"
	*= $c000
	!ct pet
;	!sl "labels.txt"

; ------------------------------------------------     
;
; mod.SetColorbase
; Modul zum Ändern der RGB-Vorgabewerte für die Konversions-
;   routinen der Lader GIF, IFF, PCX-VGA320 und PCXprep4IFLI.
;   Die Daten liegen an $0380 (bis $03af) zu je 16 Bytes Rot,
;   Grün und Blau. Die RGB-Werte müssen durch 16 geteilt sein,
;   und dabei gerundet werden (das obere Nibble kommt ins untere,
;   wobei ab $08 im unteren Nibble das obere aufgerundet wird):
;   new_value=(old_value+8)/16.
;
;   1.00, 14.03.18, first release
;   1.01, 25.08.18, added palette of c64pixels.com previews
;   1.02, 24.11.19, propagating chosen palette to system
;   1.03, 31.07.21, replaced c64pixels by Ptoing palette
;                   added backward choosing
;   1.04, 31.03.22, replaced 
;
; ------------------------------------------------ 


; ------------------------------------------------ Equates

	src	= $30	; /$31
	spalte	= sc_merk+1	; get cursor position in gadget

	checkers	= $0b00

; ------------------------------------------------ Header

	jmp start
	!by $20
	!by $00
	!by $00
	!wo modend 
	!wo 0
	!pet "Set new RGB vals"
	!pet "1.04"
	!pet "31.03.22"
	!pet "A.Dettke        "

; ------------------------------------------------ Main

start	jsr makech	; Kringel
	jsr checkpals	; welche Palette ist gerade aktiv?

	ldx #<scblst	; Screenlist ausgeben
	ldy #>scblst
	jsr gd_screen

	jmp gd_eloop

; ------------------------------------------------ Event: Set New RGB Values

evsetpal	ldy palnr		; 6 is max, 0 is min (7 is undefined palette)
	cpy #7
	beq ++++		; in undefined set "GoDot" as default choice

	lda spalte	; clicked where in gadget?
	cmp #22		; 21 is center of gadget text
	bcc ++

	cpy #6		; right: count pattern number forewards
	bne +
++++	ldy #$ff
+	iny
	bpl +++		; uncond. branch

++	tya		; left: count pattern number backwards
	bne +
	ldy #7
+	dey

+++	sty palnr

	sty gr_colorbase	; Palette dem System bekannt machen
	jsr chngepal	; Namen eintragen und anzeigen
	jsr movepal	; neue RGB-Werte aktivieren
	ldy #0		; .y=0, Offset auf gettext-Vektor
	jmp settext

; ------------------------------------------------ 

checkpals	ldx #<gr_rgb	; check which palette is active
	lda #>gr_rgb
	jsr addrgb
	bcc chngepal

; ------------------------------------------------ 

	ldy #7		; 7 if none or unknown ("other")

chngepal	sty palnr
	ldx paloffs,y
	ldy #6
-	lda palette,x
	sta paltx,y
	dex
	dey
	bpl -
	rts

; ------------------------------------------------ 

movepal	lda palnr		; (#7 is "other": don't activate)
	cmp #7
	beq +
	asl
	tax
	lda paladds,x
	sta src
	inx
	lda paladds,x
	sta src+1
	ldy #47		; amount of values (+1)
-	lda (src),y
	sta gr_rgb,y
	dey
	bpl -
+	rts

; ------------------------------------------------ Build Checksum

addrgb	stx src		; build checksum to recognize palette
	sta src+1
	ldy #40
	lda (src),y
	clc
	ldy #37
-	adc (src),y	; summarize from offset 40 and 37 to 47
	iny
	cpy #48
	bcc -
	ldy #$ff
-	iny
	cpy #7
	bcs +
	cmp palrefs,y
	bne -

; ------------------------------------------------ 

stay	clc
+	rts
	
; ------------------------------------------------ Event: Cancel

evcancel	ldx #6
-	lda #$aa
	sta checkers,x
	eor #$ff
	sta checkers+1,x
	dex
	dex
	bpl -
	sec 
	rts 

; ------------------------------------------------ Text Handler

gettext	!wo palgad

; ------------------------------------------------ 

settext	lda #<gettext
	sta sc_texttab
	lda #>gettext
	sta sc_texttab+1
	lda (sc_texttab),y
	sta sc_screentab
	iny 
	lda (sc_texttab),y
	sta sc_screentab+1
	lda #0
	tax 
	tay 
-	jsr gd_setpos
	cpx #4
	bne -
	jsr gd_trim
	jsr gd_initmove
	ldy #6
	jmp gd_xtxout1

; ------------------------------------------------ Kringel

makech	ldx #7
-	lda char,x
	sta sc_undochar,x
	lda atsign,x
	sta checkers,x
	dex 
	bpl -
	rts 

; ------------------------------------------------ 

char	!by $00,$5c,$62,$72,$02,$02,$3c,$00
atsign	!by $3c,$66,$6e,$6e,$60,$62,$3c,$00

palnr	!by 0		; Vorgabepalette ist "GoDot"
palrefs	!by 93,78,79,88,91,92,89
paloffs	!by 6,13,20,27,34,41,48,55
paladds	!wo godot,pepto,deekay,vici,colodore,coco,ptoing
palette	!scr "GoDot  "	; 0
	!scr "Pepto  "	; 7
	!scr "Deekay "	; 14
	!scr "PAL",$60,"v1 "; 21
	!scr "Colodor"	; 28
	!scr "CoCo   "	; 35
	!scr "Ptoing "	; 42
	!scr "other  "	; 49

cprefs	!by  15, 12, 11,  0,  1,  9, 10,  8,  4,  3,  2, 14,  6,  5, 13,  7	; C64 color index

godot	!by $0b,$07,$03,$00,$0f,$06,$0f,$0d,$0c,$0a,$08,$00,$00,$00,$0a,$0e	; RGBs
	!by $0b,$07,$03,$00,$0f,$04,$07,$08,$04,$0f,$00,$08,$00,$0c,$0f,$0e
	!by $0b,$07,$03,$00,$0f,$00,$07,$05,$0c,$0e,$00,$0f,$0a,$05,$06,$07
pepto	!by $09,$07,$04,$00,$0f,$04,$0a,$07,$07,$07,$07,$07,$03,$06,$0a,$0c
	!by $09,$07,$04,$00,$0f,$04,$06,$05,$04,$0a,$03,$06,$03,$09,$0d,$0c
	!by $09,$07,$04,$00,$0f,$00,$06,$02,$08,$0b,$03,$0b,$08,$06,$08,$07
deekay	!by $0c,$09,$04,$00,$0f,$04,$0d,$0b,$0a,$06,$07,$05,$02,$05,$09,$0f	; changed!
	!by $0c,$09,$04,$00,$0f,$02,$08,$05,$04,$0d,$03,$09,$00,$0b,$0f,$0e
	!by $0c,$09,$04,$00,$0f,$00,$07,$00,$0a,$0b,$03,$0d,$09,$02,$09,$05
vici	!by $0a,$07,$05,$00,$0f,$05,$0c,$09,$09,$06,$09,$07,$03,$05,$09,$0d	; ### 1.04
	!by $0a,$07,$05,$00,$0f,$04,$06,$05,$03,$0c,$03,$06,$03,$0a,$0f,$0d
	!by $0a,$07,$05,$00,$0f,$00,$07,$02,$0a,$0b,$04,$0e,$0b,$05,$09,$05
;vice	!by $0a,$07,$04,$00,$0f,$07,$0f,$0c,$0b,$03,$0c,$06,$02,$02,$06,$0e
;	!by $0a,$07,$04,$00,$0f,$03,$05,$04,$02,$0e,$02,$05,$02,$0d,$0f,$0d
;	!by $0a,$07,$04,$00,$0f,$00,$05,$00,$0e,$0c,$02,$0f,$0b,$02,$06,$01
colodore	!by $0b,$08,$05,$00,$0f,$05,$0c,$09,$09,$07,$08,$07,$03,$05,$0b,$0f
	!by $0b,$08,$05,$00,$0f,$03,$07,$05,$04,$0d,$03,$07,$03,$0b,$0f,$0f
	!by $0b,$08,$05,$00,$0f,$00,$07,$03,$09,$0d,$03,$0f,$0a,$05,$0a,$07
coco	!by $0b,$08,$05,$00,$0f,$07,$0c,$09,$09,$07,$08,$07,$03,$05,$0b,$0f
	!by $0b,$08,$05,$00,$0f,$04,$07,$05,$04,$0d,$03,$07,$03,$0b,$0f,$0f
	!by $0b,$08,$05,$00,$0f,$01,$07,$03,$09,$0d,$03,$0f,$0a,$05,$0a,$07
ptoing	!by $0b,$08,$05,$00,$0f,$05,$0c,$09,$09,$08,$09,$08,$04,$07,$0b,$0d	; ### 1.03
	!by $0b,$08,$05,$00,$0f,$04,$07,$06,$04,$0c,$04,$07,$03,$0b,$0f,$0e
	!by $0b,$08,$05,$00,$0f,$00,$07,$02,$0b,$0c,$03,$0e,$0a,$04,$08,$07

; ------------------------------------------------ Screenlist

scblst	!by $00 
	!by 6,16,11,9,$91,0,0
	!scr "Colorbase@"
palgad	!by 9,16,11,3,$ca
	!wo evsetpal
	!by $df,32
paltx	!scr "GoDot  @"
	!by 12,16,11,3,$c9
	!wo evcancel
	!scr " Leave@"
	!by $80 

; ------------------------------------------------ 

modend	!eof
