
	!src "godotlib.lib"
	*= $c000
	!ct pet
;	!sl "labels.txt"

; ------------------------------------------------ 
;
; GoDot Modifier SysColors
; Modul zum Ändern von GoDots GUI-Farben
; von Martin Burggraf
;
; 1.04: 29.09.95, public release
; 1.05: 26.06.17, completely re-coded (by A.Dettke) for system conformity
; 1.06: 01.07.17, added cosmetics (unhilite)
; 1.07: 26.03.18, added mouse colors
;
; ------------------------------------------------     

; ------------------------------------------------ Equates

	nshadow	= $30	; new shadow
	nlite 	= $31	; new lite
	nnorm	= $32	; new normal
	nhilite	= $33	; new hilite
	nmain	= $34	; new main

	border	= $d020
	backgr	= $d021
	mausfront	= $d027
	mausback	= $d028


; ------------------------------------------------  Header   

	jmp start

	!by $20
	!by $00 
	!by 1 
	!wo modend
	!wo $00
	!pet "ChangeTextcolors"
	!pet "1.07"
	!pet "26.03.18"
	!pet "M.Burggraf/$AD  "

; ------------------------------------------------ Main    

start	ldx #<sclst	; Zeiger auf Screenlist setzen
	ldy #>sclst
	jsr gd_screen	; und anzeigen

	jsr svdefault	; Defaultfarben sichern
	jsr unhilite	; Teil des Requesters neu einfärben
	jsr showall	; Farben anzeigen
	jsr storedata	; Daten sichern
	jmp gd_eloop	; warten auf Klicks (Events)

; ------------------------------------------------ Store Data

svdefault	lda sc_light	; Aktuelle Farben holen und in Tabellen eintragen
	sta nlite
	sta defdata
	lda sc_shadow
	sta nshadow
	sta defdata+1
	lda sc_hilite
	sta nhilite
	sta defdata+2
	lda sc_normtext
	sta nnorm
	sta defdata+3
	lda sc_maincolor
	sta nmain
	sta defdata+4
	lda mausfront
	sta defdata+5
	lda mausback
	sta defdata+6
	rts

; ------------------------------------------------ Event: Default

evdefault	lda default+4	; GoDots Defaultfarben in die Farbtabelle eintragen
	sta sc_maincolor
	sta nmain
	lda default+5
	sta mausfront
	lda default+6
	sta mausback

	ldx #3
-	lda default,x
	sta sc_shadow,x
	sta nshadow,x
	dex
	bpl -
			; und anzeigen:

; ------------------------------------------------ Show Current

showall	lda nlite		; Farbe: lite
	ldx #5
	jsr so2		; anzeigen
	lda nshadow	; Farbe: shadow
	ldx #7
	jsr so2		; anzeigen
	lda nnorm		; Farbe: norm
	ldx #9
	jsr so2		; anzeigen
	lda nhilite	; Farbe: hilite
	ldx #11
	jsr so2		; anzeigen
	lda nmain		; Farbe: main
	ldx #13
	jsr so2		; anzeigen
	lda mausfront	; Farbe: Maus vorn
	ldx #15
	jsr so2		; anzeigen
	lda mausback	; Farbe: Maus hinten
	ldx #17
so2	ldy #25
	jmp showcol	; auch anzeigen

; ------------------------------------------------ Event: INC Colors

cmfront	ldx mausfront
	jsr incit
	sta mausfront
	ldx #15
	jmp cshow

cmback	ldx mausback
	jsr incit
	sta mausback
	ldx #17
	jmp cshow
	
clite	ldx nlite		; Farbwert für lite erhöhen
	jsr incit
	sta nlite
	ldx #5		; Zeile angeben
	jmp cshow		; und anzeigen

cshadow	ldx nshadow	; ebenso für shadow
	jsr incit
	sta nshadow
	ldx #7
	jmp cshow

cnorm	ldx nnorm		; und für norm
	jsr incit
	sta nnorm
	ldx #9
	jmp cshow

chilite	ldx nhilite	; für hilite
	jsr incit
	sta nhilite
	ldx #11
	jmp cshow

cmain	ldx nmain		; und main
	jsr incit
	sta nmain
	ldx #13		; Zeile

cshow	ldy #25		; Spalte
	jmp showcol	; anzeigen

incit	inx 		; erhöhen bis 15, dann auf 0
	cpx #16
	bne +
	ldx #0
+	txa 
	rts

; ------------------------------------------------ Get Data

storedata	ldx #3		; aktuelle Farben holen
-	lda sc_shadow,x
	sta nshadow,x	; und in Tabellen eintragen
	sta defdata,x
	dex 
	bpl -
	lda sc_maincolor
	sta nmain
	sta defdata+4
	lda mausfront
	sta defdata+5
	lda mausback
	sta defdata+6
	clc
	rts 

; ------------------------------------------------ UnHilite

unhilite	ldx #11		; Breite
	stx sc_br
	ldx #13		; Höhe
	stx sc_ho
	lda nnorm		; Farbe: Normal Text
	ldx #5		; Zeile
	ldy #13		; Spalte
	bne encolor	; neu einfärben

; ------------------------------------------------ Farbe anzeigen

showcol	pha		; Farbe retten
	txa		; Zeile retten
	pha
	ldx #2
	stx sc_br		; Breite: 2
	dex 
	stx sc_ho		; Höhe: 1
	pla
	tax
	pla

encolor	sta sc_chgcol	; Farbe in sc_chgcol schreiben
	stx sc_zl		; Zeile angeben
	sty sc_sp		; Spalte angeben
	ldx #4		; Offset auf sc_chgcol setzen
	jmp gd_fcol	; färben
			; Rückgabe: .c=0 (im Modul bleiben)

; ------------------------------------------------ Event: Accept

evaccept	lda nlite		; lite
	ldx nshadow	; shadow
	ldy nhilite	; hilite
	sta sc_light	; neu setzen
	stx sc_shadow
	sty sc_hilite
	lda nnorm	
	ldx nmain	
	sta sc_normtext
	stx sc_maincolor
	stx border	; screen (Maus ist bereits gesetzt)
	stx backgr
	sec 		; Modul verlassen (.c=1)
	rts 

; ------------------------------------------------ Event: Cancel

evcancel	lda defdata+4	; Farben vom Aufruf des Moduls rekonstruieren
	sta sc_maincolor
	sta nmain
	ldx #3
-	lda defdata,x
	sta sc_shadow,x
	sta nshadow,x
	dex
	bpl -
	sec		; Modul verlassen (.c=1)
	rts

; ------------------------------------------------ Screenlist

sclst

	!by $00 			; Screen nicht löschen

	!by 2,12,16,21,$91		; Requester, Type 1, mit gehighlightetem Text
	!wo $00 
	!scr "SysColors@"		; Titel: "SysColors"

	!by 4,24,4,3,$cf		; Gadget 3, Farbe für den beleuchteten Rand
	!wo clite 
	!by $a0,$a0,$00 		; 2 Shift Space (für die Farbe)

	!by 6,24,4,3,$ce		; Gadget 4, Farbe für den unbeleuchteten Rand
	!wo cshadow 
	!by $a0,$a0,$00  		; 2 Shift Space (für die Farbe)

	!by 8,24,4,3,$ce		; Gadget 5, normale Textfarbe
	!wo cnorm
	!by $a0,$a0,$00  		; 2 Shift Space (für die Farbe)

	!by 10,24,4,3,$ce		; Gadget 6, Farbe für gehighlighteten Text
	!wo chilite
	!by $a0,$a0,$00  		; 2 Shift Space (für die Farbe)

	!by 12,24,4,3,$ce		; Gadget 7, Hintergrundfarbe
	!wo cmain
	!by $a0,$a0,$00  		; 2 Shift Space (für die Farbe)

	!by 14,24,4,3,$ce		; Gadget 8, Maus Vordergrundfarbe
	!wo cmfront
	!by $a0,$a0,$00  		; 2 Shift Space (für die Farbe)

	!by 16,24,4,3,$ce		; Gadget 9, Maus Hintergrundfarbe
	!wo cmback
	!by $a0,$a0,$00  		; 2 Shift Space (für die Farbe)

	!by 18,12,16,3,$cb		; Gadget 10, Rücksteller
	!wo evdefault
	!scr "Default@"

	!by 20,12,8,3,$c7		; Gadget 1, Accept
	!wo evaccept
	!scr "Accept@"

	!by 20,20,8,3,$ce		; Gadget 2, Cancel
	!wo evcancel
	!scr "Cancel@"

	!by $c0			; Info
	!by 4,12,5 
	!scr "Light@"
	!by $c0			; Info
	!by 6,12,6 
	!scr "Shadow@"
	!by $c0			; Info
	!by 8,12,11 
	!scr "Normal Text@"
	!by $c0			; Info
	!by 10,12,6 
	!scr "HiLite@"
	!by $c0			; Info
	!by 12,12,10 
	!scr "Main Color@"
	!by $c0			; Info
	!by 14,18,5
	!scr "Front@"
	!by $c0			; Info
	!by 15,12,5
	!scr "Mouse@"
	!by $c0			; Info
	!by 16,18,4
	!scr "Back@"

	!by $80 			; End

; ------------------------------------------------ Tables

defdata 	!by $00,$00,$00,$00,$00,$00,$00
default	!by $00,$0f,$0b,$01,$0c,$01,$00	; Standard-GoDot-Farben

modend	!eof
