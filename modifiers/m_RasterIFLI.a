
	!src "godotlib.lib"
	*= $c000
	!ct pet
;	!sl "labels.txt"

; ----------------------------------------- 
;
;  mod.RasterIFLI
;   Modifier to reorder pixels for IFLI convenience,
;   it converts IFLIs to plain Multicolor images
;   (exchanges pixels in every other byte)
;   (this is a combination of mod.OddSwap and mod.MCScreenFix)
;
;  1.00, 05.12.95, first release
;
; ----------------------------------------- 

; ----------------------------------------- Equates

	adcnt		= $30
	data		= $35
	bbuf		= $39

	sprptr		= $07f8
	spr255		= $3fc0

	spr2x		= $d004
	spr2y		= $d005
	msbx		= $d010
	spren		= $d015
	sprxe		= $d01d
	sprcol2		= $d029

; ----------------------------------------- Header

	jmp start
	!by $20
	!wo 0
	!wo modend
	!wo 0
	!tx "Raster IFLI     "
	!tx "1.00"
	!tx "05.12.95"
	!tx "W.Kling/A.Dettke"

; ----------------------------------------- Main

start	jsr initad	; init activity
	lda #<sy_4bit
	ldy #>sy_4bit
	sta data
	sty data+1

	ldy #0
-	tya 
	and #4		; choose every other raster line..
	beq +

	lda (data),y	; ...and exchange nibbles there
	lsr
	lsr
	lsr
	lsr
	sta bbuf
	lda (data),y
	asl
	asl
	asl
	asl
	ora bbuf
	sta (data),y

+	iny 
	bne -

	jsr adinc		; activity
	inc data+1	; next page
	lda data+1
	cmp #>sy_bigbuffer	; until end of 4bit
	bne -

	jsr initad	; re-init activity
	lda #<sy_4bit
	ldy #>sy_4bit
	sta data
	sty data+1

	ldy #0
-	lda (data),y	; exchange nibbles in every other byte
	lsr
	lsr
	lsr
	lsr
	sta bbuf
	lda (data),y
	asl
	asl
	asl
	asl
	ora bbuf
	sta (data),y

	iny
	iny
	bne -

	jsr adinc		; activity
	inc data+1	; next page
	lda data+1
	cmp #>sy_bigbuffer	; until end of 4bit
	bne -

finish	jsr clearad	; end activity
	clc		; end modifier
	rts 

; ------------------------------------------------ Abschluss Activity

clearad	lda spren		; Wanderbalken aus
	and #$f3
	sta spren
	lda msbx
	and #$f3
	sta msbx
	lda #$ff		; Grafik verändert
	sta gr_redisp
	clc 		; fertig
	rts 

; ----------------------------------------- 

adinc	dec adcnt
	bne +
	inc spr2y
	inc spr2y+2
	lda #3
	sta adcnt
+	rts 

; ------------------------------------------------ Init Activity

initad	ldy #60
	lda #0
-	sta spr255+3,y	; Spriteblock 255 vorbereiten
	dey 
	bpl -
	sty spr255	; dünner Strich
	sty spr255+1
	sty spr255+2
	lda #$0f		; Farbe hellgrau
	sta sprcol2
	sta sprcol2+1
	lda sprxe		; doppelt breit
	ora #$0c
	sta sprxe

	lda msbx		; x über 255
	ora #$0c
	sta msbx
	lda #8		; x-Ordinate: 8 (plus 256)
	sta spr2x
	lda #$20
	sta spr2x+2
	lda #$92		; y-Ordinate: 146
	sta spr2y		; (im Image Information Fenster)
	sta spr2y+2

	sty sprptr+2	; Sprite 255
	sty sprptr+3
	lda spren
	ora #$0c		; Activity-Balken aktivieren
	sta spren
	lda #3		; Startzähler (5 mal 40 = 200)
	sta adcnt
	rts

; ----------------------------------------- 

modend	!eof

