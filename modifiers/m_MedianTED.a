	!src "godotlib.lib"
	*=$c000
	!ct pet
;	!sl "labels.txt"

; ------------------------------------------------ 
;
; mod.MedianTED
; -> find the color which has the median 7Bit value in 3x3
;    in GoDot: use mod.Quick4TED so view result
;
; 0.99, 13.08.94
;
; reworked on 15.10.2018
;
; ------------------------------------------------ 

; -------------------------------------------------------------- Declarations

	lcnt		= $30
	adcnt		= $31
	dst		= $35
	src		= $37
	tcnt		= $3a
	bbuf		= $3b
	median		= $3c
	mrkx		= $3e
	mrky		= $3f

	gr_redisp		= $0fbd

	bbase 		= $c601
	buf_o 		= bbase
	buf_m 		= buf_o+322
	buf_u 		= buf_m+322
	buf_r 		= buf_u+322
	result		= $cc00

	sprptr		= $07f8
	wbuf		= sy_global
	spr255		= $3fc0

	spr2x		= $d004
	spr2y		= $d005
	msbx		= $d010
	spren		= $d015
	sprxe		= $d01d
	border		= $d020
	sprcol2		= $d029

; -------------------------------------------------------------- Header

	jmp start

	!by $20		; Modifier
	!by 0
	!by 0
	!wo modend
	!wo 0		; reserved
	!pet "MedianFilter7Bit"
	!pet "0.99"
	!pet "13.08.94"
	!pet "W.Kling         "

addtxt	!pet "Mid Value of    "
	!pet "Sorted 3x3 Array"
	!pet "buffersize!160!!"
	!by 0

; ------------------------------------------------ Main

start	jsr init_sa
	lda #0		; ab Rasterzeile 0
	sta lcnt
	jsr puffern	; drei Zeilen puffern
	jsr umlagern

-	jsr umlagern
	inc lcnt
	jsr puffern
	jsr konvol	; und bearbeiten

	dec lcnt
	jsr w_ergeb	; abspeichern
	jsr adinc

	inc lcnt		; bis alle Zeilen durch (200)
	lda lcnt
	cmp #199
	bne -

	jsr puffern	; letzte bearbeiten (Rand!)
	jsr umlagern
	jsr konvol
	jsr w_ergeb

	jmp abschl	; fertig

; ------------------------------------------------ 
; ------------------------------------------------ Konvolution
; ------------------------------------------------ 


konvol	ldy #0

kon5	lda buf_o-1,y	; aktuelle Matrix puffern
	sta wbuf
	lda buf_o,y
	sta wbuf+1
	lda buf_o+1,y
	sta wbuf+2

	lda buf_m-1,y
	sta wbuf+3
	lda buf_m,y
	sta wbuf+4
	lda buf_m+1,y
	sta wbuf+5

	lda buf_u-1,y
	sta wbuf+6
	lda buf_u,y
	sta wbuf+7
	lda buf_u+1,y
	sta wbuf+8
	sty mrkx

	ldx #4
--	lda #0
	sta bbuf
	ldy #8
-	lda wbuf,y
	bmi +
	cmp bbuf
	bcc +
	sta bbuf
	sty median
+	dey
	bpl -

	lda bbuf
	beq +

	lda #$ff
	ldy median
	sta wbuf,y
	dex
	bpl --

	lda bbuf

+	ldy mrkx
	sta result,y

	iny
	cpy #160
	bne kon5
	rts

; ------------------------------------------------ 
; ------------------------------------------------ 
; ------------------------------------------------ 

; ------------------------------------------------ Puffer umlagern

umlagern	ldy #0		; von unten nach oben schieben
-	lda buf_m-2,y
	sta buf_o-2,y
	lda buf_u-2,y
	sta buf_m-2,y
	iny 
	cpy #163
	bne -
	rts 

; ------------------------------------------------ Puffer füllen

puffern	lda lcnt		; aktuelle Rasterzeile
	pha 
	lsr		; durch 8
	lsr
	lsr
	tax 
	pla 
	and #$07		; (rz and 7) * 4
	asl
	asl
	sta src		; nach Quelle
	lda hi_rows,x
	sta src+1

	ldx #0
--	ldy #0
-	lda (src),y
	sta buf_u,x
	inx
	iny
	cpy #4
	bne -
	lda src
	clc
	adc #32
	sta src
	bcc +
	inc src+1
+	cpx #160
	bne --

	lda buf_u		; für definierten Rand sorgen
	sta buf_u-1
	sta buf_u-2
	lda buf_u+159
	sta buf_u+160
	sta buf_u+161
	rts 

; ------------------------------------------------ Ergebnis nach 4Bit

w_ergeb	lda lcnt
	pha 
	lsr
	lsr
	lsr
	tax 
	pla 
	and #$07
	asl
	asl
	sta src		; Ziel: 4Bit
	lda hi_rows,x
	sta src+1

	ldx #0
--	ldy #0
-	lda result,x
	sta (src),y
	inx
	iny
	cpy #4
	bne -
	lda src
	clc
	adc #32
	sta src
	bcc +
	inc src+1
+	cpx #160
	bne --

	rts 

; -------------------------------------------------------------- 
; --- 4bit-high-adressen der kachelzeilen

hi_rows 
	!by $40, $45, $4a, $4f, $54, $59, $5e, $63
	!by $68, $6d, $72, $77, $7c, $81, $86, $8b
	!by $90, $95, $9a, $9f, $a4, $a9, $ae, $b3, $b8 

; ------------------------------------------------ Activity

adinc	dec adcnt
	bne +
	inc spr2y
	inc spr2y+2
	lda #5
	sta adcnt
+	rts 

; ------------------------------------------------ Abschluss Activity

abschl	lda spren		; Wanderbalken aus
	and #$f3
	sta spren
	lda msbx
	and #$f3
	sta msbx
	lda #$ff		; Grafik verändert
	sta gr_redisp
	clc 		; fertig
	rts 

; ------------------------------------------------ Init Activity

init_sa	ldy #60
	lda #0
-	sta spr255+3,y	; Spriteblock 255 vorbereiten
	dey 
	bpl -
	sty spr255	; dünner Strich
	sty spr255+1
	sty spr255+2
	lda #$0f		; Farbe hellgrau
	sta sprcol2
	sta sprcol2+1
	lda sprxe		; doppelt breit
	ora #$0c
	sta sprxe

	lda msbx		; x über 255
	ora #$0c
	sta msbx
	lda #8		; x-Ordinate: 8 (plus 256)
	sta spr2x
	lda #$20
	sta spr2x+2
	lda #$92		; y-Ordinate: 146
	sta spr2y		; (im Image Information Fenster)
	sta spr2y+2

	sty sprptr+2	; Sprite 255
	sty sprptr+3
	lda spren
	ora #$0c		; Activity-Balken aktivieren
	sta spren
	lda #5
	sta adcnt
	rts

; ------------------------------------------------ 

modend	!eof
