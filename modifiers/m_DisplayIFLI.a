
	!src "godotlib.lib"
	*= $c000
	!ct pet
;	!sl "labels.txt"

; ------------------------------------------------------------ 
;
; mod.DisplayIFLI
; Module to display the contents of 4Bit as an IFLI image (REU required)
;
;   1.00, 17.08.03, first release
;   1.01, 09.06.18, changed: convert 4bit to IFLI and then display
;   1.02, 10.06.18, added: visible reaction when no REU, set re-render flag
;   1.03, 12.06.18, bugfix: wrong unpatch in fallback routine
;   1.04, 03.12.18, changed: activity bar starts immediately
;   1.05, 06.04.22, bugfix, didn't reset reu record
;
; ------------------------------------------------------------ 

	raster		= 45	; constant: value for raster beam on IFLI
	patchlen		= endofpatch-patch2

	pport		= $01
	ld3		= $d3
	mpport		= $fc

; write image to REU

	gbyte		= $30
	adcnt		= $31
	src		= $32	; /$33
	src3		= $34	; /$35
	vekt02		= $36	; /$37
	dst		= src3
	merkx		= $36
	merky		= $37
	byte		= $39
	timer		= $40

; display IFLI:

	fallback		= sy_numbers+36
	backpack		= $01b0
	irqvector		= $0314
	sprptr		= $07f8
	into		= $1417
	swap4096		= $1714
	inswap4k		= $1740
	swap4k		= swap4096-2
	bank0		= sy_4bit
	bank1		= $c000

	hist		= sy_global
	hist0		= hist+16
	htab		= hist0+32
	spr255		= $3fc0
	c64cmb		= sy_bigbuffer
	hist3		= c64cmb+$100
	playerloc		= $2000
	icolor		= $0400
	ifli		= $8400
	cram		= $ca00

	vic		= $d000
	spr2x		= $d004
	spr2y		= $d005
	msbx		= $d010
	grafreg		= $d011
	rasterlo		= $d012
	spren		= $d015
	multreg		= $d016
	charreg		= $d018
	irqflags		= $d019
	irqmask		= $d01a
	sprxe		= $d01d
	border		= $d020
	backgr		= $d021
	sprcol2		= $d029

	colorram		= $d800
	keybrow		= $dc00
	keybcol		= $dc01
	irqcontr1		= $dc0d
	vicbank		= $dd00
	ramirq		= $fffe

; --------------------------------------- Header

header 	jmp start
	!by $20
	!by 0
	!by 1		; dirty
	!wo modend
	!wo 0
	!tx "IFLI Display    "
	!tx "1.05"
	!tx "06.04.22"
	!tx "AD/Manfred Trenz"

; --------------------------------------- Main

start	lda gd_modswitch
	sta storems
	jsr check4reu
	bne +
	jsr savec64
	jsr writeifli
	jsr initreu
	jsr clearad
	jsr getjpz	; display ifli, doesn't return if successful

+	lda storems
	sta gd_modswitch
	clc
	rts

; ----------------------------------------- 
; ----------------------------------------- 
;
;   write interlace fli
;
; ----------------------------------------- 
; ----------------------------------------- 

writeifli	ldy #0		; first create table of color combis
mc64	tya
	lsr
	lsr
	lsr
	lsr
	tax
	lda cols64,x
	and #$f0
	sta bbuf
	tya
	and #$0f
	tax
	lda cols64,x
	and #$0f
	ora bbuf
	sta c64cmb,y
	iny
	bne mc64

	ldy #3		; more preparations
prepl	lda mask1,y
	sta mask,y
	dey
	bpl prepl

	lda #32
	sta col0+4
	sta maxcol

	lda #<sy_4bit	; start render
	ldx #>sy_4bit	; 4Bit at $4000 (upto $bcff)
	sta src
	stx src+1
	lda #<cram	; buffer address
	ldx #>cram
	sta dst
	stx dst+1
loop3	ldy #15		; clear histogram (for one tile)
	lda #0
ch1	sta hist,y
	dey
	bpl ch1
	lda #8
	sta blcnt
blloop	ldx #0
	ldy #0
	lda (src),y	; get byte 0 (first 2 pixels)
	sta hmbuf
	beq hms1		; skip black
	inx
hms1	iny
	lda (src),y	; get byte 1
	sta hmbuf+1
	beq hms2
	cmp hmbuf		; compare colors
	beq hms2		; don't count if equal
	inx
hms2	iny
	lda (src),y	; byte 2
	sta hmbuf+2
	beq hms3
	cmp hmbuf
	beq hms3
	cmp hmbuf+1
	beq hms3
	inx
hms3	iny
	lda (src),y	; byte 3 (finishing one tile-scanline)
	beq hms4
	cmp hmbuf
	beq hms4
	cmp hmbuf+1
	beq hms4
	cmp hmbuf+2
	beq hms4
	inx

hms4	cpx #3		; more than 2 colors?
	bcc s8		; no
	ldy #3		; yes, are these colors plain C64 colors?
hl3	lda (src),y
	and #$0f
	tax
	lda (src),y
	cmp dnib,x
	bne s71
	inc hist0,x	; count if yes
s71	dey
	bpl hl3

s8	lda src		; next 4 bytes (next scanline in tile)
	clc
	adc #4
	sta src
	bcc s9
	inc src+1
s9	dec blcnt		; tile finished?
	bne blloop	; no

	lda #0		; find most frequent color per tile
	ldx #32
	sta max
	stx maxcol
	ldy #15
m3	lda hist0,y
	beq s10
	cmp max
	beq s10
	bcc s10
	sta max
	sty maxcol
s10	dey
	bpl m3

	jsr adinc		; activity (1000 passes)
	lda maxcol	; (may be $20: flag for no color problem)
	ldy #0		; and write to color ram buffer
	sta (dst),y
	inc dst
	bne s11
	inc dst+1

s11	lda src+1		; until end of 4Bit
	cmp #>sy_bigbuffer
	beq ok3
	jmp loop3

; ----------------------------------------- 

ok3	lda #0		; now write 1st image
	sta nibflg	; (flag: take *left* pixels for that)
	jsr writefli
	ldy #192		; write bg 1 (black)
	jsr write0

	jsr writecram	; write color ram

	lda #$ff		; write 2nd image
	sta nibflg	; (and take *right* pixels for this one)
	jsr writefli
	ldy #100		; write bg 2 (black)
	jsr write0

ende	sec		; leave saver
	rts

; ----------------------------------------- Save color ram

writecram	lda #25		; init activity bar
	sta adcnt
	jsr initad
	jsr setreu

	lda #<cram
	ldx #>cram
	sta src3
	stx src3+1
	lda #4
	sta rm_werte+3
	sta crcnt
wcrl0	ldy #0
wcrl	lda (src3),y
	tax
	lda c64cmb,x
	and #15
	jsr write
	iny
	cpy #250
	bne wcrl
	lda src3
	clc
	adc #250
	sta src3
	bcc wcrs
	inc src3+1
wcrs	dec crcnt
	bne wcrl0
	rts

; ----------------------------------------- Write .y zero bytes

write0	lda #0
	jsr write
	dey
	bne write0
	rts

; ----------------------------------------- Write 1 byte with activity display

write	sta gbyte
	sty merky
	stx merkx
	jsr adinc
	lda #$90		; stash
	jsr gd_reu
	inc rm_werte+2
	bne +
	inc rm_werte+3
+	ldy merky
	ldx merkx
	rts

; ----------------------------------------- Save 1 FLI image

writefli	lda #200		; init activity bar
	sta adcnt
	jsr initad
	jsr setreu
	lda #$c0		; fix on both
	sta reubase+10

	lda nibflg
	bmi +
	ldx #>sy_4bit
	!by $2c
+	ldx #$c0
	stx rm_werte+3

	lda #0
	sta vrcnt		; counter for 8 video rams
vrloop	lda vrcnt
	ldx #>sy_4bit
	sta src
	stx src+1
	lda #<cram
	ldx #>cram
	sta src3
	stx src3+1

loop12	ldy #15		; clear histogram	
	lda #0
ch3	sta hist,y
	dey
	bpl ch3
	ldy #3
hl4	lda (src),y
	bit nibflg	; determine which image
	bmi lonib1	; first or second?
	lsr
	lsr
	lsr
	lsr
lonib1	and #$0f		; isolate color and count (1 scanline)
	tax
	inc hist,x
	dey
	bpl hl4

	lda #0		; ignore black
	tax
	sta hist,x
	tay		; find first color of tile
	lda (src3),y	; problematic colors?
	tax
	cpx #32
	beq s12
	lda #0		; yes, ignore this color
	sta hist,x
s12	sty max
	stx col0+3
	lda #32
	sta col0+1
	ldy #15		; find second color of tile
m1	lda hist,y
	beq s13
	cmp max
	beq s13
	bcc s13
	sta max
	sty col0+1
s13	dey
	bpl m1
	lda col0+1	; find third color of tile
	cmp #32		; one single color?
	beq s14		; yes
	tax		; no, ignore this color
	lda #0
	sta hist,x
	sta max
	ldy #15
m2	lda hist,y
	beq s15
	cmp max
	beq s15
	bcc s15
	sta max
	sty col0+2
s15	dey
	bpl m2

s14	lda col0+1	; create color combi
	asl
	asl
	asl
	asl
	ora col0+2
	tax
	lda c64cmb,x	; from table

	jsr write		; and save

	inc src3		; proceed to next tile
	bne s16
	inc src3+1
s16	lda src
	clc
	adc #32
	sta src
	bcc s17
	inc src+1
s17	lda src+1
	cmp #>sy_bigbuffer	; until end of 4Bit
	beq s18
	jmp loop12

s18	ldy #24		; padding
	jsr write0

	lda vrcnt		; next scanline
	clc
	adc #4
	sta vrcnt
	cmp #32
	beq ok12
	jmp vrloop

; ----------------------------------------- 

ok12	jsr initad2	; re-init bar
	jsr setreu
	lda nibflg
	bmi +
	ldx #$60
	!by $2c
+	ldx #$e0
	stx rm_werte+3

	lda #<sy_4bit	; compute bitmap
	ldx #>sy_4bit
	sta src
	stx src+1
	lda #<cram
	ldx #>cram
	sta src3
	stx src3+1

bmloop	lda #8		; 8 scanlines per tile
	sta zlcnt
zlloop	ldy #15		; clear histogram
	lda #0
ch4	sta hist,y
	dey
	bpl ch4
	ldy #3
hl5	lda (src),y
	bit nibflg
	bmi lonib2
	lsr
	lsr
	lsr
	lsr
lonib2	and #$0f
	tax
	inc hist,x	; create bitpattern for multicolor
	lda bits,y
	sta nr0,y
	lda #32
	sta col0,y
	dey
	bpl hl5
	lda #0		; ignore black
	tax
	sta hist,x
	stx col0
	ldy #0
	lda (src3),y	; problematic color?
	cmp #32
	beq s19		; no
	tax		; third color
	tya
	sta hist,x
	stx col0+3
s19	sty max
	ldy #15		; first color
m11	lda hist,y
	beq s20
	cmp max
	beq s20
	bcc s20
	sta max
	sty col0+1
s20	dey
	bpl m11
	lda col0+1	; one single color?
	cmp #32
	beq s21
	tax		; no, ignore
	lda #0
	sta hist,x
	sta max
	ldy #15		; second color
m21	lda hist,y
	beq s22
	cmp max
	beq s22
	bcc s22
	sta max
	sty col0+2
s22	dey
	bpl m21

s21	ldx #0		; sort color frequencies
lx	stx xbuf
	ldy #3
ly	lda col0,x
	cmp col0,y
	bcc s23
	beq s23
	pha
	lda col0,y
	sta col0,x
	pla
	sta col0,y
	lda nr0,x
	pha
	lda nr0,y
	sta nr0,x
	pla
	sta nr0,y
s23	dey
	cpy xbuf
	bne ly
	inx
	cpx #3
	bne lx
	ldy #$ff
	sty hstart
htl	iny
	lda col0,y	; average: (a+b)/2
	clc
	adc col0+1,y
	lsr
	cmp #16		; limit to 15
	bcc s24
	lda #15
s24	sta hend
	lda nr0,y
	ldx hstart
htl1	inx
	sta htab,x
	cpx hend
	bcc htl1
	cpx #15
	bcs s25
	stx hstart
	cpy #3
	bne htl
s25	lda #0
	sta byte
	ldy #3
rl	lda (src),y
	bit nibflg
	bmi lonib3
	lsr
	lsr
	lsr
	lsr
lonib3	and #$0f
	tax
	lda htab,x
	and mask,y
	ora byte
	sta byte
	dey
	bpl rl

	jsr write		; save result byte (with activity)

	lda src		; next scanline
	clc
	adc #4
	sta src
	bne s26
	inc src+1
s26	dec zlcnt
	beq s27
	jmp zlloop
s27	inc src3
	bne s28
	inc src3+1
s28	lda src+1
	cmp #>sy_bigbuffer	; unto end of 4Bit
	beq ok121
	jmp bmloop

ok121	rts

; --------------------------------------- Swap REU /w C64

bpack	sta reucom	; $01b0
	nop
	jmp display

; ----------------------------------------- 

setbpack	ldx #6
jv1	lda bpack,x
	sta backpack,x
	dex
	bpl jv1
	rts

; ----------------------------------------- 

getjpz	lda #$93
	jsr bsout
	lda #0		; inc on both
	sta reubase+10
	jsr resetwerte

	ldx #2
	stx merkx
	sei
	lda #$ea		; NOP to SWAP4K (statt CLI)
	sta inswap4k	; in god.main
	lda #$10		; stash 4K (from $9000)
	sta allover+6

st9	lda #$30
	sta pport
	lda ramreu,x
	tax
	stx allover+3
	lda #$90		; swap slots 2,4,3 with $9000 (nicht slot 1)
	sta allover+1
	jsr swap4096
	lda #$36
	sta pport
	lda #$92
	jsr stashc64	; swap from REU
	ldx allover+3
	lda #$90		; swap back in C64
	jsr swap4096
	dec merkx
	ldx merkx
	bpl st9

	jsr st8		; restore godot
	jsr gd_sproff

	ldx #0
-	lda patch2,x
	sta sy_numbers,x
	inx
	cpx #patchlen
	bne -

	ldx #6		; now init: swap most of the machine
pt32	lda allover,x 
	sta reubase+2,x
	dex
	bpl pt32
	inx
	stx border
	stx backgr

view	sei
	lda #$92		; SWAP (and overwrite this module)
	jmp backpack	; and then display the image

; --------------------------------------- Write C64 to REU

savec64	lda #25		; init activity bar
	sta adcnt
	jsr initad

	jsr setbpack	; fallback routines to RAM
	ldx #0
pt30	lda patch2,x
	sta sy_numbers,x
	inx
	cpx #patchlen
	bne pt30

	lda #<player
	ldx #>player
	sta src
	stx src+1
	lda #<playerloc
	ldx #>playerloc
	sta dst
	stx dst+1
	ldy #0
	ldx #4
-	lda (src),y
	sta (dst),y
	iny
	bne -
	inc src+1
	inc dst+1
	dex
	bne -
	
	lda #0		; inc on both
	sta reubase+10
	jsr resetwerte
	lda #$90		; stash
	jsr stashc64	; half the Machine

	ldx #2
	stx merkx
	sei
	lda #$ea		; NOP to SWAP4K
	sta inswap4k
	lda #$10		; stash 4K (from $9000)
	sta allover+6

st6	lda #$30		; rest of the machine
	sta pport
	lda ramreu,x
	tax
	stx allover+3
	lda #$90		; swap slots 2,4,3 with $9000
	sta allover+1
	jsr swap4096
	lda #$36
	sta pport
	lda #$90
	jsr stashc64	; and stash them to REU
	lda #$90		; swap back in C64
	ldx allover+3
	jsr swap4096
	dec merkx
	ldx merkx		; 3 x 4096 Bytes
	bpl st6

st8	lda storems
	sta gd_modswitch
	inc merkx
	jsr resetwerte
	lda #$58		; write CLI ($58) back to SWAP4K
	sta inswap4k
	cli
	rts

; -------------------------------------- Check for REU

check4reu	lda ls_ramdrive	; welcher Ramdrive?
	and #15
	ldx #6
	cmp #8		; 1700
	beq graphon	; too small
	cmp #4		; 1764
	bne st2
	cpy #2		; MODs in bank 2?
	bcs graphon	; too small
	ldx #2
	bne st7
st2	cmp #5		; 1750
	bne graphon
	cpy #6		; MODs in bank 6?
	bcs graphon	; too small
st7	stx bank
	stx basic+2
	lda rm_ramvec	; Device eingebunden?
	cmp #$4c		; equal, wenn ja
	rts

; ----------------------------------------------------- Blink the Border

graphon	ldy #2		; red
	lda border	; save border color
	pha
	sty border	; set new color (in .y, e.g. "2" for red)
	ldx #$40
	jsr gd_dl2	; delay (.x=0)
	pla
	sta border	; reset old border color
	stx sy_tbuffer	; clear key strokes
stay	clc
	rts

; --------------------------------------- Init REU values

setreu	jsr initreu
	ldx bank
	stx rm_werte+4	; bank: 6 bei 1750
	ldx #1		; read/write 1 byte
	stx rm_werte+5
	dex
	lda #gbyte	; set C64 address: gbyte ($30)
	sta rm_werte
	stx rm_werte+1
	rts		; $30, $00, $00, xx, bk, 1, 0

; ---------------------------------------- Init rm_werte

initreu	ldx #6
	lda #0		; RM_WERTE (am Ende): 
sl0	sta rm_werte,x	; $00, $c0, $00, $00, 0, 0, 0
	dex		; - C64 - | -  REU   - | Anzahl 
	bpl sl0
	lda #$c0
	sta rm_werte+1
	rts

; --------------------------------------- Init REU access

resetwerte	
	ldx #0
	txa
st5	sta allover,x
	sta patch2,x
	sta rm_werte,x
	inx
	cpx #7
	bne st5
	lda basic+2	; actual bank
	sta allover+4
	sta patch2+4
	lda #$c0		; care for a dev.REU bug
	sta rm_werte+1
	lda #$d0
	sta allover+6
	sta patch2+6
	rts

; --------------------------------------- 

stashc64	sta command	; DMA command in .a:
	ldx #6		; $90 = stash
st4	lda allover,x	; $91 = fetch
	sta rm_werte,x	; $92 = swap
	dex
	bpl st4
	lda command
st90	jmp gd_reu

; ------------------------------------------------ Activity

adinc	dec adcnt
	bne +
	inc spr2y
	inc spr2y+2
	lda cntwert
	sta adcnt
+	rts 

; ------------------------------------------------ Abschluss Activity

clearad	lda spren		; Wanderbalken aus
	and #$f3
	sta spren
	lda msbx
	and #$f3
	sta msbx
	clc 		; fertig
	rts 

; ------------------------------------------------ Init Activity

initad	sta cntwert	; Zähler (z.B. 200: 8 mal 40 = 200)
initad2	ldy #60
	lda #0
-	sta spr255+3,y	; Spriteblock 255 vorbereiten
	dey 
	bpl -
	sty spr255	; dünner Strich
	sty spr255+1
	sty spr255+2
	lda #$0f		; Farbe hellgrau
	sta sprcol2
	sta sprcol2+1
	lda sprxe		; doppelt breit
	ora #$0c
	sta sprxe

	lda msbx		; x über 255
	ora #$0c
	sta msbx
	lda #8		; x-Ordinate: 8 (plus 256)
	sta spr2x
	lda #$20
	sta spr2x+2
	lda #$92		; y-Ordinate: 146
	sta spr2y		; (im Image Information Fenster)
	sta spr2y+2

	sty sprptr+2	; Sprite 255
	sty sprptr+3
	lda spren
	ora #$0c		; Activity-Balken aktivieren
	sta spren
	rts

; --------------------------------------- Data

bits	!by $00,$55,$aa,$ff	; bitmasks

mask1	!by $c0		; 11000000
	!by $30		; 00110000
	!by $0c		; 00001100
	!by $03		; 00000011

dnib	!by $00,$11,$22,$33,$44,$55,$66,$77
	!by $88,$99,$aa,$bb,$cc,$dd,$ee,$ff

cols64	!by $00,$66,$99,$bb		; conversion table GoDot<->C64
	!by $22,$44,$88,$cc
	!by $ee,$aa,$55,$ff
	!by $33,$77,$dd,$11

storems	!by 0
col0	!by 0,0,0,0,0
nr0	!by 0,0,0,0
mask	!by 0,0,0,0
max	!by 0,0
maxcol	!by 0
bbuf	!by 0
xbuf	!by 0
blcnt	!by 0
vrcnt	!by 0
zlcnt	!by 0
hstart	!by 0
hend	!by 0
crcnt	!by 0
nibflg	!by 0
hmbuf	!by 0,0,0
cntwert	!by 200
basic	!by 1,8,6			; basicstart in bank x (default = 6)
command	!by $90			; stash (by default)
ramreu	!by $e0,$f0,$d0		; order (last first)

allover	!by 0,0,0,0		; from 0 in C64 to 0 in REU
bank	!by 6			; in Bank x (default is 1750 = 6)
	!by 0,$d0			; 53 KB / 4 KB / 53 KB


; ----------------------------------------- 
; ----------------------------------------- 
;
;   display interlace fli
;
; ----------------------------------------- 
; ----------------------------------------- 

player

!pseudopc playerloc {

; ----------------------------------------- Event: Main Display

display	jsr prepifli
	jsr iflion
	jsr countdown

-	lda grafreg	; Raster Bit 9 aus?
	bpl -		; ja, loop

	sei
	lda #$6b
	sta grafreg
	lda #$00
	sta irqmask
	lda #$81
	sta irqcontr1
	lda #$37
	sta pport
-	lda grafreg
	bpl -
	lda #$03
	sta vicbank
	lda #$15
	sta charreg
	lda #$08
	sta multreg
	lda #$1b
	sta grafreg
	cli

reinit	lda #$36
	sta pport
	jmp fallback

; ------------------------------------------ IFLI: Prepare

prepifli	lda #$3b		; Bitmap $6000
	sta iflirout+1
	lda #$08		; Farben $4000
	sta smod2+1
	lda #$40		; Backgr $7f40
	sta smod3+1
	lda #$7f
	sta smod3+2
	lda #<ifli	; Speedcode $8400
	sta vekt02
	lda #>ifli
	sta vekt02+1
	ldx #$d0

--	ldy #$11
-	lda iflirout,y
	sta (vekt02),y
	dey
	bpl -
	clc
	lda vekt02
	adc #$12
	sta vekt02
	bcc +
	inc vekt02+1
+	clc
	lda iflirout+1
	adc #$01
	and #$07
	ora #$38
	sta iflirout+1
	lda smod2+1
	adc #$10
	and #$7f
	sta smod2+1
	inc smod3+1
	dex
	beq +
	cpx #100
	bne --

	lda #$40		; Backgr $ff40
	sta smod3+1
	lda #$ff
	sta smod3+2
	bne --

+	ldy #$00
	lda #$60		; RTS dahinter
	sta (vekt02),y
	rts

; ------------------------------------------ Speedcode

iflirout	lda #$ff
	sta grafreg
smod2	lda #$ff		; self modification
	sta charreg
smod3	lda $ffff		; self modification
	sta backgr
	bit ld3		; column of cursor

; ------------------------------------------ IFLI Darstellung

iflion	lda grafreg	; sync
	bpl iflion
	sei
	lda #$6b		; Bild aus
	sta grafreg
	lda #$35
	sta pport
	ldx #$00
-	lda icolor,x	; Colorram ($d800) besetzen
	sta colorram,x
	lda icolor+$0100,x
	sta colorram+$0100,x
	lda icolor+$0200,x
	sta colorram+$0200,x
	lda icolor+$0300,x
	sta colorram+$0300,x
	inx
	bne -
	lda #$00		; Videoram an $cc00
	sta vicbank
	lda #$38
	sta charreg
	lda #$19		; Multi
	sta multreg
	lda #$7f
	sta irqcontr1
	lda irqcontr1
	lda #$01
	sta irqmask
	sta irqflags
	lda #<(rirq)
	sta ramirq
	lda #>(rirq)
	sta ramirq+1
	lda #$2f
	sta rasterlo
-	lda grafreg	; sync
	bpl -
	lda #$3b		; Grafik an
	sta grafreg
	cli
	rts

; ------------------------------------------ 

eoirq	pla
	sta pport
	pla
	tay
	pla
	tax
	pla
	rti

rirq	pha
	lda pport
	sta mpport
	lda #$35
	sta pport
	lda #<(rirq2)
	sta ramirq
	lda #$30
	sta rasterlo
	dec irqflags
	cli
	nop
	nop
	nop
	nop
	nop
	nop
	nop
	nop
	nop
	nop
	nop
rirq2	nop
	nop
	nop
	nop
	nop
	pla
	pla
	pla
	txa
	pha
	tya
	pha
	lda mpport
	pha
	jsr ifli
	dec irqflags

	lda multreg	; interlace
	eor #$01
	sta multreg
	lda vicbank	; switch images
	eor #$02
	sta vicbank

	lda #$2f
	sta rasterlo
	lda #<(rirq)
	sta ramirq
	lda #>(rirq)
	sta ramirq+1
	jmp eoirq

; ------------------------------------------ Uhr

countdown	ldx #0
	stx timer
	stx timer+1
	stx timer+2
	ldx #201

-	lda time
	beq ++
	dex
	bne ++
	ldx #201
	inc timer
	bne +
	inc timer+1
	bne +
	inc timer+2
+	lda timer+1
	cmp time+2
	bne ++
	lda timer
	cmp time+1
	bcc +
++	lda keybrow	; Keyboard abfragen
	and keybcol
	and #$10
	bne -		; nichts gedrückt: Loop
+	rts

time	!by 0,0,0

}

; --------------------------------------- Fallback to GoDot (at $0100)

patch2

!pseudopc sy_numbers {

patch	!by 0,0,0,0	; to C64 $0000, from REU $0000
bank2	!by 6		; in bank x (default = bank 6)
	!by 0,$d0		; up to $d000 
			; [7]
swap64	ldx #$90		; to $9000 in C64
	stx patch+1
	jmp swap4096	; [8]

slots	!by $d0,$e0,$f0
	!by 0		; [4]

swapreu	ldx #6
fb0	lda sy_numbers,x
	sta reubase+2,x
	dex
	bpl fb0
fb2	lda #$92		; (fb2: self modifying)
	sta reucom
	rts		; [17]

fback	sei		; jump in here ($0124)
	ldx #$fe		; init stack
	txs
	lda #18		; activate character set
	sta charreg
	lda #$d0
	sta patch+6

	lda #$60		; RTS
	sta fb2
	jsr swapreu	; swap $0000 - $cfff
	lda #$a9		; LDA
	sta fb2
	lda #$92
	sta reucom

	lda #$10		; swap 4k-wise
	sta patch+6	; [18]

	lda #$ea		; patch gd_swap4k (NOP)
	sta inswap4k
	ldx #2
	stx slots+3
fb1	ldx slots+3	; swap slots 2 - 4
	lda slots,x	; slots
	sta patch+3
	pha
	jsr swap64
	jsr swapreu
	pla
	jsr swap64
	dec slots+3
	bpl fb1
	jsr resetwerte	; reset REU records

	jsr gd_spron	; mouse pointer on
	sta gr_redisp	; set re-render flag
	lda #$58		; unpatch gd_swap4k (CLI)
	sta ptch		; unpatch ptch
	sta inswap4k
ptch	cli
	ldx #$e0		; re-exchange ldr and mod
	jsr swap4k
	lda sc_maincolor	; set screen color
	sta border
	sta backgr
	lda #8		; fix lower case characters
	jsr bsout
	jmp into		; finally start GoDot
			; [34; tot 140]
}

endofpatch

; --------------------------------------- 

modend  !eof

