
	!src "godotlib.lib"
	*= $c000
	!ct pet
;	!sl "labels.txt"

; ------------------------------------------------     
;
; mod..CMD-Clock
;  System Module to retrieve and show the time from CMD RTCs
;
;   1.00, 09.12.95, first release
;   1.01, 27.05.96, added RTC in CMD Smartmouse (different h/w type)
;   1.02, 19.10.20, bugfix: didn't recognize Smartmouse at all
;   1.03, 21.10.20, bugfix: correct pm/am recognition
;
; ------------------------------------------------ 

; ------------------------------------------------ Equates

	temp		= $30
	distrib		= $31
	ymrk		= $35

	name		= $bb
	namelen		= $b7

	filetype		= $1d29

	shift		= $028d
	joypt1		= $dc01
	ddr1		= joypt1+2

	rahmen		= $d020
	iecin		= $ffa5
	untalk		= $ffab

; ------------------------------------------------ 

header	jmp start
	!by $20
	!by 0
	!by 0
	!wo modend
	!wo 0
	!tx "CMD RT Clock    "
	!tx "1.03"
	!tx "21.10.20"
	!tx "A.Dettke        "
;
; ------------------------------------------------ 

clear	ldx #4		; Drivenummern löschen
	lda #0
chk2	sta cmd,x
	dex 
	bne chk2		; ### 1.02
	rts

; ------------------------------------------------ 

gettime	jmp (distrib)

; ------------------------------------------------ 

start	ldy #0		; init Mausflag
	sty cmd
	lda shift		; Shift, CTRL oder C= gedrückt?
	bne +		; ja, Maustest übergehen
	jsr chkmouse	; nein, ist RTC in Smartmouse?
+	jsr clear
	lda cmd		; (cmd=0 if no Smartmouse found)
	bne mousereq	; cmd=6, Smartmouse found, branch

	ldy #11		; sonst: CMD-Geräte erkennen
	ldx #4
chk1	lda doffs-8,y
	sty ymrk
	tay
	lda filetype,y	; Flag (">") im Filerequester
	ldy ymrk
	cmp #$3e		; ">"?
	bne chk0
	lda ls_units-8,y
	and #$0f
	sta cmd,x		; Drivenummer merken
	sty cmd		; Offset als Flag für found
chk0	dey 
	dex 
	bne chk1

	lda #<getcmd	; Vektor auf gettime setzen
	sta distrib
	lda #>getcmd
	sta distrib+1
	lda cmd		; ein CMD-Gerät dabei?
	bne chk3		; ja

; ------------------------------------------------ Blink the Border

graphon	ldy #2		; nein, Rahmenblinken (2: rot)
	lda rahmen	; save border color
	pha
	sty rahmen	; set new color
	ldx #$40
	jsr gd_dl2	; delay
	pla
	sta rahmen	; reset old border color
	stx sy_tbuffer	; clear key strokes
	clc		; stay (in main screen)
	rts

; ------------------------------------------------ 

mousereq	lda #<chkmouse	; Einlesevektor für RTC Smartmouse setzen
	sta distrib
	lda #>chkmouse
	sta distrib+1
chk3	jsr evtime	; Zeit einlesen und anzeigen
	jmp gd_eloop	; warten

; ------------------------------------------------ 

getcmd	lda ls_drive	; aktuellen Drive retten
	pha 

	lda cmd		; Drive CMD-RTC setzen
	sta ls_drive
	jsr gd_sproff
	sta status
	ldx #<command	; Time-Read senden
	ldy #>command
	lda comlen
	jsr gd_sendcom
	lda #0
	sta status
	lda ls_drive
	ldy #$6f
	jsr gd_talk

	ldx #0		; Antwort einlesen
gtt0	jsr iecin
	sta time,x
	inx 
	cpx #9
	bne gtt0

	jsr untalk
	jsr gd_spron
	pla 
	sta ls_drive
	rts 

; ------------------------------------------------ Uhr

showtime2	jsr maketag	; deutsch

	lda time+1	; Jahr
	jsr gd_xcnvdez
	stx year3
	sta year3+1

	lda time+2	; Monat
	jsr gd_xcnvdez
	stx year2
	sta year2+1
	lda #46		; "-"
	sta year1+2
	sta year2+2

	lda time+3	; Tag
	jsr gd_xcnvdez
	stx year1
	sta year1+1

	jmp zeit		; weiter wie oben

; ------------------------------------------------ Clock 

showtime1	jsr makeday	; englisch:

	lda time+1	; Jahr
	jsr gd_xcnvdez
	stx year3
	sta year3+1

	lda time+2	; Monat
	jsr gd_xcnvdez
	stx year1
	sta year1+1
	lda #47		; "/"
	sta year1+2
	sta year2+2

	lda time+3	; Tag
	jsr gd_xcnvdez
	stx year2
	sta year2+1

zeit	lda time+4	; Stunde
	jsr gd_xcnvdez
	stx time1
	sta time1+1

	lda time+5	; Minute
	jsr gd_xcnvdez
	stx time2
	sta time2+1

	lda time+6	; Sekunde
	jsr gd_xcnvdez
	stx time3
	sta time3+1

	ldx #<clocklst	; Requester neu ausgeben
	ldy #>clocklst
	jsr gd_screen

	lda sc_normtext	; und einfärben
	sta sc_chgcol
	lda #<daygad
	ldx #>daygad
	sta sc_screentab
	stx sc_screentab+1
	ldx #0
	ldy #0
gt0	jsr gd_setpos
	cpx #4
	bne gt0
	ldx #4
	jsr gd_fcol
	clc 
	rts 

; ------------------------------------------------ 

makeday	lda time		; Wert mal 3
	sta temp
	asl 
	clc 
	adc temp
	tax 
	ldy #0
md1	lda adays,x	; Kürzel holen
	sta daytxt,y	; und in Requester schreiben
	inx 
	iny 
	cpy #3
	bne md1
	lda time+4
	cmp #12
	beq md2
	lda time+7	; vor-/nachmittags?
	bne md2
	lda #1		; "a"
	!by $2c
md2	lda #16		; "p"
	sta time0
	lda #13		; "m"
	sta time0+1
	rts 

; ------------------------------------------------ 

maketag	lda time		; Wert mal 3
	sta temp
	asl 
	clc 
	adc temp
	tax 
	ldy #0
md3	lda gdays,x	; Kürzel holen
	sta daytxt,y	; in Requester
	inx 
	iny 
	cpy #3
	bne md3

	lda cmd		; Smartmouse?
	cmp #6
	bne md4		; nein, skip

	lda time+7	; nur Smartmouse: 12/24 Std?
	beq md4
	lda time+4	; nachm.: 12 addieren
	clc 
	adc #12
	sta time+4

md4	lda #32		; am/pm löschen
	sta time0
	sta time0+1
	rts 

; ------------------------------------------------ Switch Mode

evmode	jsr gettime	; deutsch/englisch umschalten
	ldx mode
	inx 
	beq em0
	ldx #$ff
em0	stx mode
	bne em1
	jmp showtime1	; 0: englisch
em1	jmp showtime2	; 1: deutsch

; ------------------------------------------------ Get Time

evtime	jsr gettime	; Zeit einlesen
	ldx mode		; und ausgeben
	jmp em0

; ------------------------------------------------ Leave

evcanc	sec 
	rts 

; ------------------------------------------------ 

clocklst	!by 0,10,15,18,9,$01
	!wo 0
	!by 11,16,16,5,$92
	!wo 0
	!scr " "
daytxt	!scr "Thu "
year1	!scr "12/"
year2	!scr "07/"
year3	!scr "95 @"
	!by 16,16,6,3,$cc
	!wo evmode
	!scr "Mode@"
	!by 16,22,6,3,$cc
	!wo evtime
	!scr "Time@"
	!by 16,28,4,3,$cc
	!wo evcanc
	!scr "Ok@"
	!by $c0,13,16,14
	!scr "  "
time0	!scr "am "
time1	!scr "06:"
time2	!scr "24."
time3	!scr "55 @"
	!by $80

; ------------------------------------------------ Data

daygad	!by 12,17,5,3	; das Innere des Day-Gadgets

mode	!by 0		; default: englisch (0)

doffs	!by 34,41,49,57	; Offsets auf Drives im Filerequester
cmd	!by 0,0,0,0,0	; CMD-Drivenummern mit RTC

command	!tx "t-rd"	; Kommando für Drive-RTCs
comlen	!by 4
time	!by 0,0,0,0,0,0,0,0,0

adays	!scr "SunMonTueWedThuFriSat"
gdays	!scr "So Mo Di Mi Do Fr Sa "

msflg	!by $ff		; Puffer beim Dezimal-Wandeln
mult	!by  0,10,20,30,40	; Zehner beim Wandeln
	!by 50,60,70,80,90

iobyt	!by 0
portsv	!by 0
ddrsv	!by 0
flgsv	!by 0

bnum	!by 0
clkdata	!by 0,0,0,0,0,0,0,0 

; ------------------------------------------------ CMD Smartmouse

rdclk	jsr setup
	jsr brclk
	jmp exit

; ------------------------------------------------ 

brclk	lda #$bf
	jsr sndcmd
	lda #8
	jmp gbyts

; ------------------------------------------------ 

sndcmd	pha 
	jsr rdycmd1
	pla 
	jsr sbmb
	jmp ioin

; ------------------------------------------------ 

gbyts	sta bnum
	ldy #0
gb0	jsr gbmb
	sta (ls_vekta8),y
	iny 
	cpy bnum
	bcc gb0
	jmp rsthi

; ------------------------------------------------ 

gbmb	ldx #8
gb1	jsr rdycmd2
	lda joypt1
	lsr
	lsr
	lsr
	ror iobyt
	jsr clkhi
	dex 
	bne gb1
	lda iobyt
	rts 

; ------------------------------------------------ 

sbmb	sta iobyt
	ldx #8
sb0	jsr rdycmd2
	lda #0
	ror iobyt
	rol
	asl
	asl
	ora #$f1
	sta joypt1
	jsr clkhi
	dex 
	bne sb0
	rts 

; ------------------------------------------------ 

rdycmd1	jsr ioout
	jsr rdycmd2
	lda #$f7
	!by $2c
rdycmd2	lda #$fd
	and joypt1
	sta joypt1
	rts 

; ------------------------------------------------ 

rsthi	lda #8
	!by $2c
clkhi	lda #2
	ora joypt1
	sta joypt1
	rts 

; ------------------------------------------------ 

ioout	lda #14
	!by $2c
ioin	lda #10
	sta ddr1
	rts 

; ------------------------------------------------ 

setup	php 
	sei 
	sta iobyt
	pla 
	sta flgsv
	lda joypt1
	sta portsv
	lda ddr1
	sta ddrsv
	lda #$ff
	sta joypt1
	lda #10
	sta ddr1
	lda iobyt
	rts 

; ------------------------------------------------ 

exit	sta iobyt
	lda portsv
	sta joypt1
	lda ddrsv
	sta ddr1
	lda flgsv
	pha 
	lda iobyt
	plp 
	cli 
	rts 

; ------------------------------------------------ 

chkmouse	lda #<clkdata
	ldx #>clkdata
	sta ls_vekta8
	stx ls_vekta8+1
	jsr rdclk

	lda clkdata+5	; weekday (1..7)
	beq nom0
	cmp #8
	bcs nom1
	lda clkdata+4	; month ($01..$12, SED!)
nom0	beq nomouse
	cmp #$13
nom1	bcs nomouse

; ------------------------------------------------ 

getmouse	ldy #6		; set flag for Smartmouse RTC
	sty cmd

	lda clkdata	; Sekunden
	and #$7f
	jsr wandeln

	lda clkdata+1	; Minuten
	jsr wandeln

	lda clkdata+2	; Stunde
	tax
	and #$e0		; 12h/24h?
	bpl gm24		; branch if 24h

	and #$20		; am/pm?
	beq gam
	txa		; pm
	and #$1f
	ldx #$80
	bmi +
gam	txa		; am
	and #$3f
	ldx #0
	beq +

gm24	txa		; am/pm according to time
	and #$3f
	sed
	cmp #$13
	bcc gmam
	sbc #$12
	ldx #$80
	!by $2c
gmam	ldx #$0
	cld
+	stx time+7
	jsr wandeln

	lda clkdata+3	; Datum
	jsr wandeln

	lda clkdata+4	; Monat
	jsr wandeln

	dey 
	ldx clkdata+5	; Tag der Woche
	dex
	txa
	jsr wandeln

	ldy #1
	lda clkdata+6	; Jahr

wandeln	pha 
	and #$0f
	sta msflg
	pla 
	lsr
	lsr
	lsr
	lsr
	sty ymrk
	tay 
	lda mult,y
	clc 
	adc msflg
	ldy ymrk
	sta time,y

nomouse	dey
	rts

; ------------------------------------------------ 

modend	!eof
