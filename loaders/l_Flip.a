
	!src "godotlib.lib"
	*= $c000
	!ct pet
;	!sl "labels.txt"

; -------------------------------------- 
;
;  ldr.Flip
;
;  Loader for images of FLIP (FLI-Painter)
;
;  0.99, 03.05.93, pre-release (only unpacked images)
;  1.00, 31.05.18, now reading packed FLIPs as well
;  1.01, 01.06.18, minor changes and some bugfixes
;  1.02, 13.09.21, added: break loading process with STOP key
;
; --------------------------------------

	adcnt		= $30
	read		= $31	; /$32
	src		= $33	; /$34
	dst0		= $35	; /$36
	dst		= $37	; /$38
	byte		= $39
	cntwert		= $3a
	vrcnt		= $3b
	pack		= $3c
	pcnt		= $3d
	data		= $3e
	fstart		= $40	; /$41
	merkx		= $42
	merky		= $43
	merka		= $44	
	undotype		= $45

	offx		= ls_adrmisc
	offy		= offx+1

	cbuf		= $ca00
	memstart		= $2c00	; ($5c00 - $4000 + $1000)
	vramstart		= $3000 
	bmapstart		= $5000 
	sprite		= $d015
	basin		= $ffcf

; ------------------------------------ Header

	jmp start
	!by $80
	!wo 0
	!wo modend
	!wo 0
	!tx "FLIPainter FLI  "
	!tx "1.02"
	!tx "13.09.21"
	!tx "W.Kling/A.Dettke"

; ------------------------------------ 

jerror	jmp error
jnoflip	ldx #63
	jmp noflip

; ------------------------------------ Main

start	ldx #<fromiec	; set default read vector
	ldy #>fromiec
	stx read
	sty read+1
	ldx sc_vekt20	; get start of graphics data ($4000)
	ldy sc_vekt20+1	; (set by system)
	stx dst0		; to work vector
	sty dst0+1
	jsr getname	; save filename for later info output

; ------------------------------------ get picture data

	jsr check4reu	; REU angeschlossen?
	and #$40		; isolate Overflow bit (%01000000)
	sta hasreu	; Flag setzen
	jsr gd_xopen	; open file
	jsr gd_clrms	; initialize gauge output
	jsr onebyte	; read start address:
	bne jerror
	stx sc_stop	; init break by STOP	### 1.05
	sta fstart
	tax		; if $00: unpacked ($3c00) ?
	beq +
	cpx #$f0		; if $f0: packed   ($38f0) ?
	bne jnoflip
+	jsr onebyte
	bne jerror
	sta fstart+1
	and #4
	sta ftype
	sta passes	; init pass flag
	bne +		; if bit=1: unpacked!
	jsr readpack	; if 0: init packed file reading
	ldx sc_stop	; STOP pressed?	### 1.02
	bne jerror

; ------------------------------------ Retrieve Image File

+	lda #7
	sta cntwert
	ldx #18		; set screen output for text 1 ("CRams")
	jsr tcopy
	iny
	sty adcnt

	jsr getcram	; get *color ram* ($d800 area) to buffer CBUF
je6	bne jerror

; ------------------------------------ 

	lda #50
	sta cntwert
	ldx #0		; set screen output for text 2 ("VRams")
	jsr tcopy
	iny
	sty adcnt

	lda passes
	beq +
	jsr setreu
	lda #>vramstart
	sta rm_werte+3
+	jsr setdst0	; get *8 video rams* to 4Bit area
	lda #8		; each videoram is one rasterline
	sta vrcnt
vloop	lda dst0
	ldy dst0+1
	sta dst
	sty dst+1
vloop0	jsr action	; activity display (increment gauge)
	jsr readbyte	; get 1 byte
	ldx sc_stop	; STOP pressed?	### 1.02
je3	bne je6
	ldy #3		; spread to 4 bytes (4Bit!)
vloop1	sta (dst),y
	dey
	bpl vloop1
	lda dst		; next tile
	clc
	adc #32
	sta dst
	bcc vskip0
	inc dst+1
vskip0	lda dst+1
	cmp #>sy_bigbuffer
	bne vloop0	; up to end of 4Bit area

	ldy #24		; skip dummy bytes
	jsr dumy
	lda dst0		; target next rasterline of tile
	clc
	adc #4
	sta dst0
	dec vrcnt
	bne vloop

; ------------------------------------ 

	ldx #9		; set screen output for text 3 ("Bitmap")
	jsr tcopy
	iny
	sty adcnt

	lda passes
	beq +
	jsr setreu
	lda #>bmapstart
	sta rm_werte+3
+	jsr setdst0	; get *bitmap* to 4Bit area
bloop0	jsr readbyte	; get pixel byte
	sta byte
	jsr action	; (gauge again)
	ldx sc_stop	; STOP pressed?	### 1.02
je4	bne je3
	ldy #0
bloop1	lda #0		; convert pixels to 4Bit
	asl byte		; (isolate 2 pixels)
	rol
	asl byte
	rol
	beq null		; are they: NULL (%00)?
	cmp #3		; THREE (%11)?
	beq drei
	cmp #1		; ONE (%01)?
	beq eins

	lda (dst0),y	; or TWO (%10), assign
	and #$0f
	ora #$20		; $20: color two
	sta (dst0),y
	bne next
eins	lda (dst0),y	; ONE: assign
	lsr
	lsr
	lsr
	lsr
	ora #$10		; $10: color one
	sta (dst0),y
	bne next
drei	lda #$30		; $30: color THREE: assign
	sta (dst0),y
	bne next
null	sta (dst0),y	; $00: color NULL: assign

next	iny		; through all pixels of a byte
	cpy #4
	bne bloop1

	lda dst0		; advance to next rasterline of 4Bit tile
	clc
	adc #4
	sta dst0
	bcc bskip0
	inc dst0+1

bskip0	lda dst0+1	; end of 4Bit area?
	cmp #>sy_bigbuffer
	bne bloop0
	ldy #192		; yes, skip dummy bytes behind bitmap
	jsr dumy

; ------------------------------------ process color ram

	lda #7		; initialize new text and counters
	sta cntwert
	ldx #18		; set screen output for text 4 ("CRams")
	jsr tcopy
	iny
	sty adcnt

	jsr setdst0	; assign color from color ram:
	lda #<cbuf	; source address is CBUF
	ldx #>cbuf
	sta src
	stx src+1
cloop0	ldy #0
	lda (src),y	; get color ram byte
	inc src
	bne cskip00
	inc src+1
cskip00	and #$0f		; be sure to have correct color value (lower nibble)
	ora #$30
	sta byte
	jsr action	; (gauge again)
	ldx sc_stop	; STOP pressed?	### 1.02
je5	bne je4

	ldy #31		; process *one* tile
cloop1	lda (dst0),y	; get 4Bit value
	cmp #$30		; was THREE?
	bne cskip0	; no, skip
	lda byte		; yes, store color ram byte
	sta (dst0),y
cskip0	dey
	bpl cloop1

	lda dst0		; next tile
	clc
	adc #32
	sta dst0
	bcc cskip1
	inc dst0+1

cskip1	lda dst0+1	; until end of 4Bit area
	cmp #>sy_bigbuffer
	bne cloop0

; ------------------------------------ make colors

	lda #200		; initialize text and counters
	sta cntwert
	ldx #27		; set screen output for text 5 ("Backgr")
	jsr tcopy
	iny
	sty adcnt

	jsr setdst0

	ldx #0		; convert processed bytes to colors:
	stx byte		; first: background color (black)
nloop0	jsr action
	ldx sc_stop	; STOP pressed?	### 1.02
	bne je5
	ldy #0
	lda (dst0),y
	beq nskip0	; ($00 is background color)
	and #$0f		; convert the other
	tax
	lda dnib,x	; assign GoDot colors
	!by $2c
nskip0	lda byte
	sta (dst0),y	; write back
	inc dst0
	bne nskip1
	inc dst0+1
nskip1	lda dst0+1
	cmp #>sy_bigbuffer	; until end of 4Bit area
	bne nloop0

; ------------------------------------ 

	jsr setinfo	; set file information data

sk10	jsr gd_xmess	; output floppy message
sk11	jsr gd_xclose	; close file
	jsr initreu	; reset reu record
	jsr gd_spron	; switch on mouse pointer
	sec		; set Leave flag
	rts		; leave loader

; ------------------------------------ 
; ------------------------------------ Service Routines
; ------------------------------------ 

; ------------------------------------ store color ram to buffer

getcram	lda passes
	beq +
	jsr setreu
	lda #>memstart
	sta rm_werte+3
+	lda #<cbuf
	ldx #>cbuf
	sta dst
	stx dst+1
	ldy #0
	ldx #4
	stx merka
gcl	jsr readbyte
	sta (dst),y
	sty merky
	jsr action
	ldx sc_stop	; STOP pressed?	### 1.02
	bne je2
	ldy merky
	iny
	bne gcl
	inc dst+1
	dec merka
	bne gcl
je2	rts

; ------------------------------------ read packed file

jsuberr	jmp suberror

readpack	jsr onebyte
	bne jsuberr
	sta pack		; get pack indicator

	jsr onebyte	; get end of data address (in src)
	bne jsuberr
	sta src
	jsr onebyte
	bne jsuberr
	clc
	adc #$20		; to GoDot
	sta src+1

	jsr onebyte	; get end of target address (in dst)
	bne jsuberr
	sta dst
	cmp #$3f		; is $7f3f? (mandatory!)
	bne notflip
	jsr onebyte
	bne jsuberr
	cmp #$7f
	bne notflip
	clc
	adc #$20		; to GoDot
	sta dst+1

	bit hasreu
	bvc noreu	

	lda #200		; initialize adcounter
	sta cntwert
	stx fstart	; (.x=0) target address: $5900
	lda #$59
	sta fstart+1

	ldy #11		; skip padding bytes ("align" to $5900)
	jsr dumy
	ldx #36		; set screen output for text 6 ("Read")
	jsr tcopy		; (.y=0)
	iny
	sty adcnt

-	ldy #0		; reset .y
	jsr onebyte	; retrieve packed file
	sta (fstart),y
	ldy sc_stop	; STOP pressed?	### 1.02
	bne je0
	cpx #64
	beq +
	cpx #0
	bne jsuberr
	jsr action	; (.y gets garbled here)
	inc fstart
	bne -
	inc fstart+1
	bne -

+	lda #110		; initialize adcounter
	sta cntwert
	ldx #54		; set screen output for text 7 ("Move")
	jsr tcopy
	iny
	sty adcnt
	jsr deflate	; decompress file to $5900 (End: $9f3f)
	bne je0

	lda #9		; GetUndo (save to Undo)
	sta undotype
	jsr undo4bit

	ldx #<frommem	; set proper read vector
	ldy #>frommem
	stx read
	sty read+1
	inc passes	; set passes to 1
je0	rts

noreu	ldx #93		; text 8 ("Packed images...")
	!by $2c
notflip	ldx #63		; text 9 ("Most likely...")
	pla
	pla
	jmp noflip

; ------------------------------------------------ Decompress Image

deflate	ldy #0
	lda (src),y	; get byte
	cmp pack		; is pack indicator?
	beq unpack	; yes, treat
	sta (dst),y	; no, write to target
	jsr zminus1	; proceed
--	jsr qminus1
	jsr action
	ldy sc_stop	; STOP pressed?	### 1.02
	bne je1
	lda dst+1		; at end?
	cmp #$5a
	bne deflate	; no, next byte
je1	rts

unpack	jsr qminus1	; get counter to merkx
	lda (src),y
	sta merkx
	jsr qminus1	; get byte
	lda (src),y
	sta byte
-	jsr action
	lda byte
	ldy #0
	sta (dst),y	; write merkx times
	jsr zminus1
	dec merkx
	bne -
	beq --

; ------------------------------------------------ Counters

qminus1	lda src
	bne +
	dec src+1
+	dec src
	rts

zminus1	lda dst
	bne +
	dec dst+1
+	dec dst
	rts

; -------------------------------------- Check for REU

check4reu	lda ls_sysdrive+6	; Sysdrive ist RAM?
	and #15
	tax
	lda banks,x
	sta bank		; dann hier positiver Wert
	lda rm_ramvec	; Device eingebunden?
	cmp #$4c		; equal, wenn ja
	rts

; ------------------------------------ skip bytes

dumy	jsr readbyte
	dey
	bne dumy
	rts

; ------------------------------------ set vector to start of 4Bit area

setdst0	lda #<sy_4bit
	ldy #>sy_4bit
	sta dst0
	sty dst0+1
	rts

; ------------------------------------ read one byte

readbyte	jmp (read)

fromiec	jmp basin		; read from disk (unpacked)

frommem	jmp readreu	; read from memory

; ------------------------------------ read one byte from floppy

onebyte	jsr basin
	ldx status
	rts

; ------------------------------------ read error handler

noflip	jsr setmess	; provide text (index in .x)
	jsr gd_clrms	; set output parameters
	jsr messout	; output text
	jsr sk11		; close file
	clc
	rts

suberror	pla
	pla
error	jsr sk10		; texts
	clc		; set Don't Leave flag
	rts		; end of loader (requester remains open)

; ---------------------------------------------------------------------
; -------------------------------------------------------------- Undo
; ---------------------------------------------------------------------

; -------------------------------------- Install Undo Handler
; Aufruf: lda #8 - sta undotype - jsr undo (DoUndo)
;   bzw.: lda #9 - sta undotype - jsr undo (GetUndo)

undo	lda #<getundo	; eigentliche Routine verschieben
	ldx #>getundo	; (weil das Modul ausgeblendet wird)
	sta ls_vekta8
	stx ls_vekta8+1
	ldy #setreu-getundo
sloop	lda (ls_vekta8),y
	sta sy_global,y	; nach GLOBAL
	dey
	bpl sloop
	jmp sy_global	; und dann getundo ausführen:

; -------------------------------------- 

getundo 	jsr gd_swapd	; Ram einblenden
	ldx #10		; FirstInit
	jsr rm_ramvec

	ldx #1		; Flag für 4Bit-Undo setzen
	stx rm_nextentry
	ldx #$10		; an $1000 in REU: setzen!
	stx rm_werte+3	; *** Fehler im Device! ***
	
perfundo	ldx undotype	; und DoUndo (8) bzw. GetUndo (9)
	jsr rm_ramvec
	ldx #1		; forceoff 
	jmp rm_ramvec

; --------------------------------------- Init REU values

setreu	jsr initreu
	ldx #1		; read/write 1 byte
	stx rm_werte+5
	dex
	lda #byte		; set C64 address: byte ($39)
	sta rm_werte
	stx rm_werte+1
	lda #$10		; set offset in bank ($1000)
	sta rm_werte+3	; (buf+1)
	rts		; $39, $00, $00, $10, 0, 1, 0

; ----------------------------------------- Undo 
; Undo holen    : lda #8 - sta undotype - jsr undo4bit
; Undo schreiben: lda #9 - sta undotype - jsr undo4bit

undo4bit	jsr initreu	; Init REU
	jsr undo		; move: $4000,$001000,32000
	jsr gd_sproff

; ---------------------------------------- Init rm_werte

initreu	ldx #6
	lda #0		; RM_WERTE (am Ende): 
sl0	sta rm_werte,x	; $00, $c0, $00, $00, 0, 0, 0
	dex		; - C64 - | -  REU   - | Anzahl 
	bpl sl0
	lda #$c0
	sta rm_werte+1
	rts

; --------------------------------------- Read from REU

readreu	sty merky
	stx merkx
	lda #$91		; fetch
	jsr gd_reu
	inc rm_werte+2
	bne +
	inc rm_werte+3
+	ldy merky
	ldx merkx
	lda byte
	rts

; ------------------------------------ Text Output Handler

messout	ldx #<message
	ldy #>message
	jmp gd_xtxout2

; --- select texts for output

tcopy	jsr setmess
	beq clrmess

setmess	ldy #0
-	lda txt,x
	beq +
	sta message,y
	inx
	iny
	bne -
+	rts

; --- gauge

action	dec adcnt
	bne ld4
	lda cntwert
	sta adcnt
	ldy offy
	cpy #23
	bne act0
	jsr clrmess
act0	ldx offx
	lda filltab,x
	sta mess,y
	jsr messout
	dec offx
	bpl ld4
	inc offy
	lda #7
	sta offx
ld4	rts

; --- clear gauge

clrmess	ldx #23
	lda #32
cl0	sta mess,x
	dex
	bpl cl0
	ldy #0
	ldx #7
	sty offy
	stx offx
	rts

; --- gauge characters

filltab	!by 160,93,103,127,126,124,105,109

; --- texts

txt	!scr " VRams  @"	; 0
	!scr " Bitmap @"	; 9
	!scr " CRam   @"	; 18
	!scr " Backgr @"	; 27
	!scr " Read   @"	; 36
	!scr " Expand @"	; 45
	!scr " Move   @"	; 54
	!scr "   Most likely no FLIP image.@"	; 63
	!scr "   Packed images need an REU.@"	; 93

; --- text output buffer

message	!fill 8,32
mess	!fill 24,32
	!by 0

; ------------------------------------ data

ftype	!by 0
passes	!by 0
hasreu	!by 0
bank	!by $ff
banks	!by $ff,$ff,0,0,3,7,$ff,1,0

; ------------------------------------ c64 colors

dnib	!by $00,$ff,$44,$cc,$55,$aa,$11,$dd
	!by $66,$22,$99,$33,$77,$ee,$88,$bb

; ------------------------------------ Image Information Handler

getname	ldx #0
si0	lda ls_lastname,x
	beq si1
	sta nbuf,x
	inx
	cpx #16
	bcc si0
si1	rts

; --- set file information

getdatac	ldx #4
	!by $2c
getdatag	ldx #9
	ldy #4
sinfo0	lda dtextc,x
	sta datatype,y
	dex
	dey
	bpl sinfo0
	rts

setinfo	jsr getdatac
	jsr setname
	jsr setloader
	jsr setmode
	jmp setdata

setname	lda #0
	ldx #<ls_picname
	ldy #>ls_picname
	bne si3
setloader	lda #17
	ldx #<ls_iloader
	ldy #>ls_iloader
	bne si3
setmode	lda #25
	ldx #<ls_imode
	ldy #>ls_imode
	bne si3
setdata	lda #33
	ldx #<ls_idrive
	ldy #>ls_idrive
si3	stx dst0
	sty dst0+1
	tax
	ldy #0
si4	lda nbuf,x
	beq si5
	sta (dst0),y
	inx
	iny
	bne si4
si5	rts

nbuf	!fill 16,32
	!by 0
	!scr "FLIP   @"
modetx	!scr "160x200@"
datatype	!scr "Color@"
mtext	!scr "160x200"
dtextc	!scr "Color"

; ------------------------------------ 

modend	!eof
