; ------------------------------------------------------------ 
;
; ldr.HiManRaw
; loader for files of Flexible Line Interpretation mode
;
;    0.99, 03.05.93, first release
;
; ----------------------------------------- Equates
;
*= $c000
!src "godotlib.lib"

	sprite		= $d015
	status		= $90

	dst0		= $35
	dst		= $37
	src		= dst
	byte		= $39
	cntwert		= $3a
	vrcnt		= $3b
	pack		= $3c
	pcnt		= $3d
	data		= $3e
	adcnt		= $40
	offx		= $b2
	offy		= offx+1

	tab00		= $ca00
	tab01		= $cb00
	tab10		= $cc00
	tab11		= $cd00
;
; ----------------------------------------- Header
;
	jmp start
	!by $80
	!by $00,$00
	!by <(modend),>(modend)
	!by $00,$00
	!pet "HiresManager raw"
	!pet "0.99"
	!pet "03.05.93"
	!pet "W.Kling/A.Dettke"
;
; ----------------------------------------- Main
;
jerror	jmp error
;
start	lda #50
	sta cntwert
	sta adcnt
	ldx $20
	ldy $21
	stx dst0
	sty dst0+1
	lda ls_nambuf
	cmp #$c1
	bne stt1
	lda #$81
stt1	sta ls_nambuf
	ldx $a7
	cpx #16
	beq stt2
	lda #$2a
	sta ls_nambuf,x
	inc $a7
stt2	jsr getname
;
	jsr gd_xopen
	jsr onebyte
	bne jerror
	jsr onebyte
	bne jerror
	jsr gd_clrms
	ldx #14
	jsr tcopy
	jsr maketabs
	jsr getbmap
	jsr getvrams
	jmp sk10
;
; ----------------------------------------- Video-RAMs
;
getvrams	jsr gd_clrms
	ldx #0
	jsr tcopy
	jsr setdst0
	lda #8
	sta vrcnt
vloop	lda dst0
	ldy dst0+1
	sta dst
	sty dst+1
vloop0	jsr action
	jsr basin
	tay 
	lda tab00,y
	sta col00
	lda tab01,y
	sta col01
	lda tab10,y
	sta col10
	lda tab11,y
	sta col11
	ldy #3
vloop1	lda (dst),y
	tax 
	lda col00,x
	sta (dst),y
	dey 
	bpl vloop1
	lda dst
	clc 
	adc #32
	sta dst
	bcc vskip0
	inc dst+1
vskip0	lda dst+1
	cmp #$bd
	bne vloop0
	ldy #24
	jsr dumy
	lda dst0
	clc 
	adc #4
	sta dst0
	dec vrcnt
	bne vloop
	rts 
;
; ----------------------------------------- Bitmap
;
getbmap	ldx #7
	jsr tcopy
	lda #50
	sta cntwert
	sta adcnt
;
	jsr setdst0
bloop0	jsr basin
	sta byte
	jsr action
	ldy #0
bloop1	lda #0
	asl byte
	rol 
	asl byte
	rol 
	sta (dst0),y
	iny 
	cpy #4
	bne bloop1
	lda dst0
	clc 
	adc #4
	sta dst0
	bcc bskip0
	inc dst0+1
bskip0	lda dst0+1
	cmp #$bd
	bne bloop0
	ldy #192
	jsr dumy
	rts 
;
; ----------------------------------------- Service Routines
;
maketabs	ldy #0
mtl	tya 
	and #$0f
	tax 
	lda dnib,x
	sta tab00,y
	tya 
	lsr 
	lsr 
	lsr 
	lsr 
	tax 
	lda dnib,x
	sta tab11,y
	and #$0f
	sta byte
	lda tab00,y
	and #$f0
	ora byte
	sta tab01,y
	lda tab00,y
	and #$0f
	sta byte
	lda tab11,y
	and #$f0
	ora byte
	sta tab10,y
	iny 
	bne mtl
	rts 
;
; ----------------------------------------- Finish
;
sk10	jsr gd_xmess
	jsr setinfo
	jsr gd_xclose
	lda #$03
	sta sprite
	sec 
	rts 
;
; ----------------------------------------- Vars
;
col00	!by 0
col01	!by 0
col10	!by 0
col11	!by 0
;
; ----------------------------------------- More Service
;
dumy	jsr basin
	dey 
	bne dumy
	rts 
;
setdst0	lda #<($4000)
	ldy #>($4000)
	sta dst0
	sty dst0+1
	rts 
;
onebyte	jsr basin
	ldx $90
	rts 
;
error	jsr gd_xclose
	jsr sk10
	clc 
	rts 
;
; ----------------------------------------- Messaging
;
messout	ldx #<(message)
	ldy #>(message)
	jmp gd_xtxout2
;
tcopy	ldy #0
tc0	lda txt,x
	beq clrmess
	sta message,y
	inx 
	iny 
	bne tc0
;
action	dec adcnt
	bne ld4
	lda cntwert
	sta adcnt
	ldy offy
	ldx offx
	lda filltab,x
	sta mess,y
	jsr messout
	dec offx
	bpl ld4
	inc offy
	lda #7
	sta offx
ld4	rts 
;
clrmess	ldx #20
	lda #32
cl0	sta mess,x
	dex 
	bpl cl0
	ldy #0
	ldx #7
	sty offy
	stx offx
	rts 
;
dnib	!by $00,$ff,$44,$cc,$55,$aa,$11,$dd
	!by $66,$22,$99,$33,$77,$ee,$88,$bb
;
;
filltab	!by 160,93,103,127,126,124,105,109
;
txt	!scr " VRams@"	; 0
	!scr " BMap @"	; 7
	!scr " BkGr @"	; 14
;
message	!scr " jau  "
	!fill 5,32
mess	!fill 21,32
	!by 0
;
; ----------------------------------------- Image Information
;
	iloader=ls_picname+$16
	imode=ls_picname+$22
	idrive=ls_picname+$2e
;
getname	ldx #0
si0	lda ls_lastname ,x
	beq si1
	sta nbuf,x
	inx 
	cpx #16
	bcc si0
si1	rts 
;
getdatac	ldx #4
	!by $2c
getdatag	ldx #9
	ldy #4
sinfo0	lda dtextc,x
	sta datatype,y
	dex 
	dey 
	bpl sinfo0
	rts 
;
setinfo	jsr getdatac
	jsr setname
	jsr setloader
	jsr setmode
	jmp setdata
;
setname	lda #0
	ldx #<(ls_picname)
	ldy #>(ls_picname)
	bne si3
setloader	lda #17
	ldx #<(iloader)
	ldy #>(iloader)
	bne si3
setmode	lda #25
	ldx #<(imode)
	ldy #>(imode)
	bne si3
setdata	lda #33
	ldx #<(idrive)
	ldy #>(idrive)
si3	stx dst0
	sty dst0+1
	tax 
	ldy #0
si4	lda nbuf,x
	beq si5
	sta (dst0),y
	inx 
	iny 
	bne si4
si5	rts 
;
nbuf	!fill 16,32
	!by 0
	!scr "HiresM @"
modetx	!by $33,$32,$30,24,$32,$30,$30,0
datatype	!by 67,15,12,15,18,0
ttext	!by 84,5,24,20,32,32,32
htext	!by 72,9,18,5,19,32,32
mtext	!by $33,$32,$30,24,$32,$30,$30
dtextc	!by 67,15,12,15,18
dtextg	!by 71,18,5,25,32
modend	!eof 
