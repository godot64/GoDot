
	!src "godotlib.lib"
	*= $c000
	!ct pet
;	!sl "labels.txt"

; ---------------------------
;
;    ldr.IFF
;    Lader für IFF OCS, 
;    bis zu einer Größe von 1024x768
;    unterstützt HAM, EHB und Interlace
;
;    1.00: 23.03.93, first release
;    1.01: 22.05.93, bugfix: Füllbytes im Chunk abfangen
;    1.02: 10.10.16, added: Balancing
;    1.03: 14.09.21, added: break loading process with STOP key
;
;
; ---------------------------

; --------------------------- Equates

	shift		= $028d
;
	red		= $35
	grn		= $36
	blu		= $37
	min		= $38
	gsum		= min
	minx		= $3a
	cmg		= $3b
	lace		= $3c
	pbuf		= $3d
	pcnt		= $3f
	adcnt		= $40
	nflg		= $41
	zflg		= $42
	cgflg		= $43
	gpy		= $44
	gpx		= $46
	palcnt		= $47
	xbuf		= $47
	ybuf		= $48

	name		= $b0
	dst		= name
	spalte		= $b1
	offy		= $b2
	comp		= $b3

	dst0		= $f7
	bbuf		= $f9
	lcnt8		= $fa
	lcnt		= $fb
	bcnt		= $fc
	plmask		= $fd
	hamnib		= plmask
	bytes		= $fe
;
	pal		= $3f40
	hdbuf		= modend
	planes		= hdbuf+8
	rbk		= $cc00
	gbk		= rbk+64
	bbk		= gbk+64
	dbuf		= $cd00
	dbuf1		= dbuf+64

	p0		= $bd00
	p1		= p0+128
	p2		= p1+128
	p3		= p2+128
	p4		= p3+128
	p5		= p4+128
;
; ---------------------------

header	jmp start
	!by $80
	!by 4
	!by 1		     ; dirty
	!wo modend
	!wo 0
	!pet "AMIGA ILBM (OCS)"
	!pet "1.03"
	!pet "14.09.21"
	!pet "W.Kling/A.Dettke"
;
; ---------------------------

start	ldx #$2f
stt0	lda gr_rgb,x
	sta cred,x
	dex
	bpl stt0
	inx
	stx sc_stop	; init break by STOP ### 1.03

stt1	ldx used
	bmi requ
	beq cancel
	cpx #3
	bcs cancel
;
	dec used
	jsr getname
	jsr gd_xopen
	jsr gd_clrms
	ldx #6
cl0	lda ilbm,x
	sta message,x
	dex
	bpl cl0
	lda #32
	ldx #24
cl	sta mess,x
	dex
	bpl cl
	lda #200
	sta adcnt
	lda #0
	sta offy

	jsr checkiff
	ldy #0
	tya
clbl	sta p0,y
	sta p2,y
	sta p4,y
	iny
	bne clbl
	jsr getbody	; retrieve image

	lda which
	beq cl01
	jsr getdatac
	beq cl02
cl01	jsr getdatag

cl02	jsr cancel
err	jsr gd_xclose
	jsr gd_spron
	jsr gd_xmess
	bcc cn2

; --------------------------- Exit Loader

cancel	lda #$ff		; SE: Leave Loader
	sta used
	lda shift		; Shift (-Lock) gedrückt?
	and #1
	bne cn2		; ja, Sprung

	ldy #16		; sonst: Balancing-Tabelle initialisieren
	sty gr_ctr
	sty gr_brt
lll	dey 
	tya 
	sta gr_btab,y
	bne lll

cn2	sec 		; Leave Loader to Main Screen
	rts 
;
; ---------------------------

requ	inc used
	ldx #<(ifflst)
	ldy #>(ifflst)
	jsr gd_xmloop
	jmp stt1
;
; ---------------------------

sevcol	inc used
sevgray   inc used
	ldx used
	dex
	stx which
	ldx #1
	jsr gd_xload	; open file requester
	lda #3
	sta used
	sec
	rts
;
; ---------------------------

adinc	ldy offy
	dec adcnt
	lda adcnt
	and #7
	bne adskip
	lda filltab
	sta mess,y
	inc offy
	bne messout
adskip	tax
	lda filltab,x
	sta mess,y
;
messout   ldx #<(message)
	ldy #>(message)
	jmp gd_xtxout2
;
filltab   !by 160,93,103,127,126,124,105,109
;
ilbm	!scr " ILBM  "
message   !scr " ILBM  "
mess	!pet "                         "
	!by 0
;
; ---------------------------

err1	!scr "ERROR: Width must be <1024 pxls."
err2	!scr "    ERROR: Not an IFF file.     "
err3	!scr "  ERROR: No valid IFF picture.  "
err4	!scr "    ERROR: Too many colors.     "
err5	!scr "  Loading terminated by STOP.   "

; ---------------------------

checkiff  ldy #3
il1	jsr basin
	cmp form,y
	bne faila
	dey
	bpl il1
	jsr dum4
	ldy #7
il2	jsr basin
	cmp ilbmbmhd,y
	bne failb
	dey
	bpl il2
	jsr dum4
	sta chl
	jsr gethd
	cpx #7
	bcs faild
	lda xsize+1
	beq ghs2
	cmp #>(1024)
	beq ghs1
	bcs failc
	bcc ghs2
ghs1	lda xsize
	bne failc
ghs2	lda #<(cmap)
	ldx #>(cmap)
	jsr search
	ldx #3
	jsr dumx
	sta gpy+1
	jsr basin
	sta gpy
	ora cmgfake
	sta cmgfake
	jsr chkcols
	bcs faild
	jsr setconv
	jsr getpal
	lda #<(body)
	ldx #>(body)
	jsr search
	jsr setcmg
	jsr setvmode
	jmp dum4
;
; ---------------------------

faila	ldx #32		; no IFF
	!by $2c
failb	ldx #64		; not valid
	!by $2c
failc	ldx #0		; width >1024
	!by $2c
faild	ldx #96		; too many colors
	!by $2c
faile	ldx #128		; break by STOP
	ldy #0
fl0	lda err1,x
	sta message,y
	inx
	iny
	cpy #32
	bne fl0
	jsr err
	jsr gd_clrms
	jsr messout
	jsr wait
	clc
	jmp fail3
;
; ----------------------------------------- Wait for Clicks

wait	lda #0
	sta sc_stop
	sta sc_keyprs
waiton	lda joy		; wait for click or STOP
	eor #$ff
	and #$10
	ora sc_keyprs
	ora sc_stop
	beq waiton
	rts

; ---------------------------

chkcols   lda gpy+1
	cmp #3
	bcc ccs2
	beq ccs1
	bcs ccs2
ccs1	lda gpy
	cmp #1
ccs2	rts
;
; ---------------------------

setconv   ldx used
	beq il3
	lda #<(assigncol)
	ldy #>(assigncol)
	bne il4
il3	lda #<(makegray)
	ldy #>(makegray)
il4	sta convmode
	sty convmode+1
	rts
;
; ---------------------------

convcol   jmp (convmode)
;
convmode  !by 0,0

; ---------------------------
;
dum4	ldx #4
dumx	jsr basin
	dex
	bne dumx
	rts
;
; ---------------------------

gethd	ldx #0
hdloop	jsr basin
	sta hdbuf,x
	inx
	cpx chl
	bne hdloop
	lda hdbuf
	sta xsize+1
	lda hdbuf+1
	sta xsize
	lda hdbuf+2
	sta ysize+1
	lda hdbuf+3
	sta ysize
	lda #0
	sta lace
	sta lacefake
	sta cmg
	lda hdbuf+2
	beq nolace
	cmp #$02
	bcs lace0
	lda hdbuf+3
	cmp #$23
	bcc nolace
lace0	inc lacefake
nolace	ldx planes
	stx cmgfake
	lda pltab,x
	sta plmask
	lda xsize
	sta bytes
	lda xsize+1
	lsr
	ror bytes
	lsr
	ror bytes
	lsr
	ror bytes
	lda xsize
	and #15
	beq hds1
	inc bytes
	and #8
	bne hds1
	inc bytes
hds1	lda hdbuf+10
	sta comp
	rts
;
; ---------------------------

cmgfake   !by 0
lacefake  !by 0
;
; ---------------------------

search	sta name
	stx name+1
searchl   lda #0
	sta okflg
	sta cgflg
	ldy #3
l6	jsr basin
	cmp (name),y
	beq s1
	inc okflg
s1	cmp camg,y
	beq s11
	inc cgflg
s11	dey
	bpl l6
	lda okflg
	beq ok
	lda cgflg
	beq getcmg
	ldx #3
	jsr dumx
	sta chl+1
	jsr basin
	sta chl
	and #1
	beq l7
	inc chl
	bne l7
	inc chl+1
l7	jsr basin
	lda chl
	bne s2
	dec chl+1
s2	dec chl
	lda chl
	ora chl+1
	bne l7
	beq searchl
ok	rts
;
; ---------------------------

getcmg	jsr dum4
	ldx #3
	stx cmg
	jsr dumx
	and #$08
	cmp #$08
	bne gcs1
	lda #54
	sta cmg
gcs1	jsr basin
	tax
	and #4
	sta lace
	txa
	and #$80
	cmp #$80
	bne gcs2
	lda #102
	sta cmg
gcs2	jmp searchl
;
; ---------------------------

getpal	ldx #0
gpl4	ldy gpy
	jsr basin
	lsr
	lsr
	lsr
	lsr
	jsr balancing
	sta red
	sta rbk,x
	dey
	beq palq
	jsr basin
	lsr
	lsr
	lsr
	lsr
	jsr balancing
	sta grn
	sta gbk,x
	dey
	beq palq
	jsr basin
	lsr
	lsr
	lsr
	lsr
	jsr balancing
	sta blu
	sta bbk,x
	sty gpy
	stx gpx
	cpx #64
	bcs fedup
	jsr convcol
	ldx gpx
	sta pal,x
fedup	inx
	dec gpy
	bne gpl4
	dec gpy+1
	bpl gpl4
palq	rts
;
; ---------------------------

balancing	stx gpx
	tax
	lda gr_btab,x
	ldx gpx
	rts

; ---------------------------

assigncol lda red
	clc
	adc grn
	adc blu
	tax
	lda avr,x
	ldy #0
	cmp red
	bne as0
	iny
as0	cmp grn
	bne as1
	iny
as1	cmp blu
	bne as2
	iny
as2	cpy #2
	bcc cl51
	tax
	lda gray,x
	tax
	lda cpref,x
	tax
	lda dnib,x
	rts
;
cl51	ldx level
cl6	lda #0
	sta dbuf1,x
	sta nflg
	sta zflg
	lda cred,x
	sec
	sbc red
	bne rd0
	inc zflg
rd0	bcs rpos
	eor #$ff
	adc #1
	inc nflg
rpos	tay
	lda quad,y
	sta dbuf,x
	lda cgrn,x
	sec
	sbc grn
	bne gn0
	inc zflg
gn0	bcs gpos
	eor #$ff
	adc #1
	inc nflg
gpos	tay
	lda quad,y
	clc
	adc dbuf,x
	sta dbuf,x
	bcc gsk
	inc dbuf1,x
gsk	lda cblu,x
	sec
	sbc blu
	bne bl0
	inc zflg
bl0	bcs bpos
	eor #$ff
	adc #1
	inc nflg
bpos	tay
	lda nflg
	beq nopf
	clc
	adc zflg
	cmp #3
	beq nopf
	lda #2
	!by $2c
nopf	lda #0
	clc
	adc quad,y
	adc dbuf,x
	sta dbuf,x
	bcc bsk
	inc dbuf1,x
bsk	inx
	cpx #16
	bne cl6
;
	stx min+1
	ldx level
ml1	lda dbuf1,x
	tay
	cmp min+1
	beq ms1
	bcs ms2
	bcc ms3
ms1	lda dbuf,x
	cmp min
	beq ms4
	bcs ms2
ms3	lda dbuf,x
ms4	sty min+1
	sta min
	stx minx
ms2	inx
	cpx #16
	bne ml1
	lda minx
	tay
	lda cpref,y
	tay
	lda dnib,y
	rts
;
; ---------------------------

avr	!by 0,0,1,1,1,2,2,2
	!by 3,3,3,4,4,4,5,5,5
	!by 6,6,6,7,7,7,8,8,8
	!by 9,9,9,10,10,10
	!by 11,11,11,12,12,12
	!by 13,13,13,14,14,14
	!by 15,15
;
; ---------------------------

quad	!by 0,1,4,9,16,25,36,49,64,81,100,121,144,169,196,225
;
; ---------------------------

cred	!by 11,7,3,0,15
	!by 06,15,13,12,10,08
	!by 00,00,00,10,14
;
cgrn	!by 11,7,3,0,15
	!by 04,07,08,04,15,00
	!by 08,00,12,15,14
;
cblu	!by 11,7,3,0,15
	!by 00,07,05,12,14,00
	!by 15,10,05,06,07
;
cpref	!by 11,7,3,0,15
	!by 2,9,6,5,12,4
	!by 8,1,10,14,13
;
gray	!by 3,3,2,2,2,2,1,1,1,1,0,0,0,0,4,4
;
; ---------------------------

setcmg	lda cmg
	bne scs0
	lda cmgfake
	sta cmg
	lda lacefake
	sta lace
scs0	lda cmg
	cmp #102
	bne scs1
	ldx #31
	ldy used
	beq scl2
scl1	lda rbk,x
	lsr
	sta red
	lda gbk,x
	lsr
	sta grn
	lda bbk,x
	lsr
	sta blu
	stx gpx
	jsr convcol
	ldx gpx
	cmp pal,x
	bne scs2
	and #$0f
	tay
	lda ehbtab,y
scs2	sta pal+32,x
	dex
	bpl scl1
scs1	rts
;
; ---------------------------

ehbtab	!by $00,$00,$00,$00
	!by $22,$22,$44,$33
	!by $11,$44,$33,$77
	!by $77,$66,$aa,$77
;
; ---------------------------

scl2	lda pal,x
	and #$0f
	lsr
	tay
	lda dnib,y
	sta pal+32,x
	dex
	bpl scl2
	rts
;
setvmode  ldx planes
	dex
	lda cmg
	cmp #54
	bne svms1
	ldx #7
svms1	cmp #102
	bne svms2
	inx
svms2	txa
	asl
	asl
	tax
	ldy #3
svml	lda vmodes,x
	sta viewmode+3,y
	inx
	dey
	bpl svml
	sty palcnt
	lda ysize
	bne nopal
	lda ysize+1
	cmp #3
	bcs nopal
	lda #4
	sta palcnt
nopal	rts
;
vmodes	!pet "  2   4   8  61  23  46 bhe mah "
;
; ---------------------------

readline  lda #<(p0)
	ldx #>(p0)
	sta pbuf
	stx pbuf+1
	sta red
	sta grn
	sta blu
	lda planes
	sta pcnt
nline	ldy #0
	ldx sc_stop	; STOP pressed?	### 1.03
	bne jfaile
	lda comp
	beq uncomp
next	jsr basin
	tax
	bmi decomp
rll	jsr basin
	sta (pbuf),y
	iny
	dex
	bpl rll
	bmi rls
decomp	dex
	jsr basin
rll1	sta (pbuf),y
	iny
	inx
	bne rll1
rls	cpy bytes
	bcc next
	bcs rls1
;
; ---------------------------

uncomp	jsr basin
	sta (pbuf),y
	iny
	cpy bytes
	bcc uncomp
rls1	lda pbuf
	clc
	adc #128
	sta pbuf
	bcc rls2
	inc pbuf+1
rls2	dec pcnt
	bne nline
	lda xsize+1
	lsr
	bne squeeze
	rts
;
; ---------------------------

jfaile	jmp faile

fail3	pla
	pla
	inc used
	rts
;
; ---------------------------

squeeze   lda #<(p0)
	ldx #>(p0)
	sta pbuf
	stx pbuf+1
	lda planes
	sta pcnt
sql0	ldx #0
	ldy #0
sql1	lda (pbuf),y
	asl
	rol bbuf
	asl
	asl
	rol bbuf
	asl
	asl
	rol bbuf
	asl
	asl
	rol bbuf
	asl
	sta (pbuf),y
	iny
	lda (pbuf),y
	asl
	rol bbuf
	asl
	asl
	rol bbuf
	asl
	asl
	rol bbuf
	asl
	asl
	rol bbuf
	asl
	sta (pbuf),y
	sty ybuf
	txa
	tay
	lda bbuf
	sta (pbuf),y
	inx
	ldy ybuf
	iny
	cpy bytes
	bne sql1
	lda pbuf
	clc
	adc #128
	sta pbuf
	bcc sqs1
	inc pbuf+1
sqs1	dec pcnt
	bne sql0
	rts
;
; ---------------------------

pltab	!by 0,1,3,7,15,31,63
;
dnib	!by $00,$11,$22,$33
	!by $44,$55,$66,$77
	!by $88,$99,$aa,$bb
	!by $cc,$dd,$ee,$ff
;
; ---------------------------

makegray  lda red
	cmp grn
	bne gs51
	cmp blu
	bne gs51
	jmp grok0
;
gs51	asl
	tay
	lda rtab,y
	sta gsum
	lda rtab+1,y
	sta gsum+1
	lda grn
	asl
	tay
	lda gtab,y
	clc
	adc gsum
	sta gsum
	lda gtab+1,y
	adc gsum+1
	sta gsum+1
	lda blu
	asl
	tay
	lda btab,y
	clc
	adc gsum
	sta gsum
	lda btab+1,y
	adc gsum+1
	sta gsum+1
	lda gsum
	clc
	adc #128
	lda gsum+1
	adc #0
grok0	tay
	lda dnib,y
	rts
;
; ---------------------------

getbody   lda cmg
	cmp #54
	bne noham3
	jmp getham
noham3	lda #<($4000)
	ldx #>($4000)
	sta dst0
	sta dst
	stx dst0+1
	stx dst+1
	lda #25
	sta lcnt8
lnloop8   lda #8
	sta lcnt
lnloop	jsr adinc
	jsr readline
	ldx lace
	beq lcs0
	jsr readline
lcs0	dec palcnt
	bne lcs01
	lda #4
	sta palcnt
	jsr readline
	ldx lace
	beq lcs01
	jsr readline
lcs01	ldx #0
byloop	lda #4
	sta bcnt
btloop	asl p5,x
	rol
	asl p4,x
	rol
	asl p3,x
	rol
	asl p2,x
	rol
	asl p1,x
	rol
	asl p0,x
	rol
	and plmask
	tay
	lda pal,y
	and #$f0
	sta bbuf
	asl p5,x
	rol
	asl p4,x
	rol
	asl p3,x
	rol
	asl p2,x
	rol
	asl p1,x
	rol
	asl p0,x
	rol
	and plmask
	tay
	lda pal,y
	and #$0f
	ora bbuf
	ldy #0
	sta (dst),y
	inc dst
	bne s3
	inc dst+1
s3	dec bcnt
	bne btloop
	lda dst
	clc
	adc #28
	sta dst
	bcc s4
	inc dst+1
s4	inx
	cpx #40
	bne byloop
	lda dst0
	clc
	adc #4
	sta dst0
	sta dst
	bcc s5
	inc dst0+1
s5	lda dst0+1
	sta dst+1
	dec lcnt
	beq rdy
	jmp lnloop
rdy	lda dst0
	clc
	adc #<(1248)
	sta dst0
	sta dst
	lda dst0+1
	adc #>(1248)
	sta dst0+1
	sta dst+1
	dec lcnt8
	beq rdy1
	jmp lnloop8
rdy1	rts
;
; ---------------------------

rtab	!by $00,$00,$4d,$00
	!by $9a,$00,$e7,$00
	!by $34,$01,$81,$01
	!by $ce,$01,$1b,$02
	!by $68,$02,$b5,$02
	!by $02,$03,$4f,$03
	!by $9c,$03,$e9,$03
	!by $36,$04,$83,$04
;
gtab	!by $00,$00,$97,$00
	!by $2e,$01,$c5,$01
	!by $5c,$02,$f3,$02
	!by $8a,$03,$21,$04
	!by $b8,$04,$4f,$05
	!by $e6,$05,$7d,$06
	!by $14,$07,$ab,$07
	!by $42,$08,$d9,$08
;
btab	!by $00,$00,$1c,$00
	!by $38,$00,$54,$00
	!by $70,$00,$8c,$00
	!by $a8,$00,$c4,$00
	!by $e0,$00,$fc,$00
	!by $18,$01,$34,$01
	!by $50,$01,$6c,$01
	!by $88,$01,$a4,$01
;
; ---------------------------

getham	lda #<($4000)
	ldx #>($4000)
	sta dst0
	sta dst
	stx dst0+1
	stx dst+1
	lda #25
	sta lcnt8
hlnloop8  lda #8
	sta lcnt
hlnloop   jsr adinc
	jsr readline
	ldx lace
	beq lcs1
	jsr readline
lcs1	dec palcnt
	bne lcs11
	lda #4
	sta palcnt
	jsr readline
	lda lace
	beq lcs11
	jsr readline
lcs11	ldx #0
hbyloop   lda #4
	sta bcnt
hbtloop   asl p3,x
	rol
	asl p2,x
	rol
	asl p1,x
	rol
	asl p0,x
	rol
	and #$0f
	tay
	asl p5,x
	rol
	asl p4,x
	rol
	and #3
	beq pure
	stx xbuf
	eor #$02
	cmp #$03
	bne hs1
	and #$02
hs1	tax
	sty red,x
	jsr convcol
	ldx xbuf
	bpl hok
pure	lda rbk,y
	sta red
	lda gbk,y
	sta grn
	lda bbk,y
	sta blu
	lda pal,y
;
hok	and #$f0
	sta bbuf
	asl p3,x
	rol
	asl p2,x
	rol
	asl p1,x
	rol
	asl p0,x
	rol
	and #$0f
	tay
	asl p5,x
	rol
	asl p4,x
	rol
	and #3
	beq pure1
	stx xbuf
	eor #$02
	cmp #$03
	bne hs2
	and #$02
hs2	tax
	sty red,x
	jsr convcol
	ldx xbuf
	bpl hok1
pure1	lda rbk,y
	sta red
	lda gbk,y
	sta grn
	lda bbk,y
	sta blu
	lda pal,y
;
hok1	and #$0f
	ora bbuf
	ldy #0
	sta (dst),y
	inc dst
	bne hs3
	inc dst+1
hs3	dec bcnt
	beq hs31
	jmp hbtloop
hs31	lda dst
	clc
	adc #28
	sta dst
	bcc hs4
	inc dst+1
hs4	inx
	cpx #40
	beq hs41
	jmp hbyloop
hs41	lda dst0
	clc
	adc #4
	sta dst0
	sta dst
	bcc hs5
	inc dst0+1
hs5	lda dst0+1
	sta dst+1
	dec lcnt
	beq hrdy
	jmp hlnloop
hrdy	lda dst0
	clc
	adc #<(1248)
	sta dst0
	sta dst
	lda dst0+1
	adc #>(1248)
	sta dst0+1
	sta dst+1
	dec lcnt8
	beq hrdy1
	jmp hlnloop8
hrdy1	rts
;
; ---------------------------

form	!pet "mrof"
ilbmbmhd  !pet "dhmbmbli"
cmap	!pet "pamc"
camg	!pet "gmac"
body	!pet "ydob"
xsize	!by 0,0
ysize	!by 0,0
okflg	!by 0
chl	!by 0,0
;
used	!by $ff
which	!by 0
level	!by 0
lvlnum	!pet "012345"
;
; ---------------------------

ifflst	!by 0
	!by 4,11,18,16,$81,0,0
	!scr "Load Amiga IFF@"
	!by 9,11,18,3,$ca,<(sevgray),>(sevgray)
	!scr "Gray Scale@"
	!by 12,11,11,3,$c8,<(sevcol),>(sevcol)
	!scr "Color@"
lvlgad	!by 12,22,7,3,$cf,<(sevlvl),>(sevlvl)
	!scr "Lvl "
lvltx	!by $30,0
	!by 15,11,18,3,$ca,<(cn2),>(cn2)
	!scr "Cancel Load@"
	!by $c0,6,14,10
	!scr "Convert to@" 
	!by $80
;
; ---------------------------

sevlvl	lda spalte
	cmp #25
	bcs lvlinc
lvldec	dec level
	bpl lvl0
	lda #4
	sta level
lvlinc	inc level
lvl0	ldx level
	cpx #6
	bcc lvl1
	ldx #0
	stx level
lvl1	lda lvlnum,x
	sta lvltx
	jsr gettab
	ldy #6
	jmp gd_xtxout1
;
; ---------------------------

gettab	lda #<(lvlgad)
	sta sc_screentab
	lda #>(lvlgad)
	sta sc_screentab+1
	lda #0
	tax
	tay
gt0	jsr gd_setpos
	cpx #3
	bne gt0
	stx sc_ho
	jsr gd_trim
	jmp gd_initmove
;
; ---------------------------

getname   ldx #0
si0	lda ls_lastname,x
	beq si1
	sta nbuf,x
	inx
	cpx #16
	bcc si0
si1	rts
;
getdatac  ldx #4
	!by $2c
getdatag  ldx #9
	ldy #4
sinfo0	lda dtextc,x
	sta datatype,y
	dex
	dey
	bpl sinfo0
;	rts
;
;  setinfo   jsr getdatag
	jsr setname
	jsr setloader
	jsr setmode
	jmp setdata
;
setname   lda #0
	ldx #<(ls_picname)
	ldy #>(ls_picname)
	bne si3
setloader lda #17
	ldx #<(ls_iloader)
	ldy #>(ls_iloader)
	bne si3
setmode   lda #25
	ldx #<(ls_imode)
	ldy #>(ls_imode)
	bne si3
setdata   lda #33
	ldx #<(ls_idrive)
	ldy #>(ls_idrive)
si3	stx sc_texttab
	sty sc_texttab+1
	tax
	ldy #0
si4	lda nbuf,x
	beq si5
	sta (sc_texttab),y
	inx
	iny
	bne si4
si5	rts
;
nbuf	!fill 16,32
	!by 0
viewmode  !scr "IFF    @"
modetx	!scr "320x200@"
datatype  !scr "Color@"
mtext	!scr "320x200"
dtextc	!scr "Color"
dtextg	!scr "Gray "
modend	!eof
