
!src "godotlib.lib"
*	= $c000

; ---------------------------
;
;  ldr.PaintMagic
;  Loader for PaintMagic multicolor images
;
;  1.00: 01.08.94, first release
;  1.01: 09.01.17, saves original bg color
;
; ---------------------------

; ----------------------------------------- Equates

	 fflag		= $30
	 backc		= $31
	 mcolorram	= $32
	 cflag		= $33
	 byte		= $3d
	 cnt		= $3e
	 cntac		= $3f
	 vr		= $45
	 cr		= $47

	 status	= $90
	 offx		= $b2
	 offy		= offx+1

	 vrbuf		= $c600
	 crbuf		= vrbuf+1000
	 col0		= vrbuf+2000
;
; --------------------------- Header

	jmp start
	!by $80
	!by $00,$01
	!by <(modend),>(modend)
	!by $00,$00
	!pet "PaintMagic Multi"
	!pet "1.01"
	!pet "09.01.17"
	!pet "A.Dettke/W.Kling"
;
; --------------------------- Main
;
jerror	jmp error
;
start	lda #50
	sta cntwert
	sta cntac
	ldx #<($4000)
	ldy #>($4000)
	stx sc_texttab
	sty sc_texttab+1
	stx fflag
	ldx ls_flen
	cpx #16
	beq stt2
	lda #$2a
	sta ls_nambuf,x
	inc ls_flen
	jsr getname
;
; --------------------------- Get Multicolor Image

stt2	jsr gd_xopen
	jsr gd_clrms
	ldx #18
	jsr tcopy
	ldy #116		; Skip header
stt3	jsr onebyte
	bne jerror
	dey 
	bne stt3
	lda #<(8000)	; Read bitmap (8000 Bytes)
	sta ls_vekta8
	lda #>(8000)
	sta ls_vekta8+1
;
loop	jsr onebyte
l1	bne jerror
	sta byte
	jsr action
	ldy #0
	ldx #4
bloop	lda #0
	asl byte
	rol 
	asl byte
	rol 
	sta (sc_texttab),y
	inc sc_texttab
	bne sk
	inc sc_texttab+1
sk	dex 
	bne bloop
	lda ls_vekta8
	bne sk1
	dec ls_vekta8+1
sk1	dec ls_vekta8
	lda ls_vekta8
	ora ls_vekta8+1
	beq sk2
	lda status
	beq loop
;
sk2	jsr onebyte	; Read bg color
	bne l1
	and #15
	sta backc
 	sta gr_fbkcol	; v1.01: store original file background color

	ldy #3
ll0	jsr onebyte
	bne l1
	dey 
	bne ll0
	sta mcolorram
	ldy #188		; skip remainder
ll1	jsr onebyte
l10	bne l1
	dey 
	bne ll1
	sty cflag
;
ll7	lda #<(1000)	; read color ram (1000 bytes)
	sta ls_vekta8
	lda #>(1000)
	sta ls_vekta8+1
	lda cflag
	bne ll4
	lda #<(vrbuf)
	sta sc_texttab
	lda #>(vrbuf)
	sta sc_texttab+1
;
ll4	lda #7
	sta cntwert
	sta cntac
	ldx #0
	jsr tcopy
;
loop1	lda cflag
	beq ll2
	lda mcolorram
	jmp ll3
ll2	jsr onebyte
	bne l10
ll3	sta (sc_texttab),y
	jsr action
	ldy #0
	inc sc_texttab
	bne sk3
	inc sc_texttab+1
sk3	lda ls_vekta8
	bne sk4
	dec ls_vekta8+1
sk4	dec ls_vekta8
	lda ls_vekta8
	ora ls_vekta8+1
	beq ll5
	lda status
	beq loop1
;
ll5	lda cflag
	bne sk5
	dec cflag
	bne ll7
;
sk5	lda backc
	sta (sc_texttab),y
	jsr gd_xclose
	lda #<(vrbuf)
	sta vr
	lda #>(vrbuf)
	sta vr+1
	lda #<(crbuf)
	sta cr
	lda #>(crbuf)
	sta cr+1
	lda #<($4000)
	sta sc_texttab
	lda #>($4000)
	sta sc_texttab+1
	lda #<(1000)
	sta ls_vekta8
	lda #>(1000)
	sta ls_vekta8+1
;
	lda #200
	sta cntwert
	sta cntac
	ldx #9
	jsr tcopy
;
loop2	lda (vr),y
	pha 
	lsr 
	lsr 
	lsr 
	lsr 
	sta col0+1
	pla 
	and #$0f
	sta col0+2
	lda (cr),y
	and #$0f
	sta col0+3
	lda #32
	sta cnt
bloop1	jsr action
	ldy #0
	lda (sc_texttab),y
	tax 
	lda col0,x
	tax 
	lda dnib,x
	sta (sc_texttab),y
	inc sc_texttab
	bne sk6
	inc sc_texttab+1
sk6	dec cnt
	bne bloop1
	inc vr
	bne sk7
	inc vr+1
sk7	inc cr
	bne sk8
	inc cr+1
sk8	lda ls_vekta8
	bne sk9
	dec ls_vekta8+1
sk9	dec ls_vekta8
	lda ls_vekta8
	ora ls_vekta8+1
	bne loop2
	ldx #18
	jsr tcopy
;
; --------------------------- Close File

sk10	jsr gd_xmess	; error message from drive
	lda fflag
	bne ll6
	jsr setinfo
ll6	jsr gd_spron	; sprite pointer on
	sec 
	rts 
;
; --------------------------- 

onebyte	jsr basin
	ldx $90
	rts 
;
error	jsr gd_xclose
	jsr sk10
	dec fflag
	clc 
	rts 
;
; --------------------------- Activity Display

messout	ldx #<(message)
	ldy #>(message)
	jmp gd_xtxout2
;
tcopy	ldy #0
tc0	lda txt,x
	beq clrmess
	sta message,y
	inx 
	iny 
	bne tc0
;
action	dec cntac
	bne ld4
	lda cntwert
	sta cntac
	ldy offy
	ldx offx
	lda filltab,x
	sta mess,y
	jsr messout
	dec offx
	bpl ld4
	inc offy
	lda #7
	sta offx
ld4	rts 
;
clrmess	ldx #20
	lda #32
cl0	sta mess,x
	dex 
	bpl cl0
	ldy #0
	ldx #7
	sty offy
	stx offx
	rts 
;
; --------------------------- Palette Table for MC double pixels
;
dnib	!by $00,$ff,$44,$cc,$55,$aa,$11,$dd
	!by $66,$22,$99,$33,$77,$ee,$88,$bb
;
;
filltab	!by 160,93,103,127,126,124,105,109
;
cntwert	!by 50
;
txt	!scr " Colors @"
	!scr " Convert@"
	!scr " Bitmap @"
message	!scr " Bitmap    "
mess	!scr "....................."
	!by 0
;

; --------------------------- Save Filename
;
getname	ldx #0
si0	lda ls_lastname,x
	beq si1
	sta nbuf,x
	inx 
	cpx #16
	bcc si0
si1	rts 
;
; --------------------------- Image Information

setinfo	jsr setname
	jsr setloader
	jsr setmode
	jmp setdata
;
setname	lda #0
	ldx #<(ls_picname)
	ldy #>(ls_picname)
	bne si3
setloader	lda #17
	ldx #<(ls_iloader)
	ldy #>(ls_iloader)
	bne si3
setmode	lda #25
	ldx #<(ls_imode)
	ldy #>(ls_imode)
	bne si3
setdata	lda #33
	ldx #<(ls_idrive)
	ldy #>(ls_idrive)
si3	stx sc_texttab
	sty sc_texttab+1
	tax 
	ldy #0
si4	lda nbuf,x
	beq si5
	sta (sc_texttab),y
	inx 
	iny 
	bne si4
si5	rts 
;
nbuf	!fill 16,32
	!by 0
	!scr " PMagic @"
modetx	!scr "160x200@"
datatype	!scr "Color@"
modend	!eof
