
!src "godotlib.lib"
*	= $c000

; -----------------------------------------
; Loader ComputerEyes video digitizer (NTSC)
;        1.00, 26.05.06: first release
;        1.01, 29.06.06: added SCPU awareness (slowdown)
; ----------------------------------------- 

; ----------------------------------------- Equates

	src		= $30	; /$31
	dst		= $32	; /$33
	tmpvec		= $34	; /$35
	mask		= $36

	cpu		= $01
	color		= $30
	farbe		= $32
	bitmap		= $34
	fourbit		= $36
	backc		= $38
	hinten		= $39
	vorne		= $3a
	oben		= $3b
	bytcnt		= $3c
	blkcnt		= $3d
	byte		= $3e
	merkbyte		= $3f
	yps		= $40
	merk		= $41
	vekta8		= $a8
	dest1		= $b0
	adcnt		= $ff
;
	vram		= $cc17
	vram1		= $cc16
;
	sprptr		= $07f8
	spr255		= $3fc0
	x2		= $d004
	y2		= x2+1
	spren		= $d015
	sprxe		= $d01d
	sprcol2		= $d029
;
	sprpntr		= $07e8
	scpuflg		= $1d6b	; must be $3e for SCPU
	speed01		= $d07a	; reg for 1MHz
	speed20		= $d07b	; reg for 20MHz

	graphics		= $d011
	graphbnk		= $d018
	updata		= $dd01	; $dd01
	updirct		= $dd03	; $dd03

; ----------------------------------------- Module Header

header	jmp start
	!by $80
	!by 4
	!by 0
	!wo modend
	!wo 0
	!pet "ComputerEyes    "
	!pet "1.01"
	!pet "29.06.06"
	!pet "A.Dettke        "

; ----------------------------------------- Main

start	jsr speed		; slowdown SCPU
	jsr mchar		; define listbox char
startover	ldx used		; first call?
	bmi requ		; yes, pre-requester
	beq cancel	; error and finished
	cpx #3		; all finished?
	bcs cancel
	rts

; ----------------------------------------- Leave Module

se_leave	jsr speed		; speedup SCPU
	lda #3
	bne cn1
cancel	lda #$ff
cn1	sta used
cn2	sec
	rts

; ----------------------------------------- Display First Requester

requ	lda list+1
	bne rq1
	ldx sc_screenvek
	ldy sc_screenvek+1
	stx list
	sty list+1
rq1	inc used
	ldx #<(celst)
	ldy #>(celst)
	jsr gd_xmloop
rq0	jmp startover

; ----------------------------------------- Speed Switch

speed	lda scpuflg	; adjust speed of SuperCPU
	cmp #$3e
	bne svp1
	lda speedtog
	eor #$ff
	sta speedtog
	beq svp2
	sta speed01
	rts
svp2	sta speed20
svp1	rts

; ----------------------------------------- Define new char

mchar	ldx #7
mc0	lda listchar,x
	sta sc_undochar,x
	dex
	bpl mc0
	rts

; ----------------------------------------- Select Capture level

se_caplvl	lda capflg
	cmp #1
	bne cl0
	lda #$fe
	sta capflg
cl0	inc capflg
	lda capflg
	bmi cl1
	beq cl3
	ldx #17
	!by $2c
cl3	ldx #11
	!by $2c
cl1	ldx #5
	ldy #5
cl4	lda captxts,x
	sta lvltx,y
	dex
	dey
	bpl cl4
	ldy #2
	bne cl5

; ----------------------------------------- Select Adjust

se_togadj	lda adjflg		; toggle between 0 an $ff (0=default)
	eor #$ff
	sta adjflg
mrt0	lda adjflg
	beq mrt1
	ldx #11		; show 
	!by $2c
mrt1	ldx #5
	ldy #5
mrt3	lda adjtxts,x
	sta adjtx,y
	dex
	dey
	bpl mrt3
	iny
cl5	jsr gettab
	ldy #6
	jmp gd_xtxout1

; ----------------------------------------- init text output

gettab	lda #<(tabigad)
	sta sc_texttab
	lda #>(tabigad)
	sta sc_texttab+1
	lda (sc_texttab),y
	sta sc_screentab
	iny
	lda (sc_texttab),y
	sta sc_screentab+1
	lda #0
	tax
	tay
gt0	jsr gd_setpos
	cpx #3
	bne gt0
	stx sc_ho
	jsr gd_trim
	jmp gd_initmove

tabigad	!wo gd_togadj		; 0
	!wo gd_toglvl		; 2
	!wo gd_sync		; 4

; ----------------------------------------- re-build Mainscreen 

restrequ	ldx list
	ldy list+1
	jsr gd_screen
	sec
	rts

; ----------------------------------------- Vars

pointers	!by 0,0,0,0,0,0,0,0
	!by 0,0,0,0,0,0,0,0
	!by 0,0,0,0,0,0,0,0
listchar	!by $00,$3a,$46,$4e,$40,$40,$3c,$00
list	!wo 0
used	!by $ff
speedtog	!by 0
adjflg	!by 0
capflg	!by $ff
adjtxts	!scr "Bright Sync "		; 5, 11
captxts	!scr "Normal4Level8Level"	; 5, 11, 17
synctxts	!scr "       "		; 6
	!by 91
	!scr " left "		; 13
	!scr "right "		; 20
	!by 94
	!scr "in sync" 		; 27

; ----------------------------------------- Screenlist First Requester

celst	!by 0
	!by 5,10,18,12,$91,0,0
	!scr "ComputerEyes@"
gd_togadj	!by 8,10,9,3,$c8
	!wo se_togadj
	!by 223
adjtx	!scr "Bright@"
	!by 8,19,9,3,$cf
	!wo se_adjust
	!scr "Adjust @"
gd_toglvl	!by 11,10,9,3,$c8
	!wo se_caplvl
	!by 223
lvltx	!scr "Normal@"
	!by 11,19,9,3,$cf
	!wo se_normal
	!scr "Capture@"
	!by 14,19,9,3,$cd
	!wo se_leave
	!scr " Leave @"
	!by $80 

; ----------------------------------------- Screenlist Second Requester

synclst	!by 0
gd_sync	!by 14,10,9,3,$d7
	!wo cancel
synctx	!scr "       @"
	!by $80

; ----------------------------------------- Capture 

se_normal	jsr gd_sproff
	jsr svpointers
	lda capflg
	bmi capnorm
	beq cap4lvl

; ----------------------------------------- Capture Level 8

cap8lvl	jsr level8
	bne cpn0

; ----------------------------------------- Capture Level 4

cap4lvl	jsr level4
	bne cpn0

; ----------------------------------------- Capture normal mode

capnorm	jsr gd_sproff
	jsr normal	; capture
cpn0	jsr viewgraph	; look at it, and then gr off
	jsr svpointers
	jsr restrequ
	jsr gd_spron
	sta gr_redisp
	jmp startc	; convert to 4Bit and exit


; ----------------------------------------- Timing
; from: adjust brightness, capture

wait	sec 
w02	pha
w01	sbc #1
	bne w01
	pla
	sbc #1
	bne w02
	rts

; ----------------------------------------- Start Capture
; from: adjust brightness, capture

startcap	and graphics	; toggle screen on/off
	sta graphics

	lda graphbnk	; graphics on
	ora #$08
	sta graphbnk
	lda graphics
	ora #$20
	sta graphics

switchon	lda #$3f		; device on
	sta updirct
	sei
	rts

viewgraph	jsr getkey
	beq viewgraph
	lda graphbnk	; graphics off
	and #$f7
	sta graphbnk
	lda graphics
	and #$df
	sta graphics
	rts

; ----------------------------------------- Set Color (white on black)

setcol	lda #$00		; write color ($10) to $0400 - $07ff
	sta tmpvec
	lda #$04
	sta tmpvec+1

	ldy #$00
sc0	lda #$10
sc1	sta (tmpvec),Y
	inc tmpvec
	bne sc1
	inc tmpvec+1
	lda tmpvec+1
	cmp #$08
	bne sc0
	rts

; ----------------------------------------- Save Sprite Pointers

svpointers
	ldx #23
svp0	lda sprpntr,x
	ldy pointers,x
	sta pointers,x
	tya
	sta sprpntr,x
	dex
	bpl svp0
	rts

; ----------------------------------------- Get Key

getkey	jsr checkkeyb 	; Scnkey
	lda sc_keyprs 	; # keys pressed
	rts

; ----------------------------------------- Adjust 

jsyncit 	jmp se_syncit

se_adjust	lda adjflg
	bne jsyncit
	jsr gd_sproff

; ----------------------------------------- Adjust brightness

adjust	jsr svpointers
	lda #$07
	sta mode
	lda #$ff
	jsr startcap
	jsr setcol
	lda #$09
	sta mode+1
aj0	lda #$ff
	jsr wait
	jsr digitize
	jsr getkey
	beq aj0
	cli
	jsr viewgraph
	jsr svpointers
	stx gr_redisp
	jsr restrequ
aj1	ldx #<(celst)
	ldy #>(celst)
	jsr gd_screen
	jsr gd_spron
	clc
	rts

; ----------------------------------------- Normal Capture

normal	lda #$07
	sta mode
	lda #$ef
	jsr startcap
	jsr setcol
	lda #$09
	sta mode+1
	jsr digitize
nc0	lda #$10
	ora graphics
	sta graphics
	sta gr_redisp
	cli
	rts

; ----------------------------------------- Digitize image

digitize	lda mode
	ora #$08
	sta updata
	lda #0
	sta src
	sta src+1
	lda #$4c
	clc
	adc mode+1
	tay
dg0	jsr hshake	; Handshake, Timing
	dey
	bne dg0
dg8	lda #$80
	sta mask

dg7	jsr getkey
	bne dg1		; break if key pressed

	jsr hshake
	clc
dg6	lda scanlnlo,X
	adc src
	sta tmpvec
	lda scanlnhi,X
	beq dg2
	adc src+1
	sta tmpvec+1
dg3	bit updata
	bmi dg3
	bvc dg4
	lda mask
	ora (tmpvec),Y
	bcc dg5
dg4	lda mask
	eor #$ff
	and (tmpvec),Y
dg5	sta (tmpvec),Y
	inx
	bne dg6
dg2	lsr mask
	bne dg7

	clc		; next card
	lda src
	adc #8
	sta src
	lda src+1
	adc #0
	sta src+1
	cmp #1
	bne dg8

	lda src
	cmp #$40
	bne dg8
dg1	lda mode
	sta updata
	rts

; -----------------------------------------

hshake	lda #0		; Handshake, waiting for init
	sta dst
hs0	lda updata
	bmi hs0
	ldx #2
hs1	dex
	bne hs1
	lda updata
	bmi hs0
	lda #25		; Timing
	jsr wait
	rts

; ----------------------------------------- 200 scanlines

scanlnhi	!by $20,$20,$20,$20,$20,$20,$20,$20
	!by $21,$21,$21,$21,$21,$21,$21,$21
	!by $22,$22,$22,$22,$22,$22,$22,$22
	!by $23,$23,$23,$23,$23,$23,$23,$23
	!by $25,$25,$25,$25,$25,$25,$25,$25
	!by $26,$26,$26,$26,$26,$26,$26,$26
	!by $27,$27,$27,$27,$27,$27,$27,$27
	!by $28,$28,$28,$28,$28,$28,$28,$28
	!by $2a,$2a,$2a,$2a,$2a,$2a,$2a,$2a
	!by $2b,$2b,$2b,$2b,$2b,$2b,$2b,$2b
	!by $2c,$2c,$2c,$2c,$2c,$2c,$2c,$2c
	!by $2d,$2d,$2d,$2d,$2d,$2d,$2d,$2d
	!by $2f,$2f,$2f,$2f,$2f,$2f,$2f,$2f
	!by $30,$30,$30,$30,$30,$30,$30,$30
	!by $31,$31,$31,$31,$31,$31,$31,$31
	!by $32,$32,$32,$32,$32,$32,$32,$32
	!by $34,$34,$34,$34,$34,$34,$34,$34
	!by $35,$35,$35,$35,$35,$35,$35,$35
	!by $36,$36,$36,$36,$36,$36,$36,$36
	!by $37,$37,$37,$37,$37,$37,$37,$37
	!by $39,$39,$39,$39,$39,$39,$39,$39
	!by $3a,$3a,$3a,$3a,$3a,$3a,$3a,$3a
	!by $3b,$3b,$3b,$3b,$3b,$3b,$3b,$3b
	!by $3c,$3c,$3c,$3c,$3c,$3c,$3c,$3c
	!by $3e,$3e,$3e,$3e,$3e,$3e,$3e,$3e

scanlnlo	!by $00,$01,$02,$03,$04,$05,$06,$07
	!by $40,$41,$42,$43,$44,$45,$46,$47
	!by $80,$81,$82,$83,$84,$85,$86,$87
	!by $c0,$c1,$c2,$c3,$c4,$c5,$c6,$c7
	!by $00,$01,$02,$03,$04,$05,$06,$07
	!by $40,$41,$42,$43,$44,$45,$46,$47
	!by $80,$81,$82,$83,$84,$85,$86,$87
	!by $c0,$c1,$c2,$c3,$c4,$c5,$c6,$c7
	!by $00,$01,$02,$03,$04,$05,$06,$07
	!by $40,$41,$42,$43,$44,$45,$46,$47
	!by $80,$81,$82,$83,$84,$85,$86,$87
	!by $c0,$c1,$c2,$c3,$c4,$c5,$c6,$c7
	!by $00,$01,$02,$03,$04,$05,$06,$07
	!by $40,$41,$42,$43,$44,$45,$46,$47
	!by $80,$81,$82,$83,$84,$85,$86,$87
	!by $c0,$c1,$c2,$c3,$c4,$c5,$c6,$c7
	!by $00,$01,$02,$03,$04,$05,$06,$07
	!by $40,$41,$42,$43,$44,$45,$46,$47
	!by $80,$81,$82,$83,$84,$85,$86,$87
	!by $c0,$c1,$c2,$c3,$c4,$c5,$c6,$c7
	!by $00,$01,$02,$03,$04,$05,$06,$07
	!by $40,$41,$42,$43,$44,$45,$46,$47
	!by $80,$81,$82,$83,$84,$85,$86,$87
	!by $c0,$c1,$c2,$c3,$c4,$c5,$c6,$c7
	!by $00,$01,$02,$03,$04,$05,$06,$07  

; ----------------------------------------- textout

textout4	ldx #27		; in sync
	!by $2c
textout3	ldx #20		; right
	!by $2c
textout2	ldx #13		; left
	!by $2c
textout1	ldx #6		; blank
	ldy #6
txo4	lda synctxts,x
	sta synctx,y
	dex
	dey
	bpl txo4
	ldy #4
	jmp cl5

; ----------------------------------------- Synchronize
; adjust sync

se_syncit	ldx #<(synclst)
	ldy #>(synclst)
	jsr gd_screen
	jsr gd_sproff
	jsr svpointers
	jsr textout1

syncit	jsr switchon
	lda #$08
	sta updata
sy9	lda #0
	sta tmpvec
	sta tmpvec+1
sy6	jsr getkey
	bne sy0
	lda #0
	sta dst
sy2	inc dst
	beq sy1
	lda updata
	bpl sy2
	lda dst
	cmp #$04
	bmi sy3
	cmp #$5f
	bpl sy1
	jmp sy4

sy3	inc tmpvec
	bne sy5
	inc tmpvec+1
sy5	lda tmpvec+1
	cmp #$03
	bne sy6
	jmp sy8 

sy0 	jsr textout1
	lda #$00
	sta updata
	cli
	jsr svpointers
	jmp aj1

sy1	jsr textout2
	jmp sy9

sy11	jsr textout4
	jmp sy9

sy8 	jsr textout3
	jmp sy9

sy4 	lda #$16
	jsr wait
	lda #$64
	sta dst
sy10	lda updata
	bmi sy10
	lda #$01
	jsr wait
	lda updata
	bpl sy1 
	inc dst
	bne sy10
	beq sy11

; ----------------------------------------- 4-level capture

digit	sta mode
	jsr digitize
	jmp getkey

; ----------------------------------------- 4-level capture

level4	lda #0
	beq lv00

; ----------------------------------------- 8-level capture

level8	lda #1
lv00	pha
	lda #$ef
	jsr startcap
	lda #$09
	sta mode+1
	lda #$07
	sta mode
	jsr setcol
	jsr digitize
	lda #0
	sta mode+1
	jsr getkey
	bne lv01
	lda #$b1
	sta lv02
	jsr moveto4
	lda #$11
	sta lv02
	pla
	pha
	beq lv03
	lda #6
	jsr digit
	bne lv01
	jsr filter

lv03	lda #5
	jsr digit
	bne lv01
	jsr filter
	pla
	pha
	beq lv04
	lda #4
	jsr digit
	bne lv01
	jsr filter

lv04	lda #3
	jsr digit
lv01	bne lv05
	jsr filter
	pla
	pha
	beq lv06
	lda #2
	jsr digit
	bne lv05
	jsr filter

lv06	lda #1
	jsr digit
	bne lv05
	pla
	pha
	beq lv07
	jsr filter
	lda #0
	jsr digit
	bne lv05

lv07	lda #src
	sta lv08+1
	jsr filter
	lda #dst
	sta lv08+1

lv05	pla
	jmp nc0

; ----------------------------------

moveto4	lda #$00
	sta src
	sta tmpvec
	lda #$40
	sta tmpvec+1
	lda #$20
	sta src+1
	ldy #0
lv09	lda (tmpvec),Y
lv02	ora (src),Y	; self modified src
	sta (tmpvec),Y
	iny
	bne lv09
	inc src+1
	inc tmpvec+1
	lda src+1
	cmp #$40
	bne lv09
	rts

; ----------------------------------------- Filter

filter	lda mode
	asl
	asl
	asl
	adc #<filters
	sta tmpvec
	lda #$00
	adc #>filters
	sta tmpvec+1
	lda #$00
	sta src
	sta dst
	lda #$20
	sta src+1
	lda #$40
	sta dst+1
lv11	ldy #$07
lv10	lda (src),Y
	and (tmpvec),Y
	ora (dst),Y
lv08	sta (dst),Y
	dey
	bpl lv10
	clc
	lda src
	adc #$08
	sta src
	sta dst
	bcc lv11
	inc src+1
	inc dst+1
	lda src+1
	cmp #$40
	bne lv11
	rts

; -----------------------------------------

mode	!by 0,0
padding	!wo $adad

filters	!by $80,$04,$20,$01,$08,$40,$02,$10
	!by $90,$84,$24,$21,$09,$48,$42,$12
	!by $94,$a4,$25,$29,$49,$4a,$52,$92
	!by $95,$ac,$65,$2b,$59,$ca,$56,$b2
	!by $b5,$ad,$6d,$6b,$5b,$da,$d6,$b6
	!by $bd,$ed,$6f,$7b,$db,$de,$f6,$b7
	!by $fd,$ef,$7f,$fb,$df,$fe,$f7,$bf

clearcol	lda #$00		; clear color ram to $01 (white)
	sta tmpvec
	lda #$D8
	sta tmpvec+1
	ldy #0
cc0	lda #$01
	sta (tmpvec),Y
	iny
	bne cc0
	inc tmpvec+1
	lda tmpvec+1
	cmp #$DC
	bne cc0
	rts

; --------------------------------------- Convert Hires ($2000) to 4Bit ($4000)

; --------------------------------------- Main

startc	jsr initad	; init activity display
	jsr makevram
	lda #<vram1
	ldx #>vram1
	sta farbe
	stx farbe+1
	lda #<($1fff)
	ldx #>($1fff)
	sta bitmap
	stx bitmap+1
	lda #<($4000)
	ldx #>($4000)
	sta fourbit
	stx fourbit+1
	jsr doithi	; convert
	jsr exit		; restore pointer
	jmp se_leave
;
; ---------------------------------------

dnib	!by $00,$ff,$44,$cc
	!by $55,$aa,$11,$dd
	!by $66,$22,$99,$33
	!by $77,$ee,$88,$bb
;
; --------------------------------------- Make Video RAM

makevram	ldy #0
	lda #$10		; weiﬂ auf schwarz
vloop	sta vram,y
	sta vram+250,y
	sta vram+500,y
	sta vram+750,y
	iny
	cpy #250
	bne vloop
	rts
;
; --------------------------------------- Convert to 4Bit Hires

doithi	ldy #0
	lda #$80
	sta bytcnt
	sty yps
tr0	sta blkcnt
	inc farbe
	bne tr1
	inc farbe+1
tr1	lda farbe+1
	cmp #$d0
	beq trready
	lda (farbe),y
	pha
	lsr
	lsr
	lsr
	lsr
	tax
	lda dnib,x
	sta vorne
	pla
	and #15
	tax
	lda dnib,x
	sta hinten
bytes8	ldy #0
	inc bitmap
	bne tr2
	inc bitmap+1
tr2	lda (bitmap),y
	sta byte
	ldy yps
pixels8	asl byte
	rol
	and #1
	tax
	lda hinten,x
	tax
	stx merkbyte
	lda #$aa
	and bytcnt
	beq tr4
	txa
	and #$f0
	sta (fourbit),y
	jmp tr6
tr4	lda merkbyte
	and #15
	ora (fourbit),y
	sta (fourbit),y
	iny
tr6	lsr bytcnt
	bcc pixels8
	lda #$80
	sta bytcnt
	sty yps
	lsr blkcnt
	bcc bytes8
	clc
	lda fourbit
	adc #32
	sta fourbit
	bcc doithi
	inc fourbit+1
	dec adcnt
tr5	bne doithi
	inc y2
	inc y2+2
	lda #3
	sta adcnt
	bne tr5
trready	rts

; ---------------------------------------
;
;	sprite activity display
;
; ---------------------------------------

exit	lda spren
	and #243
	sta spren
	lda spritehi
	and #243
	sta spritehi
	lda #1
	sta gr_redisp
	clc
	rts
;
initad	ldy #60
	lda #0
adl0	sta spr255+3,y
	dey
	bpl adl0
	sty spr255
	sty spr255+1
	sty spr255+2
	lda #15
	sta sprcol2
	sta sprcol2+1
	lda sprxe
	ora #12
	sta sprxe
	lda spritehi
	ora #12
	sta spritehi
	lda #8
	sta x2
	lda #32
	sta x2+2
	lda #146
	sta y2
	sta y2+2
	lda #$ff
	sta sprptr+2
	sta sprptr+3
	lda spren
	ora #12
	sta spren
	lda #3
	sta adcnt
	rts
;
modend	!eof

