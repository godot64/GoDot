
	*= $c000
	!src "godotlib.lib"

; ----------------------------------------- 
;
; ldr.AdjustCMY
;   Modul zum Ausprobieren der CMY-Farbeinstellungen der
;   verschiedenen Farbdruckertreiber unter GoDot:
;   Canon BJC, Epson Stylus, HP Colorjet, Lexmark Optra.
;   Der enstprechende Druckertreiber muss installiert sein,
;   die neu eingestellten Werte gelten nur, solange der
;   Druckertreiber nicht erneut geladen wird. Das Modul ist
;   also nur zum *Ausprobieren* anderer Werte gedacht.
;   Will man den Druckertreiber anpassen, muss man ihn
;   mit den neuen Werten patchen.
;
; 1.00,         : first release
; 1.01, 26.05.01: ?
; 1.02, 26.05.17: added passover address of cmy table in header
; 
; -------------------------------------- 

	cmytab		= $e008	; passover address
	modname		= $e00a

	ctablex		= $e867
	ctabhp		= $e9e6
	ctabcan		= $e8d5
	ctabeps		= $e91d

	pport		= $01

	count		= $30
	valcol		= $31
	valrow		= $32
	dst		= $35	; /$36

; -------------------------------------- Header

	jmp start
	!by $80
	!by $04
	!by $00
	!wo modend
	!wo 0
	!pet "Adjust CMY color"
	!pet "1.02"
	!pet "26.05.17"
	!pet "A.Dettke        "

; -------------------------------------- Main

start	ldx #<cmylst	; Screenliste ausgeben
	ldy #>cmylst
	jsr gd_screen
	jsr setprinter	; Printerdriver erkennen
	jsr copycmy	; CMY-Daten holen

	ldy #32
	jsr settab
	jsr gettab
	ldx #2		; Index auf Textfarbe
	jsr gd_fcol	; Fenster einfärben

	stx sc_loop	; .x=0 (Höhe=16)
	lda #2		; Streifen mit Breite 2 invertieren
	sta sc_br
	jsr gd_initmove
	jsr gd_invert

	ldx #1		; Einzelfärbung
	stx sc_ho
	dex 
	stx sc_merk
-	lda gr_basepal,x	; Farbe holen
	sta sc_chgcol
	ldx #4		; Index auf geholte Farbe
	jsr gd_fcol	; färben (ganze Palette durch)
	inc sc_zl		; nächste Zeile
	inc sc_merk
	ldx sc_merk	; nächste Farbe
	cpx #16
	bcc -

	ldy #34		; Hilighting der Graus
-	sty sc_merk
	jsr gettab
	ldx #3		; Index auf Hilite-Farbe
	jsr gd_fcol	; färben
	ldy sc_merk	; nächstes Grau
	iny 
	iny 
	cpy #42		; bis alle durch
	bne -

	jsr gd_eloop	; warten

; -------------------------------------- Event: Leave

evcancel	sec 		; Modul verlassen
	rts 

; -------------------------------------- Event: (Edit) CMY-Data

evshow	lda sc_merk	; wo geklickt? (Zeile)
	clc 
	sbc valrow	; Wert in valrow wurde in copycmy gesetzt
	tax 		; als Index in die CMY-Palette

	lda sc_merk+1	; wo geklickt? (Spalte)
	clc 
	sbc valcol	; als Distributor:

	cmp #8		; war Klick auf Anzeigetexte
	bcs showvals	; nur zeigen

	cmp #6		; war Klick auf die Zahlen, dann...
	bcc +

	inc yvalues,x	; ganz rechts: Y
	lda yvalues,x
	cmp #16
	bcc showvals
	lda #0
	sta yvalues,x
	beq showvals

+	cmp #$03		; Mitte: M
	bcc +
	inc mvalues,x
	lda mvalues,x
	cmp #16
	bcc showvals
	lda #0
	sta mvalues,x
	beq showvals

+	inc cvalues,x	; sonst: C
	lda cvalues,x
	cmp #16
	bcc showvals
	lda #0
	sta cvalues,x

showvals	jsr settab	; und anzeigen
	ldy #15		; 16 (mal 3) Stück
-	sty count
	lda cvalues,y	; get C
	jsr mkdec		; to decimal
	stx record
	sta record+1
	lda mvalues,y	; get M
	jsr mkdec		; to decimal
	stx record+3
	sta record+4
	lda yvalues,y	; get Y
	jsr mkdec		; to decimal
	stx record+6
	sta record+7

	tya 		; Index mal 2
	asl
	tay 
	jsr gettab	; für Ausgabe-Adressen
	ldx #<record
	ldy #>record
	jsr gd_xtxout2	; anzeigen

	ldy count		; nächster Record
	dey 
	bpl -
	clc 
	rts 

; -------------------------------------- Convert Value to Decimal

mkdec	jsr gd_xcnvdez
	cpx #$30		; leading Null?
	bne +
	ldx #$20		; ersetzen mit Space
+	rts 

; -------------------------------------- Set Printer's CMY Table Address

cmytabo	lda cmytab+1
	ora #$20
	tay
	lda cmytab
	cpy #$20
	rts

setprinter
	jsr godotoff	; RAM
	ldx #0
-	lda modname,x	; Name des Drivers checken (drei Zeichen)
	cmp lexmark,x	; Lexmark?
	bne +
	inx 
	bne -

+	cpx #3
	bne +
	jsr cmytabo
	bne spr0
	lda #<ctablex	; ja, Tabellenadresse angeben
	ldy #>ctablex
	bne spr0

+	ldx #0		; sonst:
-	lda modname,x
	cmp hpackard,x	; HP-Druckertreiber?
	bne +
	inx 
	bne -

+	cpx #3
	bne +
	jsr cmytabo
	bne spr0
	lda #<ctabhp	; ja, Tabellenadresse angeben
	ldy #>ctabhp
	bne spr0

+	ldx #0		; sonst:
-	lda modname,x
	cmp canon,x	; Canon?
	bne +
	inx 
	bne -

+	cpx #3
	bne +
	jsr cmytabo
	bne spr0
	lda #<ctabcan	; ja, Tabellenadresse angeben
	ldy #>ctabcan
	bne spr0

+	ldx #0		; sonst: 
-	lda modname,x
	cmp epson,x	; Epson?
	bne +
	inx 
	bne -

+	cpx #3
	bne +
	jsr cmytabo
	bne spr0
	lda #<ctabeps	; ja, Tabellenadresse angeben
	ldy #>ctabeps

spr0	sta dst		; eintragen
	sty dst+1
	jsr godoton
	clc 
	rts 

+	lda #<ctabtemp	; keiner davon? 
	ldy #>ctabtemp	; dann leere Tabelle angeben
	bne spr0

; -------------------------------------- CMY-Tabelle laden

copycmy	jsr godotoff	; RAM
-	lda (dst),y	; dreimal 16 Werte holen
	sta cvalues,y
	iny 
	cpy #48
	bne -
	jsr godoton
	jsr showvals	; anzeigen
	lda txshow	; Zeile und Spalte des Datenfensters holen
	sta valrow
	lda txshow+1
	sta valcol
	clc 
	rts 

; -------------------------------------- Event: Neue CMY-Tabelle zurückschreiben

evupdate	jsr godotoff	; .y=0
-	lda cvalues,y
	sta (dst),y
	iny 
	cpy #48
	bne -

; -------------------------------------- GoDot einschalten

godoton	lda #$36
	sta pport
	cli 
	rts 

; -------------------------------------- GoDot ausschalten

godotoff	sei 
	lda count
	sta pport
	ldy #0
	rts 

; -------------------------------------- CMY-Tabelle

cvalues	!by $00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00 
mvalues	!by $00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00 
yvalues	!by $00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00 

; -------------------------------------- Leere CMY-Tabelle

ctabtemp	!by $00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00 
	!by $00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00 
	!by $00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00

; -------------------------------------- Textanzeige Handler

tabigad 	!wo txblk		; 0
	!wo txblu 	; 2
	!wo txbwn 	; 4
	!wo txdgr 	; 6
	!wo txred		; 8
	!wo txpur		; 10
	!wo txora 	; 12
	!wo txmgr 	; 14
	!wo txlbl		; 16
	!wo txlrd 	; 18
	!wo txgrn 	; 20
	!wo txlgr 	; 22
	!wo txcya		; 24
	!wo txyel		; 26
	!wo txlgn 	; 28
	!wo txwht 	; 30
	!wo txwindow 	; 32
	!wo hlblk		; 34
	!wo hlgr1 	; 36
	!wo hlgr2		; 38
	!wo hlgr3		; 40
	!wo hlwht		; 42

settab	lda #<tabigad 
	sta sc_texttab
	lda #>tabigad
	sta sc_texttab+1
	rts 

gettab	lda (sc_texttab),y
	sta sc_screentab
	iny 
	lda (sc_texttab),y
	sta sc_screentab+1
	lda #0
	tax 
	tay 
gt0	jsr gd_setpos
	cpx #4
	bne gt0
	jsr gd_trim
	jmp gd_initmove

; -------------------------------------- Daten

record	!scr "00 00 00@"

lexmark	!pet "Lex"
hpackard	!pet "HP "
canon	!pet "Can"
epson	!pet "Eps"

; -------------------------------------- Orte der Farbanzeigen

txblk	!by $03,$0b,$0a,$03 
txblu	!by $04,$0b,$0a,$03 
txbwn	!by $05,$0b,$0a,$03 
txdgr	!by $06,$0b,$0a,$03 
txred	!by $07,$0b,$0a,$03 
txpur	!by $08,$0b,$0a,$03 
txora	!by $09,$0b,$0a,$03 
txmgr	!by $0a,$0b,$0a,$03 
txlbl	!by $0b,$0b,$0a,$03 
txlrd	!by $0c,$0b,$0a,$03 
txgrn	!by $0d,$0b,$0a,$03 
txlgr	!by $0e,$0b,$0a,$03 
txcya	!by $0f,$0b,$0a,$03 
txyel	!by $10,$0b,$0a,$03 
txlgn	!by $11,$0b,$0a,$03 
txwht	!by $12,$0b,$0a,$03 
hlblk	!by $03,$05,$05,$03 
hlgr1	!by $06,$05,$05,$03
hlgr2	!by $0a,$05,$05,$03
hlgr3	!by $0e,$05,$05,$03 
hlwht	!by $12,$05,$05,$03

; -------------------------------------- Screenlist

cmylst	!by $00
	!by 00,02,20,25,$81
	!wo 0 
	!scr "Adjust Colors@"
txshow	!by 03,11,10,18,$60
	!wo evshow
txwindow	!by 03,03,18,18,$20
	!wo 0 
	!by 21,06,08,03,$c0
	!wo evupdate
	!scr "Update@"
	!by 21,14,07,03,$c0
	!wo evcancel
	!scr "Leave@"
	!by $c0,01,03,06
	!scr "Colors@"
	!by $c0,01,11,07
	!scr "C  M  Y@"
	!by $c0,03,03,07	; 16mal:
	!scr "  blk"	; Farbname
	!by $e0,$e0,$00 	; plus zwei Zeichen Dither-Muster
	!by $c0,04,03,07
	!scr "  blu"
	!by $e1,$e1,$00 
	!by $c0,05,03,07
	!scr "  bwn"
	!by $e2,$e2,$00 
	!by $c0,06,03,07
	!scr "  gr1"
	!by $e3,$e3,$00 
	!by $c0,07,03,07
	!scr "  red"
	!by $e4,$e4,$00 
	!by $c0,08,03,07
	!scr "  pur"
	!by $e5,$e5,$00 
	!by $c0,09,03,07
	!scr "  ora"
	!by $e6,$e6,$00 
	!by $c0,10,03,07
	!scr "  gr2"
	!by $e7,$e7,$00 
	!by $c0,11,03,07
	!scr "  lbl"
	!by $e8,$e8,$00 
	!by $c0,12,03,07
	!scr "  lrd"
	!by $e9,$e9,$00 
	!by $c0,13,03,07
	!scr "  grn"
	!by $ea,$ea,$00 
	!by $c0,14,03,07
	!scr "  gr3"
	!by $eb,$eb,$00 
	!by $c0,15,03,07
	!scr "  cya"
	!by $ec,$ec,$00
	!by $c0,16,03,07
	!scr "  yel"
	!by $ed,$ed,$00 
	!by $c0,17,03,07
	!scr "  lgr"
	!by $ee,$ee,$00 
	!by $c0,18,03,07
	!scr "  wht"
	!by $ef,$ef,$00 
	!by $80 

; -------------------------------------- 

modend	!eof
