
; excerpted from mod.ClipWorks
;

	col	= $33	; sample location
	zeile	= sc_merk
	spalte	= zeile+1

; ------------------------------------------------ Event
; 	...
;	ldx #<scrnlst	; show screenlist
;	ldy #>scrnlst
;	jsr gd_screen	; returns .a=0

	sta sc_loop	; care for text mode
	ldx palgad	; palette gadget values
	inx
	stx palzl
	stx sc_zl
	ldx palgad+1
	inx
	stx palsp
	stx sc_sp
	ldx #4		; 4x4 area (color chooser)
	stx sc_br
	stx sc_ho
	jsr gd_esm3	; compute address and invert

	lda gr_picked	; get previously selected color to .x
	and #15		; (will be marked by "+")
	tax
	jsr initpal	; and show colors

;	jmp gd_eloop	; wait for clicks (from palgad)
;	...

; ------------------------------------------------

; ------------------------------------------------ palgad-Event: Pick Color

evgetcol	sec		; compute row where user clicked
	lda zeile
	sbc palzl
	asl
	asl
	sta hold
	sec		; compute column
	lda spalte
	sbc palsp
	clc
	adc hold
	tax		; take as index into palette
	lda gr_basepal,x
	tax
	ora #$80		; sign valid
	sta gr_picked
initpal	lda c64pal,x	; re-display palette
	sta col
	jsr makepal
	clc
	rts

; ------------------------------------------------ create palette chooser

makepal	lda gr_picked	; get current color
	and #15
	sta plast		; and store

	lda palsp		; create chooser, first column
	sta sc_sp
	pha
	ldx #1		; width and height = 1
	stx sc_br
	stx sc_ho
	dex
	stx cnty		; counters = 0
	stx hold

mp1	stx cntx
	ldy hold
	jsr colorize	; colorize area (1x1)
	sec		; is PICKED color?
	lda sc_vekt20
	sbc #40
	sta sc_vekt20
	lda sc_vekt20+1
	sbc #$d4
	sta sc_vekt20+1
	ldy sc_chgcol
	cpy plast
	beq mp2
	lda #160		; no, write shift space
	bne mp3
mp2	lda #171		; yes, write "+"
mp3	ldy #0
	sta (sc_vekt20),y
	inc sc_sp		; next column
	inc hold
	ldx cntx
	inx
	cpx #4
	bne mp1

	pla		; re-get start column
	pha
	sta sc_sp
	ldx #0
	inc sc_zl		; next row
	inc cnty
	lda cnty
	cmp #4
	bne mp1

	pla		; clear stack
	rts		; finished

colorize	lda gr_basepal,y	; get color
	sta sc_chgcol
	ldx #4
	jmp gd_fcol	; colorize area (1x1)

; ------------------------------------------------ Add to scrnlst:

;	...

palgad	!by 15,2,6,6,$40	; sample location
	!wo evgetcol

;	...

; ------------------------------------------------ Data

palzl	!by 0
palsp	!by 0
plast	!by 0
hold	!by 0
cntx	!by 0
cnty	!by 0

c64pal	!by $00,$ff,$44,$cc,$55,$aa,$11,$dd
	!by $66,$22,$99,$33,$77,$ee,$88,$bb

